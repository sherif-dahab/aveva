-------------------------------------------------------------------
--
-- Copyright 1974 to current year. AVEVA Solutions Limited and its subsidiaries. All rights reserved in original code only.
--
-------------------------------------------------------------------
--
--  File:            mdsTrunnion.pmlobj
--  Author:          Richard Martin
--  Created:         16 July 2014
--    Type:          Object Definition
--    Group:         Undefined
--      Keyword:     Undefined
--    Module:        Undefined
--
--  Description:  Object for creating all trunnion types in the supports application
--
------------------------------------------------------------------------
--
-- Methods defined:
--
--  Method call                      Return              Description
--  ===========                      ======              ===========
--  activateOffset(BOOLEAN)          -                   if the trunnion has an offset this swaps it when we move
--  addComponent(STRING)             -
--  calculateMakeupOffset(DBREF)     REAL                calculate the makeup piece offset value
--  changeTrunnionBore(GADGET,STRING) BOOLEAN             changes trunnion bore
--  changeTrunnionDirection(GADGET,STRING) BOOLEAN             changes trunnion direction
--  createBasicTrunni(DIRECTION)     BOOLEAN             create the trunni hierarchy appropriate to !this.onType
--  createBranDbHierarchy()          BOOLEAN             create the DB hierarchy for the branch (i.e. when selected element is a branch)
--  createDbHierarchy()              BOOLEAN             create the necessary database structure to define a trunnion
--  createElboDbHierarchy()          BOOLEAN             create the DB hierarchy for the elbow
--  createExtraTranci(STRING)        -                   create extra TRANCI as last member of trunnion when adding additional ancillary from form
--  createMakeup()                   -                   create the makeup piece common to most trunnions
--  createPlat(STRING,STRING)        BOOLEAN             common plate creation method
--  createReduDbHierarchy()          BOOLEAN             create the DB hierarchy for the reducer
--  createTeeDbHierarchy()           BOOLEAN             create the DB hierarchy for the tee
--  createTrunni(DIRECTION)          BOOLEAN             create the trunni hierarchy appropriate to !this.onType
--  createTrunniTT06(DIRECTION)      BOOLEAN             create the trunni hierarchy for TT06 and TT07
--  createTrunniTT08(DIRECTION)      BOOLEAN             create the trunni hierarchy for TT08 and TT09
--  createTrunniTT10(DIRECTION)      BOOLEAN             create the trunni hierarchy for TT10 and TT11
--  createTrunniTT12(DIRECTION)      BOOLEAN             create the trunni hierarchy for TT12 and TT13
--  createTrunniTT14(DIRECTION)      BOOLEAN             create the trunni hierarchy appropriate to !this.onType
--  createTrunniTT15(DIRECTION)      BOOLEAN             create the trunni hierarchy appropriate to !this.onType
--  createTrunniTT16(DIRECTION)      BOOLEAN             create the trunni hierarchy appropriate to !this.onType
--  createTrunniTT17(DIRECTION)      BOOLEAN
--  createTrunniTT20(DIRECTION)      BOOLEAN             create the trunni hierarchy appropriate to !this.onType
--  createTrunniTT21(DIRECTION)      BOOLEAN
--  createTrunniTT22(DIRECTION)      BOOLEAN             create the trunni hierarchy for TT22, TT28, TT29, TT30, TT35, TT40 and TT41
--  createTrunniTT23(DIRECTION)      BOOLEAN             create the trunni hierarchy appropriate to !this.onType
--  createTrunniTT24(DIRECTION)      BOOLEAN
--  createTrunniTT25(DIRECTION)      BOOLEAN             create the trunni hierarchy appropriate to !this.onType
--  createTrunniTT26(DIRECTION)      BOOLEAN             create the trunni hierarchy appropriate to !this.onType
--  createTrunniTT27(DIRECTION)      BOOLEAN
--  createTrunniTT33(DIRECTION)      BOOLEAN             create the trunni hierarchy appropriate to !this.onType
--  createTrunniTT35(DIRECTION)      BOOLEAN
--  createTrunniTT40(DIRECTION)      BOOLEAN             create the trunni hierarchy for TT40 and TT41
--  createTrunniTT44(DIRECTION)      BOOLEAN             create the trunni hierarchy appropriate to !this.onType
--  createTrunnion()                 -                   create the trunnion hierarchy based upon the selected elements to support
--  createTubeDbHierarchy()          BOOLEAN             create the DB hierarchy for the branch (i.e. when selected element is a branch)
--  deleteComponent()                -                   delete trunnion base component, e.g. isolation block
--  dimensionChangeAN640(DBREF)      -                   Modifies the dimensions of AN640-AN670 ancillary if the plate it is attached to is modified
--  drawAids()                       -                   draws graphical aids
--  elboInTube()                     -                   if the trunnion is supporting an elbo change the hierarchy so that it is supporting tube and visa-versa
--  elementChecks()                  BOOLEAN             perform checks on selected element
--  finalise()                       -                   set material properties and shop flags etc before finishing creation
--  fromPreliminarySupport()         STRING              Alternative method to calling mdscreatesuppelement if a trunnion is from a preliminary
--  getElbowConnectionPoint()        POSITION
--  getOrthogDir()                   DIRECTION           Get the orthogonal direction for HORI and VERT trunnions
--  getTrunniConnectionPoint()       POSITION            get the connection point for the TRUNNI element. this was previously defined as a P3 point on the element but the new point must be constructed from available data
--  inTube()                         -                   toggle from supporting component (i.e. ELBO) to TUBE
--  initialisePlates(ARRAY,ARRAY)    -                   for bores and stypes where there are multiple bores and multiple plates
--  mdstrunnion(STRING,ARRAY)        -
--  mdstrunnion(STRING,DBREF)        -
--  modOffset()                      -                   change offset for dummy leg trunnion
--  modStanchion()                   -                   For stanchions TT25, TT33 and TT34 modifies geometry based on the clearance direction values on the form
--  modWeldInsertionLength(DBREF)    -                   To deal with special case of the Insertion Length property of a collar
--  modifyTranciComponent(STRING)    REAL
--  pipeChecks()                     BOOLEAN             perform checks on pipe
--  posStanchion(BOOLEAN)            -                   Position stanchions such as TT25, TT33 and TT34 (alternative to setTrunLength)
--  presetDespars(STRING,STRING)     BOOLEAN
--  reduInTube()                     -                   if the trunnion is supporting a reducer change the hierarchy so that it is supporting tube and visa-versa
--  runChecks()                      BOOLEAN             check pipe slope, materials etc
--  secondPick(STRING,STRING)        BOOLEAN             Some trunnions require a secondary pick
--  selectAddedComponent(STRING)     -
--  selectSpref(STRING,STRING)       BOOLEAN             Selects spref from passed stype and halts creation if any errors are encountered
--  setCurrentBorePlate(STRING,STRING) REAL                matches the current plate and bore combination
--  setSSREFEDespars(DBREF)          -                   Set the despars for the trunnion on the SSREFE
--  setTrunLength(BOOLEAN)           -                   set the length of the trunnion
--  setupElementOffsets()            -                   set up the elementOffsets array that contains reconnect offsets for the softtype
--  switchAT29(STRING)               -                   When switching a trunnion between in elbow and in tube, switch AT29 from A to B and vice versa
--  teeInTube()                      -                   if the trunnion is supporting a tee change the hierarchy so that it is supporting tube and visa-versa
--  trunDirections()                 STRING              returns direction of trunnion type
--  typesAllowedOn()                 ARRAY               returns types trunnion is allowed on
--  updateTABFormGadgets(DBREF)      -
--  validateInTubeDist()             REAL                validates the trunnion to check whether it fits in the tube
--  validateTT20()                   BOOLEAN             perform checks on elbow and pipe for TT20
--  validateTrunnion(STRING)         BOOLEAN             validates trunnion,
--
define object MDSTRUNNION

  member .softType is STRING
  member .elementToSupport is DBREF
  member .secondElementToSupport is DBREF
  member .onType is STRING
  member .elementAdir is DIRECTION
  member .elementLdir is DIRECTION
  member .elementABore is BORE
  member .elementLBore is BORE
  member .elementAPos is POSITION
  member .elementLPos is POSITION
  member .elementP0Pos is POSITION
  member .elementOwningBran is DBREF
  --workingBore is the bore that we will use to define the trunni bore and connection (in the case of a REDU it's the smallest bore. in all other cases its the elementLBore bore)
  member .workingBore is BORE
  member .mdsTrunnionObject is MDSREADTRUNDATA

  --members for storing trunnion bore options that will be assigned to the mdssupporteditor form (in the bore selection list gadget)
  member .availableBores is ARRAY
  --available bores with units appended for display purposes
  member .availableBoresUnits is ARRAY

  --members that are only filled in after the createTrunni method is run
  member .suppo  is DBREF
  member .trunni is DBREF
  member .tranci is DBREF
  member .ssrefe is MDSSSREFE

  member .basePlate is DBREF
  member .bearingPlate is DBREF
  member .collar       is DBREF

  --define member to store default offsets for each component in the TRUNNI (this is used when we are setting the TRUNNI length to determine how to reconnect components)
  --used to be .setup on the mdsTT forms
  member .elementOffsetConfig is ELEMENTOFFSETCONFIG
  member .elementOffsets is ARRAY

  --storage member that keeps track of all elements created on the trunnion branch. (originally stored direct on the form as form.support.atta
  --but we will store this member list in the trunnion object that is now attached to the supporteditor form
  member .atta is ARRAY

  --define members to store the state of the trunnion with respect to the tube
  --inTube.eq(true) means that the trunnion is positioned in the tube away from p-leave of the selected element to support
  member .inTube is BOOLEAN
  member .inTubeDist is REAL

  --member to tell the support editor form whether or not the distance box should be active
  member .distanceActive is BOOLEAN

  member .inTubeToggleActive is BOOLEAN

  member .onlyAllowInTube is BOOLEAN

  member .hori is BOOLEAN

  member .dummyLegOffset is REAL

  member .baseCompExists is BOOLEAN

  member .fromPrelim is BOOLEAN

endobject

----------------------------------------------------------------------
--
-- Method:      MDSTRUNNION
--
-- Description: constructor method for the object
--
----------------------------------------------------------------------
define method .mdstrunnion(!softType is STRING, !elementsToSupport is ARRAY)

  !this.softType = !softType
  !this.hori = FALSE
  !this.elementOffsetConfig = object ELEMENTOFFSETCONFIG()
  --set the current elementOffsets array
  !this.setupElementOffsets()
  !this.dummyLegOffset = REAL()
  !this.baseCompExists = FALSE

  if (!elementsToSupport[1].type.eq('TUBI')) then
    !elementsToSupport[1] = !elementsToSupport[1].owner
  endif

  !this.elementToSupport = !elementsToSupport[1]

  if (!this.elementToSupport.type.eq('ANCI')) then
    !this.fromPrelim = !this.elementToSupport.spref.badRef().not() and !this.elementToSupport.spref.name.match('MDP/PR').gt(0)
  else
    !this.fromPrelim = FALSE
  endif

  --if the element to support type is any of the named types below then the ontype is the named type because we are supporting that element
  --if it is any other type then treat it as a branch because we will be supporting the leave type rather than the component.

  if (!this.elementToSupport.type inset ('ELBO','TEE','REDU')) then
    !this.onType = !this.elementToSupport.type
  elseif (!this.elementToSupport.type.eq('BRAN')) then
    !this.onType = 'BRAN'
  elseif (!this.fromPrelim and !this.elementToSupport.compref.type.eq('BRAN')) then
    !this.onType = 'BRAN'
  else
    !this.onType = 'TUBE'
  endif

  --set up standard attribute members for element to support
  if (!this.onType.eq('BRAN')) then
    !this.elementAdir = !this.elementToSupport.hdir.opposite().wrt( /* )
    !this.elementLdir = !this.elementToSupport.hdir.wrt( /* )
    !this.elementABore = !this.elementToSupport.hbor
    !this.elementLBore = !this.elementToSupport.hbor
    !this.elementAPos = !this.elementToSupport.hpos.wrt( /* )
    !this.elementLPos = !this.elementToSupport.hpos.wrt( /* )
    !this.elementOwningBran = !this.elementToSupport
  else
    !this.elementAdir = !this.elementToSupport.adir.wrt( /* )
    !this.elementLdir = !this.elementToSupport.ldir.wrt( /* )
    !this.elementABore = !this.elementToSupport.abor
    !this.elementLBore = !this.elementToSupport.lbor
    !this.elementAPos = !this.elementToSupport.apos.wrt( /* )
    !this.elementLPos = !this.elementToSupport.lpos.wrt( /* )
    !this.elementP0Pos = !this.elementToSupport.pPos[0].wrt( /* )
    if (!this.fromPrelim) then
      !this.elementOwningBran = !this.elementToSupport.compref.owner
    else
      !this.elementOwningBran = !this.elementToSupport.owner
    endif
  endif

  -- Get Session Bore Units
  if(!!kGetBoreUnits()) then
    !boreUnits = 'inch bore'
  else
    !boreUnits = ''
  endif

  --define the working bore
  --workingBore is the bore that we will use to define the trunni bore and connection (in the case of a REDU it's the smallest bore. in all other cases its the elementLBore bore or elementABore for a BRAN)
  if (!this.elementToSupport.type.eq('REDU')) then
    --Smallest Bore Size
    if (!this.elementABore.gt(!this.elementLBore)) then
      !this.workingBore = !this.elementLBore
    else
      !this.workingBore = !this.elementABore
    endif
  elseif (!this.elementToSupport.type.eq('BRAN')) then
    !this.workingBore = !this.elementABore
  else
    !this.workingBore = !this.elementLBore
  endif

  --setup the mdsTrunnionObject member
  !this.mdsTrunnionObject  = object MDSREADTRUNDATA()

  --specify a default 200mm distance to use if we are supporting tube instead of a component. This will be updated by the supporteditor
  --when the distance is changed in the control

  !tubLen = itlength of il tube of $!<this.elementToSupport>
  handle any
    !distance = 0mm
  elsehandle none
    --Are we creating a support on a trunnion baseplate?
    if (!tubLen.lt(200mm)) then
      !distance = !tubLen
    else
      !distance = 200mm
    endif
  endhandle

  if (!!mdsSupportEditor.previousSupport.badRef().not()) then
    var !extraDistanceFromOrigin construct distance p0 of $!this.elementToSupport to p0 of $!!mdsSupportEditor.previousSupport
    !this.inTubeDist = !distance + !extraDistanceFromOrigin.real()
  elseif (!this.fromPrelim) then
    !this.inTubeDist = 0mm
  else
    if (!!mdsSupportEditor.distance.val OR !this.onType.eq('BRAN')) then
      !this.inTubeDist = !distance
    else
      !ref = !!ce
      !!ce = !this.elementToSupport
      var !extraDistanceFromOrigin construct distance p0 to pl
      !this.inTubeDist = !distance + !extraDistanceFromOrigin.real()
      !!ce = !ref
    endif
  endif

endmethod

----------------------------------------------------------------------
--
-- Method:      MDSTRUNNION
--
-- Description: constructor method for the object for a pre existing trunnion
--
----------------------------------------------------------------------
define method .mdstrunnion(!softType is STRING, !trunni is DBREF)

  if (!trunni.type.neq('TRUNNI')) then
    return
  endif

  !supportMembers = object MDSSUPPORTMEMBERS(!trunni)

  !this.fromPrelim = FALSE
  !tranci = !supportMembers.tranci[1]
  !this.tranci = !tranci
  !this.trunni = !trunni
  !this.suppo = !trunni.owner
  !ssrefe = $!this.suppo.name/SREF.1
  !this.ssrefe = object MDSSSREFE(!ssrefe)

  var !trancis collect all TRANCI for $!<this.trunni>

  if (!trancis.size().eq(2)) then
    !this.baseCompExists = TRUE
  else
    !this.baseCompExists = FALSE
  endif

  -- get the current offset
  --revisit maybe the offset should be a property of the MDSSSREFE object
  !this.dummyLegOffset = !this.ssrefe.ssrefe.desp[4]
  handle any
    -- desp 4 not set
    !this.dummyLegOffset = 0
  endhandle

  --if hori support then set hori member to true
  !upDir = U

  if (!this.trunni.hdir.isParallel(!upDir).not()) then
    !this.hori = TRUE
  else
    !this.hori = FALSE
  endif

  !this.softType = !softType
  !this.elementOffsetConfig = object ELEMENTOFFSETCONFIG()
  --set the current elementOffsets array
  !this.setupElementOffsets()

  !this.elementToSupport = !tranci.compref

  --set up standard attribute members for element to support
  if (!this.elementToSupport.type.eq('BRAN')) then
    !this.elementAdir = !this.elementToSupport.hdir.opposite().wrt( /* )
    !this.elementLdir = !this.elementToSupport.hdir.wrt( /* )
    !this.elementABore = !this.elementToSupport.hbor
    !this.elementLBore = !this.elementToSupport.hbor
    !this.elementAPos = !this.elementToSupport.hpos.wrt( /* )
    !this.elementLPos = !this.elementToSupport.hpos.wrt( /* )
    !this.elementOwningBran = !this.elementToSupport
  else
    !this.elementAdir = !this.elementToSupport.adir.wrt( /* )
    !this.elementLdir = !this.elementToSupport.ldir.wrt( /* )
    !this.elementABore = !this.elementToSupport.abor
    !this.elementLBore = !this.elementToSupport.lbor
    !this.elementAPos = !this.elementToSupport.apos.wrt( /* )
    !this.elementLPos = !this.elementToSupport.lpos.wrt( /* )
    !this.elementP0Pos = !this.elementToSupport.pPos[0].wrt( /* )
    !this.elementOwningBran = !this.elementToSupport.owner
  endif

  --if the element to support type is any of the named types below then the ontype is the named type because we are supporting that element
  --if it is any other type then treat it as a branch because we will be supporting the leave type rather than the component.
  if (!this.elementToSupport.type inset ('ELBO','TEE','REDU')) then
    !this.onType = !this.elementToSupport.type
    !connectionPoint = !this.getTrunniConnectionPoint()
    --Convert positions to strings to account for the change in decimal place accuracy between positions
    if (!connectionPoint.wrt( /* ).string() EQ !this.tranci.pos.wrt( /* ).string()) then
      !this.inTube = false
      !this.distanceActive = false
    else
      !this.inTube = true
      !this.distanceActive = true
    endif
  else
    !this.inTube = true
    if (!this.elementToSupport.type.neq('BRAN')) then
      !this.onType = 'TUBE'
    else
      !this.onType = 'BRAN'
    endif
    !this.distanceActive = true
  endif

  -- Get Session Bore Units
  if(!!kGetBoreUnits()) then
    !boreUnits = 'inch bore'
  else
    !boreUnits = ''
  endif

  --Find configuration of base plates and bearing plates for trunnion
  var !plates coll all PLAT for $!this.suppo

  --Trunnions with a bearing plate as well as a base plate
  if (!softType inset('TT05','TT07','TT09','TT11','TT13','TT19','TT28')) then
    !this.basePlate = !plates[!plates.size() - 1].dbref()

    if (!this.basePlate.styp inset('AT59','AT68')) then
      !this.basePlate = !plates[!plates.size() - 2].dbref()
    endif

    !this.bearingPlate = !plates[!plates.size()].dbref()

    --TT27 has a lot of plates but we only want the first one
  elseif (!softType inset ('TT27')) then
    !this.basePlate = !plates[1].dbref()

    --All other trunnions have only a base plate
  else
    !this.basePlate = !plates[!plates.size()].dbref()
    if (!this.basePlate.styp inset('AT59','AT68')) then
      !this.basePlate = !plates[!plates.size() - 1].dbref()
    endif
  endif

  do !element values !trunni.members
    if (!element.type.eq('TUBI')) then
      skip
    endif
    !this.atta.append(!element)
  enddo

  --define the working bore
  --workingBore is the bore that we will use to define the trunni bore and connection (in the case of a REDU it's the smallest bore. in all other cases its the elementLBore bore or elementABore for a BRAN)
  if (!this.elementToSupport.type.eq('REDU')) then
    --Smallest Bore Size
    if (!this.elementABore.gt(!this.elementLBore)) then
      !this.workingBore = !this.elementLBore
    else
      !this.workingBore = !this.elementABore
    endif
  elseif (!this.elementToSupport.type.eq('BRAN')) then
    !this.workingBore = !this.elementABore
  else
    !this.workingBore = !this.elementLBore
  endif

  -- Run validation to deactivate in tube toggle if not valid
  !this.validateTrunnion('MODIFY')

  --set up the mdsTrunnionObject member
  !this.mdsTrunnionObject  = object MDSREADTRUNDATA()
  !this.mdsTrunnionObject.mdsTrunData(!this.softType, !this.trunni.hbor.real().value())

  !availBores = !this.mdsTrunnionObject.bores
  !unitBore = ARRAY()
  do !setUnitBores from 1 to !availBores.size()
    !mmBore = !availBores[$!setUnitBores] & 'mm'
    !unitBore.append(!mmBore.bore(!!borefmt).string())
  enddo
  !this.availableBoresUnits = !unitBore
  !this.availableBores = !availBores

  --specify a default 200mm distance to use if we are supporting tube instead of a component. This will be updated by the supporteditor
  --when the distance is changed in the control

  !tubLen = itlength of il tube of $!<this.elementToSupport>
  handle any
    !distance = 0mm
  elsehandle none
    --Are we creating a support on a trunnion baseplate?
    if (!tubLen.lt(200mm)) then
      !distance = !tubLen
    else
      !distance = 200mm
    endif
  endhandle

  if (!!mdsSupportEditor.distance.val) then
    !this.inTubeDist = !distance
  else
    !ref = !!ce
    !!ce = !this.elementToSupport
    var !extraDistanceFromOrigin construct distance p0 to pl
    handle (2,201)
      -- we are at a branch
      !extraDistanceFromOrigin = '0'
    endhandle
    !this.inTubeDist = !distance + !extraDistanceFromOrigin.real()
    !!ce = !ref
  endif

endmethod

----------------------------------------------------------------------
--
-- Method:      addComponent
--
-- Description: add trunnion component, e.g. isolation pad
--
----------------------------------------------------------------------
define method .addComponent(!type is STRING)

  --set callback on mdsancill to run method in here instead of mdscreateancillary (ancillary is not created yet!) - this normally done in mdsSelectAncillary
  --run !!mdsITrunnionAncill trunnion specific version of mdsiancill - pass this the TRUNNI, in here use validate c# object to get list into form
  --ancillary is selected and apply pressed
  --method in here creates anci (using mdsCreateSuppElement?) if anci
  --OR creates plat if plat
  --runs mdscreateancillary to select spref of component
  !!mdsAncill.action.clear()
  !softType = !this.softType
  if (!this.inTube) then
    !comp = 'TUBE'
  else
    !comp = !this.onType
  endif

  if (!type.eq('BASE')) then
    !!mdsAncill.action[1] = |!!mdsSupportEditor.support.trunnion.selectAddedComponent('BASE')|
    !!mdsITrunnionAncill('$!softType', !this.basePlate, 'BASE', !comp)
  elseif (!type.eq('HEAD')) then
    !!mdsAncill.action[1] = |!!mdsSupportEditor.support.trunnion.selectAddedComponent('HEAD')|
    !!mdsITrunnionAncill('$!softType', !this.tranci, 'HEAD', !comp)
  elseif (!type.eq('TAIL')) then
    !!mdsAncill.action[1] = |!!mdsSupportEditor.support.trunnion.selectAddedComponent('TAIL')|
    !!mdsITrunnionAncill('$!softType', !this.tranci, 'TAIL', !comp)
  endif

endmethod

----------------------------------------------------------------------
--
-- Method:      selectAddedComponent
--
-- Description: add trunnion component, e.g. isolation pad
--
----------------------------------------------------------------------
define method .selectAddedComponent(!type is STRING)

  !selectedElementType = !!mdsAncill.currRtext
  --!selectedElement = !!mdsAncill.selectedElement
  using namespace 'Aveva.Supports.Presentation.DimensionControl'
  if (!type.eq('BASE')) then
    --create new tranci at base (using !this.createExtraTranci)
    !this.createExtraTranci(!selectedElementType)
    !tabToAddTo = !!mdsSupportEditor.dimContDBref.size() + 1
  else
    --modify tranci at top (using !this.modifyTranciComponent)
    !tabToAddTo = !this.modifyTranciComponent(!selectedElementType)
  endif

  --run mdscreateancillary to select ancillary/plate/guide and run validation
  !!mdsCreateAncillary(!this.softType)

  -- Use dummy string to pass stype, as it will be done in addSuppToTab
  !nullString = ''
  !!mdsSupportEditor.addSuppToTab(!!ce,!nullString,!!mdsSupportEditor.dimCont$!tabToAddTo,!!mdsSupportEditor.fDimCont$!tabToAddTo)
  !!mdsSupportEditor.updateDistanceValue(!!mdsSupportEditor.distanceValue)


  if (!type.eq('BASE')) then
    -- Check suprfa of component for frameworks attached and re-connect them
    do !anci values !this.basePlate.suprfa
      if (!anci.styp.eq('PIPE-REST-INLINE')) then
        !anci.pos = !this.basePlate.lpos
        !anci.owner.hpos = !this.basePlate.lpos
        !anci.owner.tpos = !this.basePlate.lpos
      endif
    enddo

    !this.baseCompExists = TRUE
    -- Do not turn off AddBaseComponent button as guides or lugs can be added
  endif
endmethod

----------------------------------------------------------------------
--
-- Method:      createExtraTranci
--
-- Description: create extra TRANCI as last member of trunnion when adding additional ancillary from form
--
----------------------------------------------------------------------
define method .createExtraTranci(!type is STRING)

  var !trancis collect all TRANCI for $!<this.trunni>
  !newIndex = !trancis.size() + 1

  !!ce = !this.trunni
  !suppoName = !this.trunni.owner.name
  !!ce = !this.basePlate

  new TRANCI
  !name = !suppoName + '.' + !newIndex.string()
  name $!name
  select spref with styp '$!type' spec /MDS-Ancillaries
  !!mdsSetDesPars(!!ce)
  sel lstu
  ori pl is d
  distance 0
  fconn
  loff true

  !this.atta.append(!!ce)

  !!mdsAncill.suppelement = !!ce
  !!mdsAncill.selectedElement = !!ce

endmethod

----------------------------------------------------------------------
--
-- Method:      createTranciComponent
--
-- Description: Add A TRANCI component
--
----------------------------------------------------------------------
define method .modifyTranciComponent(!selectedType is STRING) is REAL
  !ce = !!ce
  !!ce = !this.tranci
  select spref with styp '$!selectedType' spec /MDS-Ancillaries
  conn
  handle any
    fconn
  endhandle
  !!mdsSetDesPars(!!ce)
  !!mdsAncill.suppelement = !!ce
  !!mdsAncill.selectedElement = !!ce
  !tab = 2
  !!ce = !ce
  return !tab
endmethod

----------------------------------------------------------------------
--
-- Method:      deleteComponent
--
-- Description: delete trunnion base component, e.g. isolation block
--
----------------------------------------------------------------------
define method .deleteComponent()

  var !trancis collect all TRANCI for $!<this.trunni>

  if (!trancis.size().eq(1)) then
    !!alert.error('There is no trunnion base ancillary to delete')
    return
  endif

  -- Get the component to delete
  if (!trancis.size().eq(2)) then
    !tranci = !trancis.last().dbref()
  else
    prompt off
    prompt load escape |Identify trunnion base ancillary to delete|
    id tranci @
    handle (61,528)
      return
    endhandle
    prompt dismiss
    prompt on
    !tranci = !!ce

    !found = false
    do !value values !trancis
      if (!value.dbref().eq(tranci) and (!value.neq(!trancis[1]))) then
        !found = true
        break
      endif
    enddo

    if (!found.not()) then
      !!alert.error('Identified element is not a base ancillary for $!<tranci.name>')
      return
    endif
  endif

  enhance $!tranci colour $!!<gphColOpt.highlight.code>
  if (!!alert.confirm('Are you sure you want to delete the $!<tranci.styp> $!<tranci.name>').eq('NO')) then
    unenhance $!tranci
    return
  endif

  !!ce = !tranci

  -- Get the length of the component we're deleting
  if (!tranci.sType inset ('AT33A','AT33B','AT33C','AT33D','AT33E','AT33F','AT34A','AT34B','AT34C','AT34D','AT34E','AT34F','AT35','AT59','AT68','AT100A','AT100B','AT101A','AT101B')) then
    -- Trunnion Base Plate Isolation Pad or Slide Unit etc...
    !tranciLen = !!kStrReal('cons dist p12 to p13')
  else
    !tranciLen = 0
  endif

  -- Get the current height of the trunnion
  !currentLen = !!mdsSupportEditor.dimCont1.getDimensionValueById('hei')

  -- Remove the tranci tab from the support editor
  !!mdsSupportEditor.remSuppFromTab(!tranci)

  --revisit do we need guide refs now on the base plate and on the ancillary?
  -- Remove any guide refs from the previous plate
  PREVIOUS PLAT
  if (!!ce.:MDSLinkRef.set()) then
    !!mdsLinkRefEdit(!!ce,!tranci,FALSE)
  endif
  !!ce = !tranci

  -- remove the component from the atta array
  !indx = !this.atta.findFirst(!tranci)
  if (!indx.set()) then
    !this.atta.remove(!indx)
  endif

  -- remove the component from the !!mdsSupportEditor.support.atta array also
  !indx = !!mdsSupportEditor.support.atta.findFirst(!tranci)
  if (!indx.set()) then
    !!mdsSupportEditor.support.atta.remove(!indx)
  endif

  -- Delete the ancillary
  delete TRANCI

  -- Calculate the new length of the trunnion
  !newLen = !currentLen + !tranciLen

  -- Set the new trunnion length on the support editor
  !!mdsSupportEditor.dimCont1.setDimensionValueById('hei', !newLen)

  -- Activate add base component button
  !this.baseCompExists = FALSE
  !!mdsSupportEditor.trunnionAddBaseComponent.active = !this.baseCompExists.not()

  -- Set the length of the trunnion
  !this.setTrunLength(FALSE)

endmethod

----------------------------------------------------------------------
--
-- Method:      createTrunnion
--
-- Description: create the trunnion hierarchy based upon the selected elements to support
--
----------------------------------------------------------------------
define method .createTrunnion()

  --set icon to on component mode to start
  !!mdsSupportEditor.trunnionInTubeToggle.addPixmap('AvevaSharedIcons>ID_TRUNNION_ON_PIPE>32')
  !!mdsSupportEditor.trunnionInTubeToggle.setTooltip('Component - Click to Support Pipe')

  if (!this.softType inset('TT20')) then
    -- may need to add different statements for different types
    if (!this.secondPick('tube','Identify tube to support').not()) then
      -- second pick has failed - quit
      return
    endif
  endif

  --check the pipe slope and anything else
  if (!this.runChecks().not()) then
    --checks have failed - quit
    return
  endif

  --create the trunnion hierarchy
  if (!this.createDbHierarchy().not()) then
    !!ce = !this.suppo
    handle ANY
    elsehandle NONE
      delete SUPPO
    endhandle
    --Return if there has been an error in creation
    return
  endif

  --initialise the support editor and show it
  !!mdsSupportEditor.initialise('CREATE',!this.softType,!this.suppo, !this)

  --finalise the support
  !this.finalise()

endmethod

----------------------------------------------------------------------
--
-- Method:      secondPick
--
-- Description: Some trunnions require a secondary pick
--
----------------------------------------------------------------------
define method .secondPick(!types is STRING, !message is STRING) is BOOLEAN
  !ce = !!ce
  -- Select tube to support
  prompt off
  prompt load escape '$!message'
  ID $!types @
  handle (61,528)
    prompt on
    return FALSE
  endhandle
  prompt on

  !this.secondElementToSupport = !!ce
  !!ce = !ce
  return TRUE
endmethod

----------------------------------------------------------------------
--
-- Method:      runChecks
--
-- Description: check pipe slope, materials etc
--
----------------------------------------------------------------------
define method .runChecks() is BOOLEAN

  if (!this.pipeChecks()) then
    if (!this.elementChecks()) then
      if (!this.softType.eq('TT20')) then
        if (!this.validateTT20().not()) then
          return false
        endif
      endif
      return true
    else
      return false
    endif
  else
    return false
  endif

endmethod

----------------------------------------------------------------------
--
-- Method:      pipeChecks
--
-- Description: perform checks on pipe
--
----------------------------------------------------------------------
define method .pipeChecks() is BOOLEAN

  -- Check if PIPE is NOT Vertical and within the Maximum Slope Allowed,
  -- for Horizontal Pipes, displayed on the 'Settings>Options...' form.

  -- Modified to suit both COORD ENU and COORD XYZ, so check the UP member of the !hdir DIRECTION object
  -- The check here is to test if the identified TUBI (PIPE) is truly VERTICAL
  if (!this.elementLdir.up.neq(1) and !this.elementLdir.up.neq(-1) ) then
    if (!!mdsGradient(!this.elementToSupport)) then
      !!alert.message ('The pipe identified is not within the maximum slope for horizontal pipes as defined in the application defaults ($!!mdsOptions.slope.val)')
      return false
    endif
  endif

  -- Check pipe material
  !pipeMat = !this.elementOwningBran.pspe.:MDSPipeMat

  -- Check if current selected bore is available
  -- NOTHING Created or Selected YET, so CHECK if TRUNNION STANDARD Available for the MAIN PIPE Bore
  !mdsTrunData = !this.mdsTrunnionObject.mdsTrunData(!this.softType, !this.workingBore.string().lowcase().before('mm').real().value())
  if (!mdsTrunData.not()) then
    --error with the MDS DEFAULTS in the PARAGON CATALOGUE "SECTION" or "CATEGORY" element
    if (!this.mdsTrunnionObject.mdsStd.sactive.not()) then
      --error with the MDS DEFAULTS in the PARAGON CATALOGUE "SECTION" element
      !errorMessage = !this.mdsTrunnionObject.mdsStd.smess
    elseif (!this.mdsTrunnionObject.mdsStd.cactive.not()) then
      --error with the MDS DEFAULTS in the PARAGON CATALOGUE "CATEGORY" element
      !errorMessage = !this.mdsTrunnionObject.mdsStd.cmess
    endif
    !!alert.error(!errorMessage)
    return false
  endif

  -- Get Allowable PIPE Specification Material types
  -- the "ACTIVE" .pipeMaterial is STORED, i.e. The ACTUAL BORE .pipeMaterial, if it's SET, always overrides the ZERO[0] BORE .pipeMaterial
  !stanMat = !this.mdsTrunnionObject.pipeMaterial
  !matList = !stanMat.split(',')
  !findIt  =  !matList.findFirst(!pipeMat)
  if (!findIt.string().eq('')) then
    !findIt = 0
  endif
  if (!stanMat.upcase().eq('UNSET')) then
    !!alert.error('The MDS material code of the MDS standard is unset. Aborting creation!')
    return false
  endif

  if (!pipeMat.upcase().eq('UNSET')) then
    !!alert.error('The MDS material code of the pipe specification is unset. Aborting creation!')
    return false
  endif

  if (!stanMat.neq('ALL')) then
    if (!findIt.eq(0)) then
      if (defined(!!mdsdebug)) then
        return true
      endif
      !!alert.error('The identified pipe specification has a MDS material code of $!pipeMat and does not match the MDS standards material code/s of $!stanMat')
      return false
    endif
  endif

  return true

endmethod

----------------------------------------------------------------------
--
-- Method:      validateTT20
--
-- Description: perform checks on elbow and pipe for TT20
--
----------------------------------------------------------------------
define method .validateTT20() is BOOLEAN

  -- Check to see if the elbo has a vertical section
  !paTest = pa dir of $!<this.elementToSupport> wrt /*
  !plTest = pl dir of $!<this.elementToSupport> wrt /*

  -- Modified to suit both COORD ENU and COORD XYZ, so check the UP member of the !paTest and !plTest DIRECTION objects
  -- The check here is to test if the either the PLEAVE or the PARRIVE direction of the identified ELBO is in
  -- the UP or Z direction to enable a trunnion to be created.
  if (!paTest.up.neq(1) and !plTest.up.neq(1)) then
    !!alert.error('The identified elbow is not suitable for this support.')
    return false
  endif

  if (!!mdsGradient(!this.secondElementToSupport)) then
    !!alert.message ('The pipe identified is not within the maximum slope for horizontal pipes as defined in the application defaults ($!!mdsOptions.slope.val)')
    return false
  endif

  -- Check to see if the tube is below the identified elbo
  pin 9 at $!<this.elementToSupport>
  pin 9 direction d wrt /*
  pin 9 plane pin 9 thro pl of $!<this.secondElementToSupport>
  handle any
    pin 9 plane pin 9 thro ph of $!<this.secondElementToSupport>
  endhandle
  pin 8 copy pl of $!<this.secondElementToSupport>
  handle any
    pin 8 copy ph of $!<this.secondElementToSupport>
  endhandle
  pin 8 plane pin 8 thro pin 9
  var !pin8pin9dist const dist pin 9 to pin 8
  !!mdsResetPins(8,9)

  if (!pin8pin9dist.real().gt(0.5)) then
    !!alert.message ('The identified elbow and tube are not in the same vertical plane to use this standard')
    return false
  endif

  -- Check to see if the horizontal distance is greater than the maximum stored in design parameter 1 of TT20
  !elbowUp = !this.elementToSupport.up
  !tubeUp = !this.secondElementToSupport.up
  handle any
    -- May have identified the Branch Pipe Head
    !tubeUp = !this.secondElementToSupport.hpos.wrt(/* ).up
  endhandle

  !elevDiff = !elbowUp - !tubeUp

  !min = !this.mdsTrunnionObject.minLength
  !max = !this.mdsTrunnionObject.maxLength

  if (!elevDiff.abs().nint().lt(!min)) then
    !uMin = !min.string(!!distanceFMT)
    !uTrunLen = !elevDiff.string(!!distanceFMT)
    !!alert.error(|Elevation difference of $!uTrunLen is less than the minimum of $!uMin$n. Aborting creation!|)
    return false
  elseif (!elevDiff.abs().nint().gt(!max)) then
    !uMax = !max.string(!!distanceFMT)
    !uTrunLen = !elevDiff.string(!!distanceFMT)
    !!alert.error(|Elevation difference of $!uTrunLen is more than the maximum of $!uMax$n. Aborting creation!|)
    return false
  endif

  return true
endmethod

--------------------------------------------------------------------
--
-- Method:      changeTrunnionDirection
--
-- Description: changes trunnion direction
--
----------------------------------------------------------------------
define method .changeTrunnionDirection(!gadget is GADGET, !dummyOption is STRING) is BOOLEAN

  !form = !gadget.owner()

  if (!this.trunDirections().eq('HORI')) then
    if (!form.trunnionDirection.val.upcase().match('D').gt(0) or !form.trunnionDirection.val.upcase().match('U').gt(0) or !form.trunnionDirection.val.upcase().match('-Z').gt(0) or !form.trunnionDirection.val.upcase().match('Z').gt(0)) then
      !!alert.error('Direction must be horizontal. Aborting direction change')
      !!mdsSupportEditor.trunnionDirection.val = !this.trunni.hdir.string().before(' WRT')
      return
    endif
  endif

  !testdir = !form.trunnionDirection.val
  !dirobj  = !testdir.direction()
  handle any
    !!alert.error('Direction is not valid. Aborting direction change')
    !!mdsSupportEditor.trunnionDirection.val = !this.trunni.hdir.string().before(' WRT')
    return
  endhandle

  if (!dirobj.unset()) then
    !!alert.error('Direction is not valid. Aborting direction change')
    !!mdsSupportEditor.trunnionDirection.val = !this.trunni.hdir.string().before(' WRT')
    return
  endif

  --Make sure the trunnion direction is not the same as the leave or leave opposite direction of the connecting pipe
  if (!dirobj.eq(!this.elementLdir) or !dirobj.eq(!this.elementLdir.opposite())) then
    !!alert.error('Direction cannot be the same as the connecting pipes leave or arrive direction. Aborting direction change')
    !!mdsSupportEditor.trunnionDirection.val = !this.trunni.hdir.string().before(' WRT')
    return
  endif

  if (!this.softType inset('TT25','TT33','TT34')) then
    --Check that the trunnion is perpendicular to the pipe
    if (!this.elementToSupport.type.eq('BRAN')) then
      !trunnionDir = !this.elementToSupport.hdir
    else
      !trunnionDir = !this.elementToSupport.ldir
    endif
    if (!form.trunnionDirection.val.direction().isPerpendicular(!trunnionDir)) then
      --Requires separate method as geometry is too different
      !this.modStanchion()
    else
      !alertDir1 = !trunnionDir.orthogonal( U ).string().before(' WRT')
      !alertDir2 = !alertDir1.direction().opposite().string().before(' WRT')
      !!alert.error('Direction must be perpendicular to pipe ($!alertDir1 or $!alertDir2). Aborting direction change')
      !form.trunnionDirection.val = !this.trunni.hdir.string().before(' WRT')
    endif
    return
  endif

  !!ce = !form.support.datum
  trunni
  hdir $!form.trunnionDirection.val

  -- Save current distances from ph
  !mem = mem count
  !savedist = ARRAY()
  do !dist from 1 to $!mem
    trunni
    $!dist
    pin 9 at ce
    pin 8 at ph
    var !currDist const dist pin 9 to pin 8
    !savedist.append(!currDist)
    dist $!currDist from ph
    !p3dir = p3 dir wrt world
    handle any
    endhandle
    !p9dir = p9 dir wrt world
    handle any
    endhandle
    ori and p9 is $!p9dir
    handle any
      ori
    endhandle

    if (!!ce.sType inset ('AC44','AC54','AC53')) then
      ori and p3 is $!p3dir
    endif

  enddo

  !!mdsResetPins(8,9)

  -- Finally connect to new tail pos
  trunni
  conn pt to last mem
  tcon clos

endmethod
--------------------------------------------------------------------
--
-- Method:      changeTrunnionBore
--
-- Description: changes trunnion bore
--
----------------------------------------------------------------------
define method .changeTrunnionBore(!gadget is GADGET, !option is STRING) is BOOLEAN
  if (!option.neq('SELECT')) then
    return
  endif

  -- reset the offset to zero because we are changing the bore and the previous offset is no longer valid
  if (!!mdsSupportEditor.dimCont1.getdimensionnames().findFirst('offset').set()) then
    if (!!mdsSupportEditor.dimCont1.getDimensionValueById('offset').neq(0)) then
      !!mdsSupportEditor.dimCont1.setDimensionValueById('offset', 0)
      -- reset the offset to 0
      !this.modOffset()
    endif
  endif

  !form = !gadget.owner()

  --note, the below code is modified from that in mdsresetbore.pmlfnc

  -- Access branch
  !!ce = !form.support.datum
  handle ANY
    return
  endhandle

  trunni

  -- Can only reset the bore of a trunnion if it does not contain any other supports
  !mem = mem count
  !supportMem = 0
  !otherSupports = ARRAY()
  do !listMem from 1 to $!mem
    trunni
    $!listMem
    !tempName = !!mdsGetSuppName(!!ce)
    if (!!ce.owner.name.match(!tempName).eq(0)) then
      !supportMem = !supportMem + 1
      !otherSupports.append(!!ce.name)
    endif
  enddo

  !!ce = !form.support.datum
  trunni

  if (!this.softType.eq('TT20')) then
    if (!this.secondElementToSupport.type.eq('BRAN')) then
      tbor $!<this.secondElementToSupport.hbore>
    else
      tbor $!<this.secondElementToSupport.lbore>
    endif
  else
    tbor $!form.availableTrunnionBores
  endif
  !tBore = !!ce.tBore

  -- The make up piece will require resetting to the new bore
  !!ce = !form.support.datum

  --select lstu of tranci then set hstu of trunni to same
  sel lstu
  !tube = lstu
  own
  hstu $!tube
  same

  !supportName = !!ce.name.before('.1')
  $!supportName/MAKEUP
  despar n1 $!form.availableTrunnionBores
  sel lstu

  !!ce = !form.support.datum
  !type = type
  !element = name
  var !radius const dist p0 to p1
  !od = pa od
  $!supportName/MAKEUP
  !tOd = pl od

  if (!type.eq('ELBO')) then
    !hyp = !radius.real() + (!od / 2)
    !side1 = !radius.real() + (!tOd / 2)
    !offset = (sqrt ((!hyp * !hyp) - (!side1 * !side1)))
  else
    -- Find smallest bore if redu
    if (!type.eq('REDU')) then
      !rb1 = pod 1 of $!element
      !rb2 = pod 2 of $!element
      if (!rb1.gt(!rb2)) then
        !od = !rb2
      else
        !od = !rb1
      endif
    endif
    !hyp = (!od / 2)
    if (!form.support.type inset('TT10','TT11','TT12','TT13','TT22','TT26','TT28')) then
      !pThk = (prop pthk of prev)
      !hyp = !hyp + !pThk.real()
    endif
    !side1 = (!tOd / 2)
    !offset = (sqrt ((!hyp * !hyp) - (!side1 * !side1)))
  endif

  despar n2 $!offset
  trunni

  !members = !!ce.members

  !sTypeCounter = 1
  do !reselect values !members
    --revisit can remove this when tubi is removed from members list
    if (!reselect.type.eq('TUBI')) then
      skip
    endif
    $!reselect

    if (!!ce.type inset('TRANCI')) then
      -- TT44 is a special trunnion where the reinforcing plate is hard coded
      if (!!ce.styp.neq('PIPE-REST') and (!this.softType.neq('TT44'))) then
        !sTypeCounter = !sTypeCounter + 1
      endif
      skip
    endif

    -- For Trunnion TYpe !!mdsTT23 Only
    -- Get the Existing Spec of the LSTU of the Existing Element and Select it at the end if Possible
    --revisit for TT23
    if (!this.softType.eq('TT23')) then
      !spec = spown of lstu
    endif

    -- The first member may be a reinforcing plate AT29,AT31, or stop ST08
    -- if so skip it but change despars to the new od
    if (!!ce.styp.match('AT29').gt(0) or !!ce.styp.match('AT31').gt(0) or !!ce.styp.match('AT105').gt(0)) then
      !dps = despar
      !!mdsSetDesPars(!!ce)
      despar n1 $!dps[1]
      despar n2 $!tOd
      despar n3 $!dps[3]
      skip
    elseif (!!ce.styp.match('ST08').gt(0) or !!ce.styp.match('AN05').gt(0) or !!ce.styp.match('GT11').gt(0) or !!ce.styp.match('ST07').gt(0)) then
      !stopStype = styp
      select spref with styp '$!stopStype' spec /MDS
      !!mdsSetDesPars(!!ce)
      skip
    endif
    -- It may be another support so of so change the bore of the support and reselect it with default parameters
    if (!otherSupports.find(!!ce.name).size().gt(0)) then
      !sType = styp
      !spec =  spec of spref

      if (!spec.name eq '/MDS-Ancillaries') then
        select spref with styp '$!sType' spec /MDS-Ancillaries
        !!mdsSetDesPars(!!ce)
      elseif (!spec.name eq '/MDS') then
        select spref with styp '$!sType' spec /MDS
        !!mdsSetDesPars(!!ce)
      elseif (!spec.name eq '/MDF') then
        select spref with styp '$!sType' spec /MDF
        !!mdsSetDesPars(!!ce)
      endif
      sel lstu
      skip
    endif

    -- Reselect the makeupoff for TT20
    if (!!ce.styp.match('MAKEUPOFF').gt(0) and !this.softType.eq('TT20')) then
      select spref with styp 'MAKEUPOFF' spec /MDS-Ancillaries
      despar $!tBore 0mm 0mm
      sel lstu

      if (!this.secondElementToSupport.type.eq('BRAN')) then
        !tOd = !this.secondElementToSupport.phod
      else
        !tOd = pl od of $!<this.secondElementToSupport>
      endif
      !od = aodiam

      if (!od gt !tOd) then
        !!alert.error('The connecting bore of the pipe, is less than the bore of the trunnion. Aborting creation.')
        return FALSE
      endif

      !hyp = (!od / 2)
      !side1 = (!tOd / 2)
      !offset = (sqrt ((!side1 * !side1) - (!hyp * !hyp)))
      despar n2 $!offset
      conn to last mem of owner
      handle any
        fconn to last mem of owner
        handle any
          return false
        endhandle
      endhandle
    endif

    if (!!ce.styp.match('MAKEUP').gt(0)) then
      -- Set SSREFE to bore of leave of TRREDU
      if (!this.ssrefe.trunBoreSSREFE(!!ce).not()) then
        -- cannot find a spcom for the ssrefe
        return FALSE
      endif
      skip
    elseif (!!ce.styp.match('FFW').gt(0)) then
      select spref with styp '$!!ce.styp' spec /MDS-Ancillaries
      skip
    else
      -- If its an AC45 save design para 15 as this is the angle
      if (!!ce.styp.eq('AC45')) then
        !savedDP = despar
      endif
      !sType = styp
      -- The trunnion may have a guide added so if its not the same support name select with the same styp
      if (!!ce.sType.match('GT12').gt(0) or !!ce.sType.match('GT27').gt(0)) then
        select spref with styp '$!sType' spec /MDS
      else
        trunni
        !bore = ph bore
        same

        -- Available Trunnion Bores are NOW determined from the MDS DEFAULTS stored in the PARAGON DB CATALOGUE
        !mdsStd = !form.support.type.upcase()
        !rbore  = !bore.real().value()
        !mdsTrunObj  = object MDSREADTRUNDATA()
        !mdsTrunData = !mdsTrunObj.mdsTrunData(!mdsStd, !rbore)

        -- Check if the Trunnion type and bore is ACTIVE for the current Project
        if (!mdsTrunData.not()) then
          -- ERROR with the MDS DEFAULTS in the PARAGON CATALOGUE "SECTION" or "CATEGORY" element
          if (!mdsTrunObj.mdsStd.sactive.not()) then
            -- ERROR with the MDS DEFAULTS in the PARAGON CATALOGUE "SECTION" element
            !errorMessage = !mdsTrunObj.mdsStd.smess
          elseif (!mdsTrunObj.mdsStd.cactive.not()) then
            -- ERROR with the MDS DEFAULTS in the PARAGON CATALOGUE "CATEGORY" element
            !errorMessage = !mdsTrunObj.mdsStd.cmess
          endif

          -- Advise USER and EXIT Trunnion Support Modification
          !!alert.error(!errorMessage)
          unenhance all
          return

        elseif (!this.softType inset ('TT36','TT42','TT43','TT44')) then
          !selectedbore = !form.availableTrunnionBores.selection()
          !plate = !form.availableTrunnionPlates.selection()
          !index = !form.support.trunnion.setCurrentBorePlate(!selectedbore,!plate)
          !sstype = !mdsTrunObj.sTypes[!index]
        else
          -- List the Available Trunnion Bore Sizes for the Current TRUNNION
          !sstype = !mdsTrunObj.sTypes[!form.availableTrunnionBores.val]

        endif
        -- this code was added to deal with one or two plates
        !nStype = !sstype.split(',')
        select spref with styp '$!nStype[$!sTypeCounter]' spec /MDS-Ancillaries
        handle(42,501)(42,64)
          !!alert.error('Cannot select the STYPE $!nStype[$!sTypeCounter] for the Trunnion Bore of the support')
          return FALSE
        endhandle
        -- go and update the baseplate tab
        !this.updateTABFormGadgets(!!ce)

        -- Changing the bore size changes the plate size refresh the dimensions in the form
        !sTypeCounter = !sTypeCounter + 1
      endif
      -- if the styp is AC20 then the size is worked from the pl bore
      -- flip the component and then back again
      if (!sType.eq('AC20')) then
        flip
        !!mdsSetDesPars(!!ce)
        !allow = desp
        desp n4 0mm
        prev weld
        allow $!allow[4]
        nex
        flip
      else
        !!mdsSetDesPars(!!ce)
      endif
      -- If its an AC45 save design para 15 as this is the angle
      if (!!ce.styp.eq('AC45')) then
        despar n15 $!savedDP[15]
      endif
    endif
    --revisit when do TT23
    if (!this.softType.eq('TT23')) then
      sel lstu with spec $!spec
    else
      sel lstu
    endif
  enddo

  -- Reset position of members
  !atta = /$!form.name.val
  if (!form.support.type.eq('TT21')) then
    !!mdsSetTrunLen('GADGET',!form.tLength.val,!atta,'U WRT /*',!form.support.type,!form)
  elseif (!form.support.type.upcase().eq('TT27')) then
    !!mdsSetTrunLen('GADGET',!form.tLength.val,!atta,!form.trunnionDirection.val,!form.support.type,!form)
  elseif (!form.support.type.upcase().eq('TT37')) then
    !!mdsSetTrunLen('GADGET',!form.tLength.val,!atta,!form.trunnionDirection.val,!form.support.type,!form)
  else
    !this.setTrunLength(FALSE)
    --!!mdsSetTrunLen('GADGET',!form.tLength.val,!atta,'D WRT /*',!form.support.type,!form)
  endif

  if (!form.support.type inset('TT10','TT11','TT12','TT13','TT22','TT28')) then
    $!supportName/MAKEUP
    -- Design para 10 of the prev comp needs to be to the pl od of the make up piece
    -- to spread the clamps apart
    !plOdClamp = pl od
    prev
    despar n10 $!plOdClamp
    same
  endif

endmethod

--------------------------------------------------------------------
--
-- Method:      updateTABFormGadgets
--
-- Description: updates tabs
--
----------------------------------------------------------------------
define method .updateTABFormGadgets(!savedElement is DBREF)

  -- Changing the bore size changes the plate size refresh the dimensions in the form
  !!ce = !savedElement
  -- make sure the despars are up to date
  !!mdsSetDesPars(!!ce)

  !controlIndex = !!mdsSupportEditor.dimContDBref.findFirst(!savedElement)

  !supportTooltip = ''
  !supportImage = !savedElement.styp
  !replacementName = !!mdsGetProjectStandard(!supportImage)
  !!mdsSupportEditor.dimCont$!<controlIndex>.show(!supportImage,!supportTooltip,'Branch Information')
  !!mdsGetSupportDims(!savedElement,!!mdsSupportEditor.dimCont$!<controlIndex>)
  -- Set the distance gadget to the same as the first one
  !!mdsSupportEditor.dimCont$!<controlIndex>.setDimensionValueById('distance', !!mdsSupportEditor.distanceValue)
  !!mdsSupportEditor.dimCont$!<controlIndex>.setDimensionActive('distance', !!mdsSupportEditor.dimCont1.getDimensionActive('distance'))
  !!mdsSupportEditor.fDimCont$!<controlIndex>.tag = !replacementName
  -- TM added to reset the icons if you change a plate
  !!mdsSupportEditor.formSetup.disableAncillaryIcons(!savedElement)

endmethod

--------------------------------------------------------------------
--
-- Method:      typesAllowedOn
--
-- Description: returns types trunnion is allowed on
--
----------------------------------------------------------------------
define method .typesAllowedOn() is ARRAY

  !typesAllowedOn = ARRAY()

  if (!this.softType inset('TT14','TT15','TT16','TT20','TT21','TT39','TT42')) then
    !typesAllowedOn.append('ELBO')
  elseif (!this.softType inset('TT10','TT11','TT12','TT13','TT17','TT22','TT24','TT25','TT26','TT27','TT28','TT29','TT30','TT31','TT32','TT33','TT34','TT35','TT38','TT40','TT41','TT44')) then
    !typesAllowedOn.append('TUBE')
  else
    --default case e.g. for TT04
    !typesAllowedOn.append('BRAN')
    !typesAllowedOn.append('ELBO')
    !typesAllowedOn.append('REDU')
    !typesAllowedOn.append('TEE')
    !typesAllowedOn.append('TUBE')
  endif

  return !typesAllowedOn
endmethod

--------------------------------------------------------------------
--
-- Method:      trunDirections
--
-- Description: returns direction of trunnion type
--
----------------------------------------------------------------------
define method .trunDirections() is STRING

  --returns 'VERT' if vertical trunnion (only allowed on horizontal pipes)
  --returns 'HORI' if horizontal trunnion (only allowed on vertical pipes)
  --returns 'BOTH' if allowed on horizontal or vertical pipes

  if (!this.softType inset('TT14','TT15','TT27','TT36')) then
    !trunDirections = 'BOTH'
  elseif (!this.softType inset('TT16','TT17','TT24','TT25','TT31','TT32','TT33','TT34')) then
    !trunDirections = 'HORI'
  else
    --default case e.g. for TT04
    !trunDirections = 'VERT'
  endif

  return !trunDirections

endmethod

--------------------------------------------------------------------
--
-- Method:      validateTrunnion
--
-- Description: validates trunnion,
--
----------------------------------------------------------------------
define method .validateTrunnion(!mode is STRING) is BOOLEAN

  !allowedOn = !this.typesAllowedOn()
  !this.onlyAllowInTube = FALSE

  if (!allowedOn.size().eq(1) AND !allowedOn.findFirst('TUBE').set()) then
    !this.onlyAllowInTube = TRUE
  endif

  --if in create mode and !this.onType found in allowed on array then continue, otherwise return

  if (!mode.eq('CREATE') AND !allowedOn.findFirst(!this.onType).unset() AND !this.onlyAllowInTube.not()) then
    !!alert.error('Identified element type not suitable for selected trunnion type')
    return false
  endif

  !this.inTubeToggleActive = TRUE
  --if 'BRAN' is not in types allowed then deactivate intube button
  if (!allowedOn.findFirst('BRAN').unset()) then
    !this.inTubeToggleActive = FALSE
  endif

  !trunDirections = !this.trunDirections()
  if (!trunDirections.eq('VERT')) then
    --if vertical trunnion then do not allow intube mode if pl of element is up
    if (!this.elementLdir.up.eq(1)) then
      !this.inTubeToggleActive = FALSE
    endif

    if (!this.onType.eq('ELBO')) then
      if (!this.softType.eq('TT21')) then
        if (!this.elementAdir.up.neq(-1) and !this.elementLdir.up.neq(-1)) then
          !!alert.error('Identified elbow is not suitable for a TT21')
          return FALSE
        endif
      elseif (!this.elementAdir.up.neq(1) and !this.elementLdir.up.neq(1)) then
        --elbow must have either leave or arrive up to be supported with vertical trunnion
        !this.inTubeToggleActive = FALSE
        if (!mode.eq('CREATE')) then
          if (!this.elementLdir.up.neq(-1)) then
            !!alert.error('Identified elbow is not suitable for a vertical trunnion, creating in tube')
          else
            !!alert.error('Identified elbow is not suitable for a vertical trunnion')
            return FALSE
          endif
        endif
        !this.onlyAllowInTube = TRUE
      endif

      if (!this.onlyAllowInTube) then
        if (!!mdsGradient(!this.elementToSupport)) then
          !!alert.message ('The pipe identified is not within the maximum slope for horizontal pipes as defined in the application defaults ($!!mdsOptions.slope.val)')
          return FALSE
        endif
      endif

    else
      --if leave is up or down then do not allow to support, return
      if (!this.elementLdir.up.eq(1) OR !this.elementLdir.up.eq(-1)) then
        if (!this.onType.eq('BRAN')) then
          !!alert.error('Identified tube is not suitable for a vertical trunnion')
        else
          !!alert.error('Identified component is not suitable for a vertical trunnion')
        endif
        return FALSE
      endif

      if (!this.onType.eq('REDU')) then
        !pv1 = object POINTVECTOR(!this.elementToSupport.apos.wrt( /* ), !this.elementAdir )
        !pv2 = object POINTVECTOR(!this.elementToSupport.lpos.wrt( /* ), !this.elementLdir )
        !pv1Pos = !pv1.intersection(!pv2.plane())
        if (!pv1Pos.eq(!pv2.position)) then
          !this.inTubeToggleActive = FALSE
          if (!mode.eq('CREATE')) then
            !!alert.error('Identified reducer must be eccentric, creating in tube')
          endif
          !this.onlyAllowInTube = TRUE
        endif
        --revisit not sure this should be here
        --!spref = !!ce.spref.name
        --:MDSComment '$!spref'

      elseif (!this.onType inset ('BRAN','TUBE')) then
        !this.inTubeToggleActive = FALSE
      endif

    endif

  elseif (!trunDirections.eq('HORI')) then

    if (!this.softType.eq('TT16')) then

      !ref = !!ce
      --------------------------------------------------------------------------------
      -- Check if element identified is suitable for this type of dummy extension support
      --------------------------------------------------------------------------------
      !!ce = !this.elementToSupport
      ---------------------------------------------------------------------------------------------------
      -- Get the TT15 and TT16 Client CODES in MDS12
      !tt14code = !!mdsgetdefprop('TRUNNION', 'TT14', 'CCOD').after(|'|).before(|'|)
      !tt15code = !!mdsgetdefprop('TRUNNION', 'TT15', 'CCOD').after(|'|).before(|'|)
      ---------------------------------------------------------------------------------------------------

      !paTest = pa dir wrt /*
      !plTest = pl dir wrt /*
      if (!paTest.string().eq('U WRT /*') or !plTest.string().eq('U WRT /*') or !paTest.string().eq('Z WRT /*') or !plTest.string().eq('Z WRT /*')) then

        !!alert.error('Identified elbow is not suitable. please use $!tt15code Type B')

        !!ce = !ref
        return false
      elseif (!paTest.string().eq('D WRT /*') or !plTest.string().eq('D WRT /*') or !paTest.string().eq('-Z WRT /*') or !plTest.string().eq('-Z WRT /*')) then

        !!alert.error('Identified elbow is not suitable. please use $!tt14code Type A')

        !!ce = !ref
        return false
      endif

      !!ce = !ref
    endif
  elseif (!trunDirections.eq('BOTH')) then
    --check elbow has at least one up or down arrive or leave
    !ref = !!ce
    !!ce = !this.elementToSupport
    if (!this.softType inset('FT14','FT15')) then

      !paTest = pa dir wrt /*
      !plTest = pl dir wrt /*

      if ((!paTest.string().eq('D WRT /*').not() and !plTest.string().eq('D WRT /*').not()) and (!paTest.string().eq('U WRT /*').not() and !plTest.string().eq('U WRT /*').not())) then
        --do the same test for xyz coordinates
        if ((!paTest.string().eq('-Z WRT /*').not() and !plTest.string().eq('-Z WRT /*').not()) and (!paTest.string().eq('Z WRT /*').not() and !plTest.string().eq('Z WRT /*').not())) then
          !!alert.error('Identified elbow is not suitable')
          !!ce = !ref
          return FALSE
        endif
      endif
    endif
    !!ce = !ref

    if (!this.softType.eq('TT36') and !this.onType inset ('BRAN','TUBE')) then
      !this.inTubeToggleActive = FALSE
    endif

  else
    --do nothing

  endif

  return TRUE

endmethod

--------------------------------------------------------------------
--
-- Method:      validateInTubeDist
--
-- Description: validates the trunnion to check whether it fits in the tube
--
----------------------------------------------------------------------
define method .validateInTubeDist() is REAL

  --Check that there is enough distance between components to create the trunnion

  !ce = !!ce
  !!ce = !this.elementToSupport
  if (!!ce.type.neq('BRAN')) then
    var !extraDistanceFromOrigin construct distance p0 to pl
    !elementLeavePos = !!ce.lpos.wrt(/* )

    -- Navigate to the next element and get its arrive position
    next
    handle ANY
      -- There isn't a next element so get the branch tail position
      !arrivePos = !this.elementOwningBran.tpos.wrt(/* )
    elsehandle NONE
      !arrivePos = !!ce.apos.wrt(/* )
    endhandle
  else
    !extraDistanceFromOrigin = 0mm
    !elementLeavePos = !!ce.hpos.wrt(/* )

    -- Were at a branch so navigate to the first element and get its arrive position
    1
    handle ANY
      -- There isn't a first element, so get the branch tail position
      !arrivePos = !this.elementOwningBran.tpos.wrt(/* )
    elsehandle NONE
      !arrivePos = !!ce.apos.wrt(/* )
    endhandle
  endif

  !!ce = !ce

  var !stringDist const dist $!elementLeavePos to $!arrivePos

  if (!this.tranci.styp inset('AC28','AC29','AC31')) then
    !od = (!this.tranci.desp[10] + !this.tranci.desp[4]) * 2
  else
    !od = lod of TRREDU 1 of $!<this.trunni>
  endif

  if (!od.real().value().gt(!stringDist.real().value())) then
    return -1
  endif

  if (!!mdsSupportEditor.distance.val) then
    !dist = !stringDist.real()
  else
    !dist = !stringDist.real() + !extraDistanceFromOrigin.real()
  endif

  if (!dist.real().value().gt((!this.inTubeDist + (!od / 2)))) then
    return 0
  endif

  return !dist.real() - (!od / 2)

endmethod

--------------------------------------------------------------------
--
-- Method:      elementChecks
--
-- Description: perform checks on selected element
--
----------------------------------------------------------------------
define method .elementChecks() is BOOLEAN

  --run validation to check trunnion can be created on selected component and activate intube toggle if appropriate
  if (!this.validateTrunnion('CREATE').not()) then
    return FALSE
  endif

  --The available Trunnion Bore Sizes are stored in the object member !this.mdsTrunnionObject.bores
  !availBores = !this.mdsTrunnionObject.bores

  if (!availBores.empty()) then
    !!alert.error('There are no available Trunnion bores. Aborting Trunnion creation.')
    return false
  endif

  !unitBore = ARRAY()
  do !setUnitBores from 1 to !availBores.size()
    !mmBore = !availBores[$!setUnitBores] & 'mm'
    !unitBore.append(!mmBore.bore(!!borefmt).string())
  enddo

  !this.availableBoresUnits = !unitBore
  !this.availableBores = !availBores

  --revisit, need to know what this bit should do now in relation to the form
  $(
  !mmBore = !bore.string()
  !mainBore = !mmBore
  !callingForm.mainBore.val = 'Main     Bore =     $!mainBore'
  !callingForm.tLength.val = !this.mdsTrunnionObject.defLength
  $)

  return TRUE

endmethod

----------------------------------------------------------------------
--
-- Method:      createDbHierarchy
--
-- Description: create the necessary database structure to define a trunnion
--
----------------------------------------------------------------------
define method .createDbHierarchy() is BOOLEAN

  if (!this.onType.eq('ELBO')) then
    if (!this.createElboDbHierarchy().not()) then
      return FALSE
    endif
    !this.distanceActive = false
  elseif (!this.onType.eq('REDU')) then
    if (!this.createReduDbHierarchy().not()) then
      return FALSE
    endif
    !this.distanceActive = false
  elseif (!this.onType.eq('TEE')) then
    if (!this.createTeeDbHierarchy().not()) then
      return FALSE
    endif
    !this.distanceActive = false
  elseif (!this.onType.eq('BRAN')) then
    if (!this.createBranDbHierarchy().not()) then
      return FALSE
    endif
    !this.distanceActive = true
  elseif (!this.onType.eq('TUBE')) then
    if (!this.createTubeDbHierarchy().not()) then
      return FALSE
    endif
    !this.distanceActive = true
  endif

  --if we have found a reducer, tee or elbow that does not allow trunnion orientation (e.g. vertical) then move into tube
  if (!this.onlyAllowInTube) then
    !this.inTube = FALSE
    !this.inTube()
    if (!this.inTube.not()) then
      !!alert.error('There is not enough tube to create the trunnion, cancelling creation')
      !!ce = !this.suppo
      delete suppo
      handle ANY
      endhandle
      return false
    endif
  endif

  return TRUE
endmethod

----------------------------------------------------------------------
--
-- Method:      createElboDbHierarchy
--
-- Description: create the DB hierarchy for the elbow
--
----------------------------------------------------------------------
define method .createElboDbHierarchy() is BOOLEAN

  -- draw arrive leave on elbo
  if (!this.softType inset ('TT14','TT15','TT16')) then
    !plPos = pl pos of $!this.elementToSupport
    !paPos = pa pos of $!this.elementToSupport

    -- offset aid text position to do not hide other texts
    !plPos = !plPos.offset(pl dir of $!this.elementToSupport, 100)
    !paPos = !paPos.offset(pa dir of $!this.elementToSupport, 100)

    !!drawAidText('Arrive', !paPos, 210)
    !!drawAidText('Leave', !plPos, 210)
  endif

  --set the flag that tells us we are supporting a component
  !this.inTube = false

  if (!this.trunDirections().eq('VERT')) then
    if (!this.softType.eq('TT21')) then
      !trunDir = object DIRECTION('u wrt /*')
    else
      !trunDir = object DIRECTION('d wrt /*')
    endif
  elseif (!this.trunDirections().eq('BOTH')) then

    if (!this.onlyAllowInTube) then
      !trunDir = !this.getOrthogDir()
    else

      --for TT14 and TT15 we check that at least one of arrive and leave dir of elbow is up or down
      !where = !!alert.confirm('Create dummy extension from, Yes for Vertical, No for Horizontal',0,0)

      if (!where.eq('NO')) then
        !this.hori = TRUE

        if (!this.elementAPos.up.gt(!this.elementLPos.up)) then
          if (!this.elementP0Pos.up.gt(!this.elementLPos.up)) then
            !trunDir = !this.elementToSupport.adir.opposite()
          else
            !trunDir = !this.elementToSupport.ldir.opposite()
          endif
        else

          if (!this.elementP0Pos.up.gt(!this.elementAPos.up)) then
            !trunDir = !this.elementToSupport.ldir.opposite()
          else
            !trunDir = !this.elementToSupport.adir.opposite()
          endif

        endif

      else
        !this.hori = FALSE

        if (!this.elementAPos.up.gt(!this.elementLPos.up)) then
          if (!this.elementP0Pos.up.gt(!this.elementLPos.up)) then
            !trunDir = object DIRECTION('u wrt /*')
          else
            !trunDir = object DIRECTION('d wrt /*')
          endif
        else

          if (!this.elementP0Pos.up.gt(!this.elementAPos.up)) then
            !trunDir = object DIRECTION('u wrt /*')
          else
            !trunDir = object DIRECTION('d wrt /*')
          endif

        endif

      endif

    endif

  elseif (!this.trunDirections().eq('HORI')) then

    if (!this.softType.eq('TT16')) then
      --trunDir for TT16 here
      !this.hori = TRUE
      !where = !!alert.confirm('Create dummy extension from, Yes for arrive, No for leave',0,0)
      if (!where.eq('NO')) then
        !trunDir = !this.elementToSupport.ldir.opposite()
      else
        !trunDir = !this.elementToSupport.adir.opposite()
      endif

    else
      !trunDir = !this.getOrthogDir()
    endif

  endif

  --create the trunnion hierarchy
  return !this.createTrunni(!trunDir)

endmethod

----------------------------------------------------------------------
--
-- Method:      createTeeDbHierarchy
--
-- Description: create the DB hierarchy for the tee
--
----------------------------------------------------------------------
define method .createTeeDbHierarchy() is BOOLEAN

  --set the flag that tells us we are supporting a component
  !this.inTube = false

  if (!this.trunDirections().eq('VERT')) then
    !trunDir = object DIRECTION('d wrt /*')
  else
    !trunDir = !this.getOrthogDir()
  endif

  --create the trunnion hierarchy
  return !this.createTrunni(!trunDir)

endmethod

----------------------------------------------------------------------
--
-- Method:      createReduDbHierarchy
--
-- Description: create the DB hierarchy for the reducer
--
----------------------------------------------------------------------
define method .createReduDbHierarchy() is BOOLEAN

  --set the flag that tells us we are supporting a component
  !this.inTube = false

  if (!this.trunDirections().eq('VERT')) then
    !trunDir = object DIRECTION('d wrt /*')
  else
    !trunDir = !this.getOrthogDir()
  endif

  --create the trunnion hierarchy
  return !this.createTrunni(!trunDir)


endmethod

----------------------------------------------------------------------
--
-- Method:      createBranDbHierarchy
--
-- Description: create the DB hierarchy for the branch (i.e. when selected element is a branch)
--
----------------------------------------------------------------------
define method .createBranDbHierarchy() is BOOLEAN

  --set the flag that tells us we are supporting a component
  !this.inTube = true

  if (!this.trunDirections().eq('VERT')) then
    !trunDir = object DIRECTION('d wrt /*')
  else
    !trunDir = !this.getOrthogDir()
  endif

  --create the trunnion hierarchy
  return !this.createTrunni(!trunDir)

endmethod

----------------------------------------------------------------------
--
-- Method:      createTubeDbHierarchy
--
-- Description: create the DB hierarchy for the branch (i.e. when selected element is a branch)
--
----------------------------------------------------------------------
define method .createTubeDbHierarchy() is BOOLEAN

  --set the flag that tells us we are supporting a component
  !this.inTube = true

  if (!this.trunDirections().eq('VERT')) then
    !trunDir = object DIRECTION('d wrt /*')
  else
    !trunDir = !this.getOrthogDir()
  endif

  --create the trunnion hierarchy
  if (!this.createTrunni(!trunDir).not()) then
    return FALSE
  endif

  --we are supporting the component tee. Modify to support the tube
  !ldir = !this.elementLdir
  !lpos = !this.elementLPos

  --create a pointvector distanced along the line by the inTubeDist

  !p0pos = !this.elementP0Pos

  if (!!mdsSupportEditor.distance.val) then
    !pvLeave = object POINTVECTOR(!lpos, !ldir)
  else
    !pvLeave = object POINTVECTOR(!p0pos, !ldir)
  endif

  --!pvLeave = object POINTVECTOR(!lpos.position(), !ldir)
  !pvLeave = !pvLeave.offset(!this.inTubeDist)

  --drag move through !pvLeavePos (note cannot do this at trunni level so we have to reconnect the head after the move
  !!ce = !this.tranci
  !pvLeavePos = !pvLeave.position
  drag move $!ldir thro $!pvLeavePos

  --reconnect the trunni head to the arrive of the tranci
  !!ce = !this.trunni
  conn ph to pa of 1

  --set the in-tube flag
  !this.inTube = true
  !this.distanceActive = true

  return TRUE

endmethod
----------------------------------------------------------------------
--
-- Method:      finalise
--
-- Description: set material properties and shop flags etc before finishing creation
--
----------------------------------------------------------------------
define method .finalise()

  --revisit, need to sort something out for the dimension control
  $(
  !!mdsGetProp(!callingForm.plength,!callingForm.plength,'PLEN',!tBore,'$!sType')
  !!mdsGetProp(!callingForm.pwidth,!callingForm.pwidth,'PWID',!tBore,'$!sType')
  !!mdsGetProp(!callingForm.pThk,!callingForm.pThk,'PTHK',!tBore,'$!sType')
  $)

  --Get the default trunnion height from catalogue and use it as the initial value
  !mdsTrunData = !this.mdsTrunnionObject.mdsTrunData(!this.softType, !this.trunni.hbor.real().value())
  !defHeight   = !this.mdsTrunnionObject.defLength
  if (!mdsTrunData) then
    --setup the trunnion with an initial height equal to the minimum value
    !!mdsSupportEditor.dimCont1.setDimensionValueById('hei', !defHeight)
  else
    --setup the trunnion with an initial height of 500mm
    !!mdsSupportEditor.dimCont1.setDimensionValueById('hei', 500mm)
  endif

  if (!this.softType inset('TT25','TT33','TT34')) then
    !!mdsSupportEditor.dimCont1.setDimensionValueById('clea', !this.mdsTrunnionObject.userdps[4].min)
  endif

  --Re-position the trunnion elements to match the trunnion height value
  !this.setTrunLength(false)

  --if dummy leg offset is set then set offset gadget (e.g. TT14)
  if (!this.dummyLegOffset.set()) then
    !!mdsSupportEditor.dimCont1.setDimensionValueById('offset', !this.dummyLegOffset)
  endif

  -- Set the material and shop flags for all comps except the notes
  -- and remove the insulation automatically
  !!ce = !this.trunni
  ispe =0
  !members = mem count
  do !set from 1 to !members
    !!ce = !this.trunni
    $!set
    !!mdssetmatref(!!ce)
    handle any
    endhandle
    ispe =0
  enddo

  !!ce = !this.trunni

  --Set same temperature of TRUNNI as owning branch
  !this.trunni.temp = !this.elementOwningBran.temp

  !shop = !this.mdsTrunnionObject.shop
  shop $!shop

  do !set from 1 to !members
    !!ce = !this.trunni
    $!set
    shop $!shop
  enddo
  if (!this.collar.set()) then
    if (!this.collar.sType.eq('AC20')) then
      !this.modWeldInsertionLength(!this.collar)
    endif
  endif

endmethod

----------------------------------------------------------------------
--
-- Method:      createMakeup
--
-- Description: create the makeup piece common to most trunnions
--
----------------------------------------------------------------------
define method .createMakeup()

  -- makeup piece
  if (!this.onType.eq('ELBO') or !this.onType.eq('REDU')) then
    new TRREDU select spref with styp 'MAKEUP' spec /MDS-Ancillaries
  else
    new TRREDU select spref with styp 'MAKEUPOFF' spec /MDS-Ancillaries
  endif
  despar $!this.availableBores[1] 0mm 0mm
  sel lstu
  connect
  handle (7,3)
    fconnect
  endhandle
  mtoc off

  !!ce.name = !this.suppo.name & '/MAKEUP'

  !offset = !this.calculateMakeupOffset(!!ce)
  desp num 2 $!offset
  !this.atta.append(!!ce)

endmethod

----------------------------------------------------------------------
--
-- Method:      createTrunni
--
-- Description: create the trunni hierarchy appropriate to !this.onType
--
----------------------------------------------------------------------
define method .createTrunni(!direction is DIRECTION) is BOOLEAN

  if (!this.softType inset('TT04','TT05','TT18','TT19','TT36','TT37','TT38','TT39','TT42','TT43')) then
    return !this.createBasicTrunni(!direction)
  elseif (!this.softType inset('TT07')) then
    return !this.createTrunniTT06(!direction)
  elseif (!this.softType inset('TT09')) then
    return !this.createTrunniTT08(!direction)
  elseif (!this.softType inset('TT11')) then
    return !this.createTrunniTT10(!direction)
  elseif (!this.softType inset('TT13')) then
    return !this.createTrunniTT12(!direction)
  elseif (!this.softType inset('TT30')) then
    return !this.createTrunniTT35(!direction)
  elseif (!this.softType inset('TT34')) then
    return !this.createTrunniTT33(!direction)
  elseif (!this.softType inset('TT40','TT41')) then
    return !this.createTrunniTT40(!direction)
  elseif (!this.softType inset('TT28','TT29','TT30')) then
    return !this.createTrunniTT22(!direction)
  elseif (!this.softType inset('TT31','TT32')) then
    return !this.createTrunniTT24(!direction)
  else
    return !this.createTrunni$!this.softType(!direction)
  endif

endmethod

----------------------------------------------------------------------
--
-- Method:      createBasicTrunni
--
-- Description: create the trunni hierarchy appropriate to !this.onType
--
----------------------------------------------------------------------
define method .createBasicTrunni(!direction is DIRECTION) is BOOLEAN

  --create the support element hierarchy

  if (!this.fromPrelim.not()) then
    !!mdsCreateSuppElement(!this.softType, !this.elementToSupport)
    !suppName = ''
  else
    !suppName = !this.fromPreliminarySupport()
    if (!suppName.eq('')) then
      return FALSE
    endif
  endif

  --set member data for the new hierarchy
  !this.tranci = !!ce
  !this.trunni = !!ce.owner
  !this.suppo = !!ce.owner.owner

  --set the support names
  !!mdsSetSupportNames(!this.suppo, !suppName)
  !suppoName = !this.suppo.name

  !this.trunni.pspe = !this.elementOwningBran.pspe
  --select the TRUNNI tube
  if (!this.onType.eq('BRAN')) then
    if (!this.elementToSupport.type.eq('BRAN')) then
      !this.trunni.hstu = !this.elementToSupport.hstu
    else
      !this.trunni.hstu = !this.elementToSupport.lstu
    endif
  else
    !this.trunni.hstu = !this.elementToSupport.lstu
  endif

  --revisit, this can be put in when we have a selector for TRANCI in the MDS-Ancillaries spec
  !!ce = !this.tranci
  select spref with styp 'PIPE-REST' spec /MDS-Ancillaries
  select lstu

  !this.atta.append(!this.tranci)

  --set up the trunni head and tail
  !this.trunni.hpos = !this.getTrunniConnectionPoint()
  !this.trunni.hdir = !direction
  !this.trunni.tdir = !direction.opposite()
  !this.trunni.tcon = 'ATT'

  !!ce = !this.tranci
  conn
  --revisit, this should not be the case
  handle (7, 3)
    fconn
  endhandle

  -- set up the SSREFE
  !this.ssrefe = object MDSSSREFE(!this.tranci, !this.softType)
  !!ce = !this.ssrefe.ssrefe
  stext '$!this.suppo.name'
  :MDSSuppType |$!this.softType|

  !!ce = !this.tranci

  -- Cut hole comment
  :MDSTrunNote1 ''

  -- create makeup piece
  !this.createMakeup()

  -- Set SSREFE to bore of leave of TRREDU
  if (!this.ssrefe.trunBoreSSREFE(!!ce).not()) then
    -- cannot find a spcom for the ssrefe
    return FALSE
  endif

  if (!!mdsTrunNotes.boolean()) then
    :MDSTubeNote1 ''
  endif

  -- Baseplate atta
  new PLAT
  !this.basePlate = !!ce

  -- SET the variable for TRUNNION Bore Size
  !tBore = pa bore
  handle any
    !tBore = pa bore of prev
  endhandle
  !this.trunni.tbor = !tBore

  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[1]).not()) then
    return FALSE
  endif
  sel lstu
  ori pl is d
  distance 1000
  :MDSTrunNote1 ''
  name $!suppoName/PLAT.1
  !this.atta.append(!!ce)
  !!mdsSetDesPars(!!ce)

  if (defined(!this.mdsTrunnionObject.sTypes[1].split(',')[2])) then
    -- we need to create a base plate and move the tail to its end
    new PLAT

    if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[2]).not()) then
      return FALSE
    endif
    sel lstu
    ori pl is d
    pos pa at pl of prev
    !this.atta.append(!!ce)
    !!mdsSetDesPars(!!ce)
    name $!suppoName/PLAT.2
    !this.bearingPlate = !!ce
    $!suppoName/PLAT.1
    pos pl at pa of next
  endif


  --connect the trunnion tail to the last member
  !!ce = !this.trunni
  conn pt to pl of last mem

  return TRUE
endmethod

----------------------------------------------------------------------
--
-- Method:      createTrunniTT06
--
-- Description: create the trunni hierarchy for TT06 and TT07
--
----------------------------------------------------------------------
define method .createTrunniTT06(!direction is DIRECTION) is BOOLEAN

  --create the support element hierarchy
  if (!this.fromPrelim.not()) then
    !!mdsCreateSuppElement(!this.softType, !this.elementToSupport)
    !suppName = ''
  else
    !suppName = !this.fromPreliminarySupport()
    if (!suppName.eq('')) then
      return FALSE
    endif
  endif

  --set member data for the new hierarchy
  !this.tranci = !!ce
  !this.trunni = !!ce.owner
  !this.suppo = !!ce.owner.owner

  --set the support names
  !!mdsSetSupportNames(!this.suppo, !suppName)
  !suppoName = !this.suppo.name

  !this.trunni.pspe = !this.elementOwningBran.pspe
  --select the TRUNNI tube
  if (!this.onType.eq('BRAN')) then
    if (!this.elementToSupport.type.eq('BRAN')) then
      !this.trunni.hstu = !this.elementToSupport.hstu
    else
      !this.trunni.hstu = !this.elementToSupport.lstu
    endif
  else
    !this.trunni.hstu = !this.elementToSupport.lstu
  endif

  --revisit, this can be put in when we have a selector for TRANCI in the MDS-Ancillaries spec
  !!ce = !this.tranci
  select spref with styp 'PIPE-REST' spec /MDS-Ancillaries
  select lstu
  !this.atta.append(!this.tranci)

  --set up the trunni head and tail
  !this.trunni.hpos = !this.getTrunniConnectionPoint()
  !this.trunni.hdir = !direction
  !this.trunni.tdir = !direction.opposite()
  !this.trunni.tcon = 'ATT'

  !!ce = !this.tranci
  conn
  --revisit, this should not be the case
  handle (7, 3)
    fconn
  endhandle

  -- set up the SSREFE
  !this.ssrefe = object MDSSSREFE(!this.tranci, !this.softType)
  !!ce = !this.ssrefe.ssrefe
  stext '$!this.suppo.name'
  :MDSSuppType |$!this.softType|

  !!ce = !this.tranci

  -- Cut hole comment
  :MDSTrunNote1 ''

  -- create Makeup Piece
  !this.createMakeup()

  -- Set SSREFE to bore of leave of TRREDU
  if (  !this.ssrefe.trunBoreSSREFE(!!ce).not()) then
    -- cannot find a spcom for the ssrefe
    return FALSE
  endif

  -- SET the variable for TRUNNION Bore Size
  !tBore = pa bore
  handle any
    !tBore = pa bore of prev
  endhandle
  !this.trunni.tbor = !tBore

  -- Create field fitt weld for allowance
  new WELD
  select spref with styp 'FFW' spec /MDS-Ancillaries
  name $!suppoName/FFW
  !this.atta.append(!!ce)
  fconn

  -- Create collar
  new PLAT
  !this.collar = !!ce
  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[1]).not()) then
    return FALSE
  endif
  sel lstu
  ori pl is d
  distance 300
  name $!suppoName/PLAT.2
  !this.atta.append(!!ce)
  -- Because the size of the collar is worked off the leave bore
  -- flip the component
  flip
  !!mdsSetDesPars(!!ce)
  flip
  conn
  handle (7,3)
    fconnect
  endhandle

  if (!!mdsTrunNotes.boolean()) then
    :MDSTubeNote1 ''
  endif

  -- WELD FFW Allowance Set and DESP 4 set to ZERO on the COLLAR PLATE
  !allow = desp
  desp n4 0mm
  prev
  allow $!allow[4]
  conn to next
  next

  -- Create baseplate
  new PLAT
  !this.basePlate = !!ce
  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[2]).not()) then
    return FALSE
  endif
  sel lstu
  ori pl is d
  distance 500
  if (!!mdsTrunNotes.boolean()) then
    :MDSTrunNote1 ''
  endif
  name $!suppoName/PLAT.1
  !this.atta.append(!!ce)
  !!mdsSetDesPars(!!ce)

  if (defined(!this.mdsTrunnionObject.sTypes[1].split(',')[3])) then
    -- Create bearing plate
    new PLAT
    !this.bearingPlate = !!ce
    if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[3]).not()) then
      return FALSE
    endif
    sel lstu
    ori pl is d
    distance 100
    name $!suppoName/PLAT.3
    !this.atta.append(!!ce)
    !!mdsSetDesPars(!!ce)

    --connect to previous member
    prev
    conn to next
    handle (7,3)
      fconnect to next
    endhandle
  endif

  --connect the trunnion tail to the last member
  !!ce = !this.trunni
  conn pt to pl of last mem

  return TRUE
endmethod

----------------------------------------------------------------------
--
-- Method:      createTrunniTT08
--
-- Description: create the trunni hierarchy for TT08 and TT09
--
----------------------------------------------------------------------
define method .createTrunniTT08(!direction is DIRECTION) is BOOLEAN

  --create the support element hierarchy
  if (!this.fromPrelim.not()) then
    !!mdsCreateSuppElement(!this.softType, !this.elementToSupport)
    !suppName = ''
  else
    !suppName = !this.fromPreliminarySupport()
    if (!suppName.eq('')) then
      return FALSE
    endif
  endif

  --set member data for the new hierarchy
  !this.tranci = !!ce
  !this.trunni = !!ce.owner
  !this.suppo = !!ce.owner.owner

  --set the support names
  !!mdsSetSupportNames(!this.suppo, !suppName)
  !suppoName = !this.suppo.name

  !this.trunni.pspe = !this.elementOwningBran.pspe
  --select the TRUNNI tube
  if (!this.onType.eq('BRAN')) then
    if (!this.elementToSupport.type.eq('BRAN')) then
      !this.trunni.hstu = !this.elementToSupport.hstu
    else
      !this.trunni.hstu = !this.elementToSupport.lstu
    endif
  else
    !this.trunni.hstu = !this.elementToSupport.lstu
  endif

  --revisit, this can be put in when we have a selector for TRANCI in the MDS-Ancillaries spec
  !!ce = !this.tranci
  select spref with styp 'PIPE-REST' spec /MDS-Ancillaries
  select lstu

  !this.atta.append(!this.tranci)

  --set up the trunni head and tail
  !this.trunni.hpos = !this.getTrunniConnectionPoint()
  !this.trunni.hdir = !direction
  !this.trunni.tdir = !direction.opposite()
  !this.trunni.tcon = 'ATT'

  !!ce = !this.tranci
  conn
  --revisit, this should not be the case
  handle (7, 3)
    fconn
  endhandle

  -- set up the SSREFE
  !this.ssrefe = object MDSSSREFE(!this.tranci, !this.softType)
  !!ce = !this.ssrefe.ssrefe
  stext '$!this.suppo.name'
  :MDSSuppType |$!this.softType|

  !!ce = !this.tranci

  -- Cut hole comment
  :MDSTrunNote1 ''
  if (!!mdsTrunNotes.boolean()) then
    :MDSTubeNote1 ''
  endif

  -- create Makeup Piece
  !this.createMakeup()

  -- Set SSREFE to bore of leave of TRREDU
  if (  !this.ssrefe.trunBoreSSREFE(!!ce).not()) then
    -- cannot find a spcom for the ssrefe
    return FALSE
  endif

  if (!!mdsTrunNotes.boolean()) then
    :MDSTubeNote1 ''
  endif

  -- SET the variable for TRUNNION Bore Size
  !tBore = pa bore
  handle any
    !tBore = pa bore of prev
  endhandle
  !this.trunni.tbor = !tBore

  -- Create breaking plates
  new PLAT
  -- spkbrk true
  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[1]).not()) then
    return FALSE
  endif
  sel lstu
  ori pl is d
  distance 500
  if (!!mdsTrunNotes.boolean()) then
    :MDSTubeNote1 ''
  endif
  name $!suppoName/PLAT.2

  !this.atta.append(!!ce)
  !!mdsSetDesPars(!!ce)

  ------------------------------------------------------------------------------
  -- IMPORTANT Discovery ** The Property 'PDIST' is NOT used in the calculations below
  -- and doesn't seem to have EVER been used!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  -- This POSITION of the breaking plates is set by the AC27 property PDIST
  -- from the bop of the main header. Get the value and position the breaking plate.
  --!pdist = prop pdist

  !plates = !!ce
  goto compref of tranci 1
  var !od pl od
  var !insu insu
  !!ce = !plates
  --case for branch needed
  if (!this.onType inset('ELBO','BEND')) then
    pin 9 at compref of tranci 1
  else
    pin 9 at pl of compref of tranci 1
  endif
  pin 9 dir d
  pin 9 dist ($!od / 2)

  -- Determine the position of the Underside of the FIRST PLATE to the Main Bore BOP i.e. Should be 200mm
  -- Half of the SHIM plates thickness needs to be added to the 200mm dimension here because moving the
  -- Pin 9 position just 200mm, as above, placed Pin 9 at the CENTRE<P0 origin> position of the 'Break Out Plates' Assembly i.e. AC27
  -- Calculate the distance to move Pin 9

  -- The Property STHK represents the SHIM plates thickness for the 'Break Out Plates' Assembly - AC27
  !shim = prop STHK
  !half = !shim.real() / 2
  !sdis = 200 + !half
  pin 9 dist $!<sdis>mm

  if (!this.onType.eq('ELBO')) then
    pin 9 dist -$!insu
  endif
  thro pin 9
  ------------------------------------------------------------------------------

  -- Create baseplate
  new PLAT
  !this.basePlate = !!ce
  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[2]).not()) then
    return FALSE
  endif
  sel lstu
  ori pl is d
  distance 1000
  if (!!mdsTrunNotes.boolean()) then
    :MDSTrunNote1 ''
  endif
  name $!suppoName/PLAT.1
  !this.atta.append(!!ce)
  !!mdsSetDesPars(!!ce)

  if (defined(!this.mdsTrunnionObject.sTypes[1].split(',')[3])) then
    -- Create bearing plate
    new PLAT
    !this.bearingPlate = !!ce
    if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[3]).not()) then
      return FALSE
    endif
    sel lstu
    ori pl is d
    distance 100
    name $!suppoName/PLAT.3
    !this.atta.append(!!ce)
    !!mdsSetDesPars(!!ce)

    --connect to previous member
    prev
    conn to next
    handle (7,3)
      fconnect to next
    endhandle
  endif

  --connect the trunnion tail to the last member
  !!ce = !this.trunni
  conn pt to pl of last mem

  return TRUE
endmethod

----------------------------------------------------------------------
--
-- Method:      createTrunniTT10
--
-- Description: create the trunni hierarchy for TT10 and TT11
--
----------------------------------------------------------------------
define method .createTrunniTT10(!direction is DIRECTION) is BOOLEAN

  --create the support element hierarchy
  if (!this.fromPrelim.not()) then
    !!mdsCreateSuppElement(!this.softType, !this.elementToSupport)
    !suppName = ''
  else
    !suppName = !this.fromPreliminarySupport()
    if (!suppName.eq('')) then
      return FALSE
    endif
  endif

  --set member data for the new hierarchy
  !this.tranci = !!ce
  !this.trunni = !!ce.owner
  !this.suppo = !!ce.owner.owner

  --set the support names
  !!mdsSetSupportNames(!this.suppo, !suppName)
  !suppoName = !this.suppo.name

  !this.trunni.pspe = !this.elementOwningBran.pspe
  --select the TRUNNI tube
  if (!this.onType.eq('BRAN')) then
    if (!this.elementToSupport.type.eq('BRAN')) then
      !this.trunni.hstu = !this.elementToSupport.hstu
    else
      !this.trunni.hstu = !this.elementToSupport.lstu
    endif
  else
    !this.trunni.hstu = !this.elementToSupport.lstu
  endif

  !index = 0
  do !i indices !this.availableBores
    if (!this.availableBores[!i].eq(!this.workingBore.string())) then
      !index = !i
    endif
  enddo
  if (!index.eq(0)) then
    !index = 1
  endif

  !sType = !this.mdsTrunnionObject.sTypes[!index]

  !splitStype = !sType.split(',')

  !!ce = !this.tranci
  if (!this.selectSpref('', !splitStype[1]).not()) then
    return FALSE
  endif

  select lstu
  !this.atta.append(!this.tranci)

  --set up the trunni head and tail
  !this.trunni.hpos = !this.getTrunniConnectionPoint()
  !this.trunni.hdir = !direction
  !this.trunni.tdir = !direction.opposite()
  !this.trunni.tcon = 'ATT'
  !!mdsSetDesPars(!!ce)
  if (!!ce.compref.type.eq('BRAN')) then
    conn
    handle (7, 3)
      fconn
    endhandle
    ori and p4 is hdir of compref
  else
    conn and p4 is pl of compref
    --revisit, this should not be the case
    handle (7, 3)
      fconn and p4 is pl of compref
    endhandle
  endif

  -- set up the SSREFE
  !this.ssrefe = object MDSSSREFE(!this.tranci, !this.softType)
  !!ce = !this.ssrefe.ssrefe
  stext '$!this.suppo.name'
  :MDSSuppType |$!this.softType|

  !!ce = !this.tranci

  -- Cut hole comment
  :MDSTrunNote1 ''

  -- Makeup Piece
  new TRREDU select spref with styp 'MAKEUPOFF' spec /MDS-Ancillaries

  despar $!this.availableBores[1] 0mm 0mm
  sel lstu
  connect
  handle (7,3)
    fconnect
  endhandle
  mtoc off

  -- Design para 10 of the prev comp needs to be to the pl od of the make up piece
  -- to spread the clamps apart
  !plOdClamp = pl od
  prev
  despar n10 $!plOdClamp
  -- Set the distance so it is 200mm away from the clamp end
  if (!this.tranci.styp inset('AC28','AC29','AC31')) then
    !this.inTubeDist = !this.inTubeDist + !this.tranci.desp[10] + !this.tranci.desp[4]
  endif
  next

  name $!suppoName/MAKEUP

  !redu = !!ce
  if (!this.elementToSupport.type.eq('BRAN')) then
    !od = !this.elementToSupport.phod
  else
    !od = pl od of $!<this.elementToSupport>
  endif
  !tOd = lodiam
  !!ce = !this.tranci
  !hyp = (!od / 2)
  !pThk = (prop PTHK)
  !hyp = !hyp + !pThk.real()
  !side1 = (!tOd / 2)
  !offset = (sqrt ((!hyp * !hyp) - (!side1 * !side1)))
  !!ce = !redu
  desp num 2 $!offset
  !this.atta.append(!!ce)

  -- Set SSREFE to bore of leave of TRREDU
  if (  !this.ssrefe.trunBoreSSREFE(!!ce).not()) then
    -- cannot find a spcom for the ssrefe
    return FALSE
  endif

  -- Create field fitt weld for allowance
  new WELD
  select spref with styp 'FFW' spec /MDS-Ancillaries
  name $!suppoName/FFW
  !this.atta.append(!!ce)
  fconn

  -- Collar Plate
  if (!this.createPlat('$!suppoName/PLAT.1', !splitStype[2]).not()) then
    return FALSE
  endif
  !this.collar = !!ce

  -- SET the variable for TRUNNION Bore Size
  !tBore = pa bore
  handle any
    !tBore = pa bore of prev
  endhandle
  !this.trunni.tbor = !tBore

  -- Because the size of the collar is worked off the leave bore
  -- flip the component
  flip
  !!mdsSetDesPars(!!ce)
  flip
  conn
  handle (7,3)
    fconnect
  endhandle

  if (!!mdsTrunNotes.boolean()) then
    :MDSTubeNote1 ''
  endif

  -- WELD FFW Allowance Set and DESP 4 set to ZERO on the COLLAR PLATE
  !allow = desp
  desp n4 0mm
  prev
  allow $!allow[4]
  conn to next
  next

  -- Create base plate
  if (!this.createPlat('$!suppoName/PLAT.2', !splitStype[3]).not()) then
    return FALSE
  endif

  !this.basePlate = !!ce

  if (!!mdsTrunNotes.boolean()) then
    :MDSTrunNote1 ''
  endif

  if (!this.softType.eq('TT11')) then
    -- Create bearing plate
    if (!this.createPlat('$!suppoName/PLAT.3', !splitStype[4]).not()) then
      return FALSE
    endif
    !this.bearingPlate = !!ce
  endif

  --connect the trunnion tail to the last member
  !!ce = !this.trunni
  conn pt to pl of last mem

  return TRUE
endmethod

----------------------------------------------------------------------
--
-- Method:      createTrunniTT12
--
-- Description: create the trunni hierarchy for TT12 and TT13
--
----------------------------------------------------------------------
define method .createTrunniTT12(!direction is DIRECTION) is BOOLEAN

  --create the support element hierarchy
  if (!this.fromPrelim.not()) then
    !!mdsCreateSuppElement(!this.softType, !this.elementToSupport)
    !suppName = ''
  else
    !suppName = !this.fromPreliminarySupport()
    if (!suppName.eq('')) then
      return FALSE
    endif
  endif

  --set member data for the new hierarchy
  !this.tranci = !!ce
  !this.trunni = !!ce.owner
  !this.suppo = !!ce.owner.owner

  --set the support names
  !!mdsSetSupportNames(!this.suppo, !suppName)
  !suppoName = !this.suppo.name

  !this.trunni.pspe = !this.elementOwningBran.pspe
  --select the TRUNNI tube
  if (!this.onType.eq('BRAN')) then
    if (!this.elementToSupport.type.eq('BRAN')) then
      !this.trunni.hstu = !this.elementToSupport.hstu
    else
      !this.trunni.hstu = !this.elementToSupport.lstu
    endif
  else
    !this.trunni.hstu = !this.elementToSupport.lstu
  endif

  !index = 0
  do !i indices !this.availableBores
    if (!this.availableBores[!i].eq(!this.workingBore.string())) then
      !index = !i
    endif
  enddo
  if (!index.eq(0)) then
    !index = 1
  endif

  !sType = !this.mdsTrunnionObject.sTypes[!index]
  !splitStype = !sType.split(',')

  !!ce = !this.tranci
  if (!this.selectSpref('', !splitStype[1]).not()) then
    return FALSE
  endif
  select lstu
  !this.atta.append(!this.tranci)

  --set up the trunni head and tail
  !this.trunni.hpos = !this.getTrunniConnectionPoint()
  !this.trunni.hdir = !direction
  !this.trunni.tdir = !direction.opposite()
  !this.trunni.tcon = 'ATT'
  !!mdsSetDesPars(!!ce)
  if (!!ce.compref.type.eq('BRAN')) then
    conn
    handle (7, 3)
      fconn
    endhandle
    ori and p3 is hdir of compref
  else
    conn and p3 is pl of compref
    handle (7, 3)
      fconn and p3 is pl of compref
    endhandle
  endif

  -- set up the SSREFE
  !this.ssrefe = object MDSSSREFE(!this.tranci, !this.softType)
  !!ce = !this.ssrefe.ssrefe
  stext '$!this.suppo.name'
  :MDSSuppType |$!this.softType|

  !!ce = !this.tranci

  -- Cut hole comment
  :MDSTrunNote1 ''

  -- Makeup Piece
  new TRREDU select spref with styp 'MAKEUPOFF' spec /MDS-Ancillaries

  despar $!this.availableBores[1] 0mm 0mm
  sel lstu
  connect
  handle (7,3)
    fconnect
  endhandle
  mtoc off

  -- Design para 10 of the prev comp needs to be to the pl od of the make up piece
  -- to spread the clamps apart
  !plOdClamp = pl od
  prev
  despar n10 $!plOdClamp
  -- Set the distance so it is 200mm away from the clamp end
  if (!this.tranci.styp inset('AC28','AC29','AC31')) then
    !this.inTubeDist = !this.inTubeDist + !this.tranci.desp[10] + !this.tranci.desp[4]
  endif
  next

  --revisit, need to figure out if this is needed on the supporteditor
  name $!suppoName/MAKEUP

  !redu = !!ce
  if (!this.elementToSupport.type.eq('BRAN')) then
    !od = !this.elementToSupport.phod
  else
    !od = pl od of $!<this.elementToSupport>
  endif
  !tOd = lodiam
  !!ce = !this.tranci
  !hyp = (!od / 2)
  !pThk = (prop PTHK)
  !hyp = !hyp + !pThk.real()
  !side1 = (!tOd / 2)
  !offset = (sqrt ((!hyp * !hyp) - (!side1 * !side1)))
  !!ce = !redu
  desp num 2 $!offset
  !this.atta.append(!!ce)

  -- Set SSREFE to bore of leave of TRREDU
  if (  !this.ssrefe.trunBoreSSREFE(!!ce).not()) then
    -- cannot find a spcom for the ssrefe
    return FALSE
  endif

  if (!!mdsTrunNotes.boolean()) then
    :MDSTubeNote1 ''
  endif

  -- Baseplate atta
  if (!this.createPlat('$!suppoName/PLAT.1', !splitStype[2]).not()) then
    return FALSE
  endif

  -- SET the variable for TRUNNION Bore Size
  !tBore = pa bore
  handle any
    !tBore = pa bore of prev
  endhandle
  !this.trunni.tbor = !tBore

  -- Because the size of the collar is worked off the leave bore
  -- flip the component
  flip
  !!mdsSetDesPars(!!ce)
  flip
  conn
  handle (7,3)
    fconnect
  endhandle
  if (!!mdsTrunNotes.boolean()) then
    :MDSTrunNote1 ''
  endif
  !this.basePlate = !!ce

  if (!this.softType.eq('TT13')) then
    -- Create bearing plate
    if (!this.createPlat('$!suppoName/PLAT.2', !splitStype[3]).not()) then
      return FALSE
    endif
    !this.bearingPlate = !!ce
  endif

  --connect the trunnion tail to the last member
  !!ce = !this.trunni
  conn pt to pl of last mem

  return TRUE
endmethod


----------------------------------------------------------------------
--
-- Method:      createTrunniTT14
--
-- Description: create the trunni hierarchy appropriate to !this.onType
--
----------------------------------------------------------------------
define method .createTrunniTT14(!direction is DIRECTION) is BOOLEAN

  --create the support element hierarchy
  if (!this.fromPrelim.not()) then
    !!mdsCreateSuppElement(!this.softType, !this.elementToSupport)
    !suppName = ''
  else
    !suppName = !this.fromPreliminarySupport()
    if (!suppName.eq('')) then
      return FALSE
    endif
  endif

  --set member data for the new hierarchy
  !this.tranci = !!ce
  !this.trunni = !!ce.owner
  !this.suppo = !!ce.owner.owner

  --set the support names
  !!mdsSetSupportNames(!this.suppo, !suppName)
  !suppoName = !this.suppo.name

  !ce = !!ce
  !!ce = !this.elementToSupport
  var !radius const dist p0 to p1
  !!ce = !ce

  !this.trunni.pspe = !this.elementOwningBran.pspe
  --select the TRUNNI tube
  if (!this.onType.eq('BRAN')) then
    if (!this.elementToSupport.type.eq('BRAN')) then
      !this.trunni.hstu = !this.elementToSupport.hstu
    else
      !this.trunni.hstu = !this.elementToSupport.lstu
    endif
  else
    !this.trunni.hstu = !this.elementToSupport.lstu
  endif

  --revisit, this can be put in when we have a selector for TRANCI in the MDS-Ancillaries spec
  !!ce = !this.tranci
  select spref with styp 'PIPE-REST' spec /MDS-Ancillaries
  select lstu

  !this.atta.append(!this.tranci)

  --set up the trunni head and tail
  !this.trunni.hpos = !this.getTrunniConnectionPoint()
  !this.trunni.hdir = !direction
  !this.trunni.tdir = !direction.opposite()

  !this.trunni.tcon = 'ATT'

  !!ce = !this.tranci
  conn
  --revisit, this should not be the case
  handle (7, 3)
    fconn
  endhandle

  -- set up the SSREFE
  !this.ssrefe = object MDSSSREFE(!this.tranci, !this.softType)
  !!ce = !this.ssrefe.ssrefe
  stext '$!this.suppo.name'
  :MDSSuppType |$!this.softType|

  !!ce = !this.tranci

  -- Cut hole comment
  :MDSTrunNote1 ''

  -- makeup piece
  if (!this.onType.eq('ELBO') or !this.onType.eq('REDU')) then
    new TRREDU select spref with styp 'MAKEUP' spec /MDS-Ancillaries
  else
    new TRREDU select spref with styp 'MAKEUPOFF' spec /MDS-Ancillaries
  endif
  despar $!this.availableBores[1] 0mm 0mm
  sel lstu
  connect
  handle (7,3)
    fconnect
  endhandle
  mtoc off
  --revisit, need to figure out if this is needed on the supporteditor
  name $!suppoName/MAKEUP

  if (!this.elementToSupport.type.eq('BRAN')) then
    !hOd = !this.elementToSupport.phod
  else
    !hOd = pl od of $!<this.elementToSupport>
  endif
  !tOd = !!ce.lodiam

  -- Work out the adjustment needed for the exact length to be added to the iso
  -- Work out horizontal offset
  -- and put in gadget
  !hoff = ((!hOd - !tOd) / 2)
  if (!this.hori.not()) then
    !hoff = 0
  endif

  !hyp = !radius.real() + (!hOd / 2)
  !side1 = !radius.real() + (!tOd / 2) - !hoff

  if (!this.hori) then

    if (!this.elementAPos.up.gt(!this.elementLPos.up)) then

      if (!this.elementP0Pos.up.gt(!this.elementLPos.up)) then

        !offset = (sqrt ((!hyp * !hyp) - (!side1 * !side1)))
      else
        !offset = 0
      endif

    else

      if (!this.elementP0Pos.up.gt(!this.elementAPos.up)) then
        !offset = (sqrt ((!hyp * !hyp) - (!side1 * !side1)))
      else
        !offset = 0
      endif

    endif

  else
    !offset = (sqrt ((!hyp * !hyp) - (!side1 * !side1)))
  endif

  !this.dummyLegOffset = !hoff
  desp num 2 $!offset
  desp num 3 $!hoff
  !this.atta.append(!!ce)

  -- Set SSREFE to bore of leave of TRREDU
  if (  !this.ssrefe.trunBoreSSREFE(!!ce).not()) then
    -- cannot find a spcom for the ssrefe
    return FALSE
  endif

  !ref = !!ce
  !!ce = !this.ssrefe.ssrefe

  desp num 4 $!this.dummyLegOffset

  !!ce = !ref

  if (!!mdsTrunNotes.boolean()) then
    :MDSTubeNote1 ''
  endif

  -- Baseplate atta
  new PLAT
  !this.basePlate = !!ce

  -- SET the variable for TRUNNION Bore Size
  !tBore = pa bore
  handle any
    !tBore = pa bore of prev
  endhandle
  !this.trunni.tbor = !tBore

  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[1]).not()) then
    return FALSE
  endif
  sel lstu
  ori pl is $!direction
  distance 1000
  :MDSTrunNote1 ''
  name $!suppoName/PLAT.1
  !this.atta.append(!!ce)
  !!mdsSetDesPars(!!ce)

  --connect the trunnion tail to the last member
  !!ce = !this.trunni
  conn pt to pl of last mem

  return TRUE
endmethod

----------------------------------------------------------------------
--
-- Method:      createTrunniTT15
--
-- Description: create the trunni hierarchy appropriate to !this.onType
--
----------------------------------------------------------------------
define method .createTrunniTT15(!direction is DIRECTION) is BOOLEAN

  --create the support element hierarchy
  if (!this.fromPrelim.not()) then
    !!mdsCreateSuppElement(!this.softType, !this.elementToSupport)
    !suppName = ''
  else
    !suppName = !this.fromPreliminarySupport()
    if (!suppName.eq('')) then
      return FALSE
    endif
  endif

  --set member data for the new hierarchy
  !this.tranci = !!ce
  !this.trunni = !!ce.owner
  !this.suppo = !!ce.owner.owner

  --set the support names
  !!mdsSetSupportNames(!this.suppo, !suppName)
  !suppoName = !this.suppo.name

  !ce= !!ce
  !!ce = !this.elementToSupport
  var !radius const dist p0 to p1
  !!ce = !ce

  !this.trunni.pspe = !this.elementOwningBran.pspe
  --select the TRUNNI tube
  if (!this.onType.eq('BRAN')) then
    if (!this.elementToSupport.type.eq('BRAN')) then
      !this.trunni.hstu = !this.elementToSupport.hstu
    else
      !this.trunni.hstu = !this.elementToSupport.lstu
    endif
  else
    !this.trunni.hstu = !this.elementToSupport.lstu
  endif

  --revisit, this can be put in when we have a selector for TRANCI in the MDS-Ancillaries spec
  !!ce = !this.tranci
  select spref with styp 'PIPE-REST' spec /MDS-Ancillaries
  select lstu

  !this.atta.append(!this.tranci)

  --set up the trunni head and tail
  !this.trunni.hpos = !this.getTrunniConnectionPoint()
  !this.trunni.hdir = !direction
  !this.trunni.tdir = !direction.opposite()

  !this.trunni.tcon = 'ATT'

  !!ce = !this.tranci
  conn
  --revisit, this should not be the case
  handle (7, 3)
    fconn
  endhandle

  -- set up the SSREFE
  !this.ssrefe = object MDSSSREFE(!this.tranci, !this.softType)
  !!ce = !this.ssrefe.ssrefe
  stext '$!this.suppo.name'
  :MDSSuppType |$!this.softType|

  !!ce = !this.tranci

  -- Cut hole comment
  :MDSTrunNote1 ''

  -- makeup piece
  if (!this.onType.eq('ELBO') or !this.onType.eq('REDU')) then
    new TRREDU select spref with styp 'MAKEUP' spec /MDS-Ancillaries
  else
    new TRREDU select spref with styp 'MAKEUPOFF' spec /MDS-Ancillaries
  endif
  despar $!this.availableBores[1] 0mm 0mm
  sel lstu
  connect
  handle (7,3)
    fconnect
  endhandle
  mtoc off
  --revisit, need to figure out if this is needed on the supporteditor
  name $!suppoName/MAKEUP

  if (!this.elementToSupport.type.eq('BRAN')) then
    !hOd = !this.elementToSupport.phod
  else
    !hOd = pl od of $!<this.elementToSupport>
  endif
  !tOd = !!ce.lodiam

  -- Work out the adjustment needed for the exact length to be added to the iso
  -- Work out horizontal offset
  -- and put in gadget
  !hoff = ((!hOd - !tOd) / 2)
  if (!this.hori.not()) then
    !hoff = 0
  endif

  !hyp = !radius.real() + (!hOd / 2)
  !side1 = !radius.real() + (!tOd / 2) + !hoff
  !offset = (sqrt ((!hyp * !hyp) - (!side1 * !side1)))

  !this.dummyLegOffset = !hoff
  desp num 2 $!offset
  desp num 3 $!hoff
  !this.atta.append(!!ce)

  -- Set SSREFE to bore of leave of TRREDU
  if (  !this.ssrefe.trunBoreSSREFE(!!ce).not()) then
    -- cannot find a spcom for the ssrefe
    return FALSE
  endif

  !ref = !!ce
  !!ce = !this.ssrefe.ssrefe

  desp num 4 $!this.dummyLegOffset

  !!ce = !ref

  if (!!mdsTrunNotes.boolean()) then
    :MDSTubeNote1 ''
  endif

  -- Baseplate atta
  new PLAT
  !this.basePlate = !!ce

  -- SET the variable for TRUNNION Bore Size
  !tBore = pa bore
  handle any
    !tBore = pa bore of prev
  endhandle
  !this.trunni.tbor = !tBore

  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[1]).not()) then
    return FALSE
  endif
  sel lstu
  ori pl is $!direction
  distance 1000
  :MDSTrunNote1 ''
  name $!suppoName/PLAT.1
  !this.atta.append(!!ce)
  !!mdsSetDesPars(!!ce)

  --connect the trunnion tail to the last member
  !!ce = !this.trunni
  conn pt to pl of last mem

  return TRUE
endmethod

----------------------------------------------------------------------
--
-- Method:      createTrunniTT16
--
-- Description: create the trunni hierarchy appropriate to !this.onType
--
----------------------------------------------------------------------
define method .createTrunniTT16(!direction is DIRECTION) is BOOLEAN

  --create the support element hierarchy
  if (!this.fromPrelim.not()) then
    !!mdsCreateSuppElement(!this.softType, !this.elementToSupport)
    !suppName = ''
  else
    !suppName = !this.fromPreliminarySupport()
    if (!suppName.eq('')) then
      return FALSE
    endif
  endif

  --set member data for the new hierarchy
  !this.tranci = !!ce
  !this.trunni = !!ce.owner
  !this.suppo = !!ce.owner.owner

  --set the support names
  !!mdsSetSupportNames(!this.suppo, !suppName)
  !suppoName = !this.suppo.name

  !ce= !!ce
  !!ce = !this.elementToSupport
  var !radius const dist p0 to p1
  !!ce = !ce

  !this.trunni.pspe = !this.elementOwningBran.pspe
  --select the TRUNNI tube
  if (!this.onType.eq('BRAN')) then
    if (!this.elementToSupport.type.eq('BRAN')) then
      !this.trunni.hstu = !this.elementToSupport.hstu
    else
      !this.trunni.hstu = !this.elementToSupport.lstu
    endif
  else
    !this.trunni.hstu = !this.elementToSupport.lstu
  endif

  --revisit, this can be put in when we have a selector for TRANCI in the MDS-Ancillaries spec
  !!ce = !this.tranci
  select spref with styp 'PIPE-REST' spec /MDS-Ancillaries
  select lstu

  !this.atta.append(!this.tranci)

  --set up the trunni head and tail

  !this.trunni.hdir = !direction
  !this.trunni.tdir = !direction.opposite()
  !this.trunni.hpos = !this.getTrunniConnectionPoint()
  !this.trunni.tcon = 'ATT'

  !!ce = !this.tranci
  conn
  --revisit, this should not be the case
  handle (7, 3)
    fconn
  endhandle

  -- set up the SSREFE
  !this.ssrefe = object MDSSSREFE(!this.tranci, !this.softType)
  !!ce = !this.ssrefe.ssrefe
  stext '$!this.suppo.name'
  :MDSSuppType |$!this.softType|

  !!ce = !this.tranci

  -- Cut hole comment
  :MDSTrunNote1 ''

  -- makeup piece
  if (!this.onType.eq('ELBO') or !this.onType.eq('REDU')) then
    new TRREDU select spref with styp 'MAKEUP' spec /MDS-Ancillaries
  else
    new TRREDU select spref with styp 'MAKEUPOFF' spec /MDS-Ancillaries
  endif
  despar $!this.availableBores[1] 0mm 0mm
  sel lstu
  connect
  handle (7,3)
    fconnect
  endhandle
  mtoc off
  --revisit, need to figure out if this is needed on the supporteditor
  name $!suppoName/MAKEUP

  if (!this.elementToSupport.type.eq('BRAN')) then
    !hOd = !this.elementToSupport.phod
  else
    !hOd = pl od of $!<this.elementToSupport>
  endif
  !tOd = !!ce.lodiam

  -- Work out the adjustment needed for the exact length to be added to the iso
  -- Work out horizontal offset
  -- and put in gadget
  !hoff = ((!hOd - !tOd) / 2)

  if (!this.hori.not()) then
    !hoff = 0
  endif

  !hyp = !radius.real() + (!hOd / 2)
  !side1 = !radius.real() + (!tOd / 2) + !hoff
  !offset = (sqrt ((!hyp * !hyp) - (!side1 * !side1)))

  if (!!mdsOptions.templates.val.dbref().desc.eq('Initec Industrial Templates')) then
    !this.dummyLegOffset = !hoff
  endif

  desp num 2 $!offset
  desp num 3 $!hoff
  !this.atta.append(!!ce)

  -- Set SSREFE to bore of leave of TRREDU
  if (  !this.ssrefe.trunBoreSSREFE(!!ce).not()) then
    -- cannot find a spcom for the ssrefe
    return FALSE
  endif

  !ref = !!ce
  !!ce = !this.ssrefe.ssrefe

  if (!this.dummyLegOffset.set()) then
    desp num 4 $!this.dummyLegOffset
  endif

  !!ce = !ref

  if (!!mdsTrunNotes.boolean()) then
    :MDSTubeNote1 ''
  endif

  -- Baseplate atta
  new PLAT
  !this.basePlate = !!ce

  -- SET the variable for TRUNNION Bore Size
  !tBore = pa bore
  handle any
    !tBore = pa bore of prev
  endhandle
  !this.trunni.tbor = !tBore

  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[1]).not()) then
    return FALSE
  endif
  sel lstu
  ori pl is $!direction
  distance 1000
  :MDSTrunNote1 ''
  name $!suppoName/PLAT.1
  !this.atta.append(!!ce)
  !!mdsSetDesPars(!!ce)

  --connect the trunnion tail to the last member
  !!ce = !this.trunni
  conn pt to pl of last mem

  return TRUE
endmethod

----------------------------------------------------------------------
--
-- Method:      .createTrunniTT17
--
-- Description:
--
----------------------------------------------------------------------
define method .createTrunniTT17(!direction is DIRECTION) is BOOLEAN

  --create the support element hierarchy
  if (!this.fromPrelim.not()) then
    !!mdsCreateSuppElement(!this.softType, !this.elementToSupport)
    !suppName = ''
  else
    !suppName = !this.fromPreliminarySupport()
    if (!suppName.eq('')) then
      return FALSE
    endif
  endif

  --set member data for the new hierarchy
  !this.tranci = !!ce
  !this.trunni = !!ce.owner
  !this.suppo = !!ce.owner.owner

  --set the support names
  !!mdsSetSupportNames(!this.suppo, !suppName)
  !suppoName = !this.suppo.name

  !this.trunni.pspe = !this.elementOwningBran.pspe
  --select the TRUNNI tube
  if (!this.onType.eq('BRAN')) then
    if (!this.elementToSupport.type.eq('BRAN')) then
      !this.trunni.hstu = !this.elementToSupport.hstu
    else
      !this.trunni.hstu = !this.elementToSupport.lstu
    endif
  else
    !this.trunni.hstu = !this.elementToSupport.lstu
  endif

  !!ce = !this.tranci
  select spref with styp 'PIPE-REST' spec /MDS-Ancillaries
  select lstu

  !!mdsSetDesPars(!!ce)
  sel lstu
  ori pl is d

  !this.atta.append(!this.tranci)

  --set up the trunni head and tail
  !this.inTube = true
  !this.trunni.hpos = !this.getTrunniConnectionPoint()
  !this.trunni.hdir = !direction
  !this.trunni.tdir = !direction.opposite()
  !this.trunni.tcon = 'ATT'

  !!mdsSupportEditor.trunnionDirection.val = !direction.string().before(' WRT')

  !!ce = !this.tranci

  conn
  handle(7, 3)
    fconn
  endhandle

  -- set up the SSREFE
  !this.ssrefe = object MDSSSREFE(!this.tranci, !this.softType)
  !!ce = !this.ssrefe.ssrefe
  stext '$!this.suppo.name'
  :MDSSuppType |$!this.softType|

  !!ce = !this.tranci

  -- Cut hole comment
  :MDSTrunNote1 ''

  -- create makeup piece
  !this.createMakeup()

  -- Design para 10 of the prev comp needs to be to the pl od of the make up piece
  -- to spread the clamps apart
  !plOdClamp = pl od
  prev
  despar n10 $!plOdClamp
  -- Set the distance so it is 200mm away from the clamp end
  if (!this.tranci.styp inset('AC28','AC29','AC31')) then
    !this.inTubeDist = !this.inTubeDist + !this.tranci.desp[10] + !this.tranci.desp[4]
  endif
  next

  -- Set SSREFE to bore of leave of TRREDU
  if (  !this.ssrefe.trunBoreSSREFE(!!ce).not()) then
    -- cannot find a spcom for the ssrefe
    return FALSE
  endif

  if (!!mdsTrunNotes.boolean()) then
    :MDSTubeNote1 ''
  endif

  -- Baseplate atta
  new PLAT
  !this.basePlate = !!ce

  -- SET the variable for TRUNNION Bore Size
  !tBore = pa bore
  handle any
    !tBore = pa bore of prev
  endhandle
  !this.trunni.tbor = !tBore

  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[1]).not()) then
    return FALSE
  endif
  sel lstu
  ori pl is d
  distance 1000
  :MDSTrunNote1 ''
  name $!suppoName/PLAT.1
  !this.atta.append(!!ce)
  !!mdsSetDesPars(!!ce)

  --revisit - do not think this is required for a TT17 as it never has a 3rd item
  --  if defined(!this.mdsTrunnionObject.sTypes[1].split(',')[3]) then
  --    -- we need to create a base plate and move the tail to its end
  --    new PLAT despar 0mm 0mm 0mm 0mm 0mm 0mm 0mm 0mm 0mm 0mm
  --    if !this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[3]).not() then
  --      return FALSE
  --    endif
  --    sel lstu
  --    ori pl is d
  --    pos pa at pl of prev
  --    !this.atta.append(!!ce)
  --    !!mdsSetDesPars(!!ce)
  --    name $!suppoName/PLAT.2
  --    !this.bearingPlate = !!ce
  --    $!suppoName/PLAT.1
  --    pos pl at pa of next
  --  endif

  --connect the trunnion tail to the last member
  !!ce = !this.trunni
  conn pt to pl of last mem

  return TRUE
endmethod

----------------------------------------------------------------------
--
-- Method:      createTrunniTT20
--
-- Description: create the trunni hierarchy appropriate to !this.onType
--
----------------------------------------------------------------------
define method .createTrunniTT20(!direction is DIRECTION) is BOOLEAN

  --create the support element hierarchy
  if (!this.fromPrelim.not()) then
    !!mdsCreateSuppElement(!this.softType, !this.elementToSupport)
    !suppName = ''
  else
    !suppName = !this.fromPreliminarySupport()
    if (!suppName.eq('')) then
      return FALSE
    endif
  endif

  --set member data for the new hierarchy
  !this.tranci = !!ce
  !this.trunni = !!ce.owner
  !this.suppo = !!ce.owner.owner

  --set the support names
  !!mdsSetSupportNames(!this.suppo, !suppName)
  !suppoName = !this.suppo.name

  !this.trunni.pspe = !this.elementOwningBran.pspe
  !this.trunni.hstu = !this.elementToSupport.lstu

  !!ce = !this.tranci
  select spref with styp 'PIPE-REST' spec /MDS-Ancillaries
  select lstu

  !this.atta[1] = !this.tranci

  --set up the trunni head and tail
  !this.trunni.hpos = !this.getTrunniConnectionPoint()
  !this.trunni.hdir = !direction
  !this.trunni.tdir = !direction.opposite()
  !this.trunni.tcon = 'ATT'
  if (!this.secondElementToSupport.type.eq('BRAN')) then
    var !ptBore hbore of $!<this.secondElementToSupport>
  else
    var !ptBore lbore of $!<this.secondElementToSupport>
  endif
  !this.trunni.tBore = !ptBore.bore()

  -- Position the tranci through the compref, after the validation this should be on the connecting tube
  !pv = object POINTVECTOR(!this.trunni.hpos, !direction)
  if (!this.secondElementToSupport.type.eq('BRAN')) then
    !pv = !pv.through(!this.secondElementToSupport.hpos)
  else
    !pv = !pv.through(!this.secondElementToSupport.lpos)
  endif

  !this.trunni.tpos = !pv.position

  !!ce = !this.tranci
  conn
  handle (7, 3)
    fconn
  endhandle

  -- set up the SSREFE
  !this.ssrefe = object MDSSSREFE(!this.tranci, !this.softType)
  !!ce = !this.ssrefe.ssrefe
  :MDSSuppType |$!this.softType|
  stext '$!this.suppo.name'

  !!ce = !this.tranci

  -- Cut hole comment
  :MDSTrunNote1 ''

  -- Makeup Piece
  new TRREDU select spref with styp 'MAKEUP' spec /MDS-Ancillaries
  name $!suppoName/MAKEUP
  despar $!this.availableBores[1] 0mm 0mm
  sel lstu
  connect
  handle (7,3)
    fconnect
  endhandle
  mtoc off

  if (!this.elementToSupport.type.eq('BRAN')) then
    !od = !this.elementToSupport.phod
  else
    !od = pl od of $!<this.elementToSupport>
  endif
  !tOd = lodiam

  !hyp = (!od)
  !side1 = (!tOd)
  !offset = (sqrt ((!hyp * !hyp) - (!side1 * !side1)))
  despar n2 $!offset

  !this.atta[2] = !!ce

  -- Set SSREFE to bore of leave of TRREDU
  if (!this.ssrefe.trunBoreSSREFE(!!ce).not()) then
    -- cannot find a spcom for the ssrefe
    return FALSE
  endif

  !index = 0
  do !i indices !this.availableBores
    if (!this.availableBores[!i].eq(!this.workingBore.string())) then
      !index = !i
    endif
  enddo
  if (!index.eq(0)) then
    !index = 1
  endif

  !sType = !this.mdsTrunnionObject.sTypes[!index]
  new PLAT $!suppoName/PLAT.1
  if (!this.selectSpref('', !sType).not()) then
    return FALSE
  endif
  !!mdsSetDesPars(!!ce)
  sel lstu
  pin 9 at tpos of owner
  pin 9 dir tdir of owner
  if (!this.secondElementToSupport.type.eq('BRAN')) then
    !tOd = !this.secondElementToSupport.phod
  else
    !tOd = pl od of $!<this.secondElementToSupport>
  endif
  pin 9 dist ($!tOd / 2)
  !dp = despar
  pin 9 dist $!dp[7]
  flip ori
  pos at tpos of owner
  plane pl thro pin 9
  !this.atta[3] = !!ce

  -- Create make up piece at tail - connection to tee
  new TRREDU select spref with styp 'MAKEUPOFF' spec /MDS-Ancillaries
  despar $!ptBore 0mm 0mm
  sel lstu
  mtoc off
  name $!suppoName/MAKEUP2
  !this.atta[4] = !!ce

  !od = aodiam

  if (!od gt !tOd) then
    !!alert.error('The connecting bore of the pipe, is less than the bore of the trunnion. Aborting creation.')
    return FALSE
  endif

  !hyp = (!od / 2)
  !side1 = (!tOd / 2)
  !offset = (sqrt ((!side1 * !side1) - (!hyp * !hyp)))
  despar n2 $!offset

  connect to pt of owner
  handle (7,3)
    fconnect to pt of owner
  endhandle

  -- Create second tranci to connect to the secondary pipe pick
  !name = !suppoName + '.2'
  new TRANCI
  compref $!<this.secondElementToSupport>
  select spref with styp 'PIPE-REST' spec /MDS-Ancillaries
  name $!name
  select lstu
  connect
  handle (7,3)
    fconnect
  endhandle

  :MDSTrunNote1 ''
  !this.atta[5] = !!ce

  !!mdsResetPins(1,9)

  --connect the trunnion tail to the last member
  !!ce = !this.trunni
  conn pt to pl of last mem

  return TRUE
endmethod

----------------------------------------------------------------------
--
-- Method:      .createTrunniTT21
--
-- Description:
--
----------------------------------------------------------------------
define method .createTrunniTT21(!direction is DIRECTION) is BOOLEAN
  --create the support element hierarchy
  if (!this.fromPrelim.not()) then
    !!mdsCreateSuppElement(!this.softType, !this.elementToSupport)
    !suppName = ''
  else
    !suppName = !this.fromPreliminarySupport()
    if (!suppName.eq('')) then
      return FALSE
    endif
  endif

  --set member data for the new hierarchy
  !this.tranci = !!ce
  !this.trunni = !!ce.owner
  !this.suppo = !!ce.owner.owner

  --set the support names
  !!mdsSetSupportNames(!this.suppo, !suppName)
  !suppoName = !this.suppo.name

  !this.trunni.pspe = !this.elementOwningBran.pspe
  !this.trunni.hstu = !this.elementToSupport.lstu

  !!ce = !this.tranci
  select spref with styp 'PIPE-REST' spec /MDS-Ancillaries
  select lstu

  !this.atta.append(!!ce)

  --set up the trunni head and tail
  !this.trunni.hpos = !this.getTrunniConnectionPoint()
  !this.trunni.hdir = !direction
  !this.trunni.tdir = !direction.opposite()
  !this.trunni.tcon = 'ATT'

  !!ce = !this.tranci
  conn
  handle (7, 3)
    fconn
  endhandle

  -- set up the SSREFE
  !this.ssrefe = object MDSSSREFE(!this.tranci, !this.softType)
  !!ce = !this.ssrefe.ssrefe
  :MDSSuppType |$!this.softType|
  stext '$!this.suppo.name'

  !!ce = !this.elementToSupport
  var !radius const dist p0 to p1
  !od = pa od
  handle (2,252)
    !od = ph od
  endhandle

  !!ce = !this.tranci

  -- Set SSREFE to bore of leave of TRREDU
  if (!this.ssrefe.trunBoreSSREFE(!!ce).not()) then
    -- cannot find a spcom for the ssrefe
    return FALSE
  endif

  !tOd = !!ce.aodiam

  -- Work out the adjustment needed for the exact length to be added to the iso
  !hyp = !radius.real() + (!od / 2)
  !side1 = !radius.real() + (!tOd / 2)
  !offset = (sqrt ((!hyp * !hyp) - (!side1 * !side1)))


  -- Makeup Piece
  new TRREDU select spref with styp 'MAKEUP' spec /MDS-Ancillaries
  name $!suppoName/MAKEUP
  despar $!this.availableBores[1] $!offset 0mm
  sel lstu
  connect
  handle (7,3)
    fconnect
  endhandle
  mtoc off
  :MDSTrunNote1 ''
  :MDSTrunNote2 ''
  !this.atta.append(!!ce)

  -- Baseplate atta
  new PLAT
  !index = 0
  do !i indices !this.availableBores
    if (!this.availableBores[!i].eq(!this.workingBore.string())) then
      !index = !i
    endif
  enddo
  if (!index.eq(0)) then
    !index = 1
  endif

  !sType = !this.mdsTrunnionObject.sTypes[!index]
  if (!this.selectSpref('', !sType).not()) then
    return FALSE
  endif
  sel lstu
  ori pl is u
  conn
  handle any
    pos pa at pl of prev
  endhandle
  name $!suppoName/PLAT.1
  !this.atta.append(!!ce)
  !!mdsSetDesPars(!!ce)
  !this.basePlate = !!ce

  --connect the trunnion tail to the last member
  !!ce = !this.trunni
  conn pt to pl of last mem

  return TRUE
endmethod

----------------------------------------------------------------------
--
-- Method:      .createTrunniTT22
--
-- Description: create the trunni hierarchy for TT22, TT28, TT29, TT30, TT35, TT40 and TT41
--
----------------------------------------------------------------------
define method .createTrunniTT22(!direction is DIRECTION) is BOOLEAN

  --create the support element hierarchy
  if (!this.fromPrelim.not()) then
    !!mdsCreateSuppElement(!this.softType, !this.elementToSupport)
    !suppName = ''
  else
    !suppName = !this.fromPreliminarySupport()
    if (!suppName.eq('')) then
      return FALSE
    endif
  endif

  --set member data for the new hierarchy
  !this.tranci = !!ce
  !this.trunni = !!ce.owner
  !this.suppo = !!ce.owner.owner

  --set the support names
  !!mdsSetSupportNames(!this.suppo, !suppName)
  !suppoName = !this.suppo.name

  !this.trunni.pspe = !this.elementOwningBran.pspe
  --select the TRUNNI tube
  if (!this.onType.eq('BRAN')) then
    if (!this.elementToSupport.type.eq('BRAN')) then
      !this.trunni.hstu = !this.elementToSupport.hstu
    else
      !this.trunni.hstu = !this.elementToSupport.lstu
    endif
  else
    !this.trunni.hstu = !this.elementToSupport.lstu
  endif

  -- we have a selector for the TRANCI
  !!ce = !this.tranci
  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[1]).not()) then
    return FALSE
  endif
  !!mdsSetDesPars(!!ce)
  sel lstu
  ori pl is d

  --  --revisit, this can be put in when we have a selector for TRANCI in the MDS-Ancillaries spec
  --!!ce = !this.tranci
  --select spref with styp 'PIPE-REST' spec /MDS-Ancillaries
  --select lstu

  !this.atta.append(!this.tranci)

  --set up the trunni head and tail
  !this.inTube = true
  !this.trunni.hpos = !this.getTrunniConnectionPoint()
  !this.trunni.hdir = !direction
  !this.trunni.tdir = !direction.opposite()
  !this.trunni.tcon = 'ATT'

  !!ce = !this.tranci
  if (!!ce.compref.type.eq('BRAN')) then
    conn
    handle (7, 3)
      fconn
    endhandle
    ori and p3 is hdir of compref
  else
    conn and p3 is pl of compref
    --revisit, this should not be the case
    handle (7, 3)
      fconn and p3 is pl of compref
    endhandle
  endif

  -- set up the SSREFE
  !this.ssrefe = object MDSSSREFE(!this.tranci, !this.softType)
  !!ce = !this.ssrefe.ssrefe
  stext '$!this.suppo.name'
  :MDSSuppType |$!this.softType|

  !!ce = !this.tranci

  -- Cut hole comment
  :MDSTrunNote1 ''

  -- create makeup piece
  !this.createMakeup()

  -- Design para 10 of the prev comp needs to be to the pl od of the make up piece
  -- to spread the clamps apart
  !plOdClamp = pl od
  prev
  if (!this.softType.neq('TT29')) then
    despar n10 $!plOdClamp
  endif
  -- Set the distance so it is 200mm away from the clamp end
  if (!this.tranci.styp inset('AC28','AC29','AC31')) then
    !this.inTubeDist = !this.inTubeDist + !this.tranci.desp[10] + !this.tranci.desp[4]
  endif
  next

  -- Set SSREFE to bore of leave of TRREDU
  if (  !this.ssrefe.trunBoreSSREFE(!!ce).not()) then
    -- cannot find a spcom for the ssrefe
    return FALSE
  endif

  if (!!mdsTrunNotes.boolean()) then
    :MDSTubeNote1 ''
  endif


  if (!this.softType inset('TT22','TT28')) then
    -- add a weld
    new WELD
    select spref with styp 'FFW' spec /MDS-Ancillaries
    name $!suppoName/FFW
    !this.atta.append(!!ce)
    fconn
  endif

  -- Baseplate atta
  new PLAT
  !this.basePlate = !!ce

  -- SET the variable for TRUNNION Bore Size
  !tBore = pa bore
  handle any
    !tBore = pa bore of prev
  endhandle
  !this.trunni.tbor = !tBore

  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[2]).not()) then
    return FALSE
  endif
  sel lstu
  ori pl is d
  distance 1000
  :MDSTrunNote1 ''
  name $!suppoName/PLAT.1
  !this.atta.append(!!ce)
  !!mdsSetDesPars(!!ce)

  if (defined(!this.mdsTrunnionObject.sTypes[1].split(',')[3])) then
    -- we need to create a base plate and move the tail to its end
    new PLAT
    if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[3]).not()) then
      return FALSE
    endif
    sel lstu
    ori pl is d
    pos pa at pl of prev
    !this.atta.append(!!ce)
    !!mdsSetDesPars(!!ce)
    name $!suppoName/PLAT.2
    !this.bearingPlate = !!ce
    $!suppoName/PLAT.1
    pos pl at pa of next
  endif

  if (defined(!this.mdsTrunnionObject.sTypes[1].split(',')[4])) then
    -- we need to create a base plate and move the tail to its end
    $!suppoName/PLAT.2
    new PLAT
    if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[4]).not()) then
      return FALSE
    endif
    sel lstu
    ori pl is d
    pos pa at pl of prev
    !this.atta.append(!!ce)
    !!mdsSetDesPars(!!ce)
    name $!suppoName/PLAT.3
    !this.bearingPlate = !!ce
    $!suppoName/PLAT.1
    pos pl at pa of next
  endif



  --connect the trunnion tail to the last member
  !!ce = !this.trunni
  conn pt to pl of last mem

  return TRUE
endmethod

----------------------------------------------------------------------
--
-- Method:      .createTrunniTT24
--
-- Description:
--
----------------------------------------------------------------------
define method .createTrunniTT24(!direction is DIRECTION) is BOOLEAN

  --create the support element hierarchy
  if (!this.fromPrelim.not()) then
    !!mdsCreateSuppElement(!this.softType, !this.elementToSupport)
    !suppName = ''
  else
    !suppName = !this.fromPreliminarySupport()
    if (!suppName.eq('')) then
      return FALSE
    endif
  endif

  --set member data for the new hierarchy
  !this.tranci = !!ce
  !this.trunni = !!ce.owner
  !this.suppo = !!ce.owner.owner

  --set the support names
  !!mdsSetSupportNames(!this.suppo, !suppName)
  !suppoName = !this.suppo.name

  !this.trunni.pspe = !this.elementOwningBran.pspe
  --select the TRUNNI tube
  if (!this.onType.eq('BRAN')) then
    if (!this.elementToSupport.type.eq('BRAN')) then
      !this.trunni.hstu = !this.elementToSupport.hstu
    else
      !this.trunni.hstu = !this.elementToSupport.lstu
    endif
  else
    !this.trunni.hstu = !this.elementToSupport.lstu
  endif

  --!!ce = !this.tranci
  --select spref with styp 'PIPE-REST' spec /MDS-Ancillaries
  --select lstu


  -- we have a selector for the TRANCI
  !!ce = !this.tranci
  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[1]).not()) then
    return FALSE
  endif
  !!mdsSetDesPars(!!ce)
  sel lstu
  ori pl is d

  --  --revisit, this can be put in when we have a selector for TRANCI in the MDS-Ancillaries spec
  --!!ce = !this.tranci
  --select spref with styp 'PIPE-REST' spec /MDS-Ancillaries
  --select lstu

  !this.atta.append(!this.tranci)

  --set up the trunni head and tail
  !this.inTube = true
  !this.trunni.hpos = !this.getTrunniConnectionPoint()
  !this.trunni.hdir = !direction
  !this.trunni.tdir = !direction.opposite()
  !this.trunni.tcon = 'ATT'

  !!mdsSupportEditor.trunnionDirection.val = !direction.string().before(' WRT')

  !!ce = !this.tranci

  conn
  --revisit, this should not be the case
  handle (7, 3)
    fconn
  endhandle

  if (!!ce.compref.type.eq('BRAN')) then
    conn
    handle (7, 3)
      fconn
    endhandle
    ori and p3 is hdir of compref
  else
    conn and p3 is pl of compref
    --revisit, this should not be the case
    handle (7, 3)
      fconn and p3 is pl of compref
    endhandle
  endif

  -- set up the SSREFE
  !this.ssrefe = object MDSSSREFE(!this.tranci, !this.softType)
  !!ce = !this.ssrefe.ssrefe
  stext '$!this.suppo.name'
  :MDSSuppType |$!this.softType|

  !!ce = !this.tranci

  -- Cut hole comment
  :MDSTrunNote1 ''

  -- create makeup piece
  !this.createMakeup()

  -- Design para 10 of the prev comp needs to be to the pl od of the make up piece
  -- to spread the clamps apart
  !plOdClamp = pl od
  prev
  despar n10 $!plOdClamp
  -- Set the distance so it is 200mm away from the clamp end
  if (!this.tranci.styp inset('AC28','AC29','AC31')) then
    !this.inTubeDist = !this.inTubeDist + !this.tranci.desp[10] + !this.tranci.desp[4]
  endif
  next

  -- Set SSREFE to bore of leave of TRREDU
  if (  !this.ssrefe.trunBoreSSREFE(!!ce).not()) then
    -- cannot find a spcom for the ssrefe
    return FALSE
  endif

  if (!!mdsTrunNotes.boolean()) then
    :MDSTubeNote1 ''
  endif

  -- Baseplate atta
  new PLAT
  !this.basePlate = !!ce

  -- SET the variable for TRUNNION Bore Size
  !tBore = pa bore
  handle any
    !tBore = pa bore of prev
  endhandle
  !this.trunni.tbor = !tBore

  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[2]).not()) then
    return FALSE
  endif
  sel lstu
  ori pl is d
  distance 1000
  :MDSTrunNote1 ''
  name $!suppoName/PLAT.1
  !this.atta.append(!!ce)
  !!mdsSetDesPars(!!ce)

  if (defined(!this.mdsTrunnionObject.sTypes[1].split(',')[3])) then
    -- we need to create a base plate and move the tail to its end
    new PLAT
    if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[3]).not()) then
      return FALSE
    endif
    sel lstu
    ori pl is d
    pos pa at pl of prev
    !this.atta.append(!!ce)
    !!mdsSetDesPars(!!ce)
    name $!suppoName/PLAT.2
    !this.bearingPlate = !!ce
    $!suppoName/PLAT.1
    pos pl at pa of next
  endif

  --connect the trunnion tail to the last member
  !!ce = !this.trunni
  conn pt to pl of last mem
  !!ce = !this.tranci
  !!mdsSetDesPars(!!ce)

  return TRUE
endmethod
----------------------------------------------------------------------
--
-- Method:      createTrunniTT23
--
-- Description: create the trunni hierarchy appropriate to !this.onType
--
--
----------------------------------------------------------------------
define method .createTrunniTT23(!direction is DIRECTION) is BOOLEAN

  --create the support element hierarchy
  if (!this.fromPrelim.not()) then
    !!mdsCreateSuppElement(!this.softType, !this.elementToSupport)
    !suppName = ''
  else
    !suppName = !this.fromPreliminarySupport()
    if (!suppName.eq('')) then
      return FALSE
    endif
  endif

  --set member data for the new hierarchy
  !this.tranci = !!ce
  !this.trunni = !!ce.owner
  !this.suppo = !!ce.owner.owner

  --set the support names
  !!mdsSetSupportNames(!this.suppo, !suppName)
  !suppoName = !this.suppo.name

  !this.trunni.pspe = !this.elementOwningBran.pspe
  --select the TRUNNI tube
  if (!this.onType.eq('BRAN')) then
    if (!this.elementToSupport.type.eq('BRAN')) then
      !this.trunni.hstu = !this.elementToSupport.hstu
    else
      !this.trunni.hstu = !this.elementToSupport.lstu
    endif
  else
    !this.trunni.hstu = !this.elementToSupport.lstu
  endif


  --revisit, this can be put in when we have a selector for TRANCI in the MDS-Ancillaries spec
  !!ce = !this.tranci
  select spref with styp 'PIPE-REST' spec /MDS-Ancillaries
  select lstu

  !this.atta.append(!this.tranci)

  --set up the trunni head and tail
  !this.trunni.hpos = !this.getTrunniConnectionPoint()
  !this.trunni.hdir = !direction
  !this.trunni.tdir = !direction.opposite()
  !this.trunni.tcon = 'ATT'

  !!ce = !this.tranci
  conn
  --revisit, this should not be the case
  handle (7, 3)
    fconn
  endhandle

  -- set up the SSREFE
  !this.ssrefe = object MDSSSREFE(!this.tranci, !this.softType)
  !!ce = !this.ssrefe.ssrefe
  stext '$!this.suppo.name'
  :MDSSuppType |$!this.softType|

  !!ce = !this.tranci

  -- Cut hole comment
  :MDSTrunNote1 ''

  -- create makeup piece
  !this.createMakeup()

  -- Set SSREFE to bore of leave of TRREDU
  if (  !this.ssrefe.trunBoreSSREFE(!!ce).not()) then
    -- cannot find a spcom for the ssrefe
    return FALSE
  endif

  if (!!mdsTrunNotes.boolean()) then
    :MDSTubeNote1 ''
  endif

  -- Baseplate atta

  !this.basePlate = !!ce

  -- SET the variable for TRUNNION Bore Size
  !tBore = pa bore
  handle any
    !tBore = pa bore of prev
  endhandle
  !this.trunni.tbor = !tBore


  !sTypes = !this.mdsTrunnionObject.sTypes[1].split(',')

  --  Create plates
  do !plate index !sTypes
    if (!this.createPlat('$!suppoName/PLAT.$!<plate>',!sTypes[!plate]).not()) then
      return FALSE
    endif
    if (!plate.eq(!sTypes.size())) then
      !this.basePlate = !!ce
      dist 1000
      :MDSTrunNote1 ''
    elseif (!plate.eq(!sTypes.size())) then
      !this.bearingPlate = !!ce
      pos pa at pl of prev
    endif
  enddo

  --connect the trunnion tail to the last member
  !!ce = !this.trunni
  conn pt to pl of last mem

  return TRUE
endmethod
----------------------------------------------------------------------
--
-- Method:      createTrunniTT25
--
-- Description: create the trunni hierarchy appropriate to !this.onType
--
----------------------------------------------------------------------
define method .createTrunniTT25(!direction is DIRECTION) is BOOLEAN

  --create the support element hierarchy
  if (!this.fromPrelim.not()) then
    !!mdsCreateSuppElement(!this.softType, !this.elementToSupport)
    !suppName = ''
  else
    !suppName = !this.fromPreliminarySupport()
    if (!suppName.eq('')) then
      return FALSE
    endif
  endif

  --set member data for the new hierarchy
  !this.tranci = !!ce
  !this.trunni = !!ce.owner
  !this.suppo = !!ce.owner.owner

  --set the support names
  !!mdsSetSupportNames(!this.suppo, !suppName)
  !suppoName = !this.suppo.name

  !this.trunni.pspe = !this.elementOwningBran.pspe
  --select the TRUNNI tube

  if (!this.onType.eq('BRAN')) then
    if (!this.elementToSupport.type.eq('BRAN')) then
      !this.trunni.hstu = !this.elementToSupport.hstu
    else
      !this.trunni.hstu = !this.elementToSupport.lstu
    endif
  else
    !this.trunni.hstu = !this.elementToSupport.lstu
  endif

  --revisit, this can be put in when we have a selector for TRANCI in the MDS-Ancillaries spec
  !!ce = !this.tranci
  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[1]).not()) then
    return FALSE
  endif
  select lstu
  !this.atta.append(!this.tranci)
  !!mdsSetDesPars(!!ce)
  --  !!ce.:MDSSuppType = !this.sType

  -- Set the initial distance so the plate doesn't overlap the compref (piping component)
  !this.inTubeDist = !this.inTubeDist - 200mm + !this.tranci.desp[2] / 2

  --set up the trunni head and tail
  !this.trunni.hpos = !this.getTrunniConnectionPoint()
  !this.trunni.hdir = !direction
  !this.trunni.tdir = !direction.opposite()
  !this.trunni.tcon = 'ATT'
  !!mdsSupportEditor.trunnionDirection.val = !direction.string().before(' WRT')

  if (!!ce.compref.type.eq('BRAN')) then
    conn and p5 is hdir of compref
    handle (7, 3)
      fconn and p5 is hdir of compref
    endhandle
  else
    conn and p5 is pl of compref
    --revisit, this should not be the case
    handle (7, 3)
      fconn and p5 is pl of compref
    endhandle
  endif

  -- set up the SSREFE
  !this.ssrefe = object MDSSSREFE(!this.tranci, !this.softType)
  !!ce = !this.ssrefe.ssrefe
  stext '$!this.suppo.name'
  :MDSSuppType |$!this.softType|

  !!ce = !this.tranci

  -- Cut hole comment
  :MDSTrunNote1 ''

  -- create makeup piece
  !this.createMakeup()
  !od = pa od

  -- Set SSREFE to bore of leave of TRREDU
  if (  !this.ssrefe.trunBoreSSREFE(!!ce).not()) then
    -- cannot find a spcom for the ssrefe
    return FALSE
  endif

  new bend
  if (!this.selectSpref('TRANCI', !this.mdsTrunnionObject.sTypes[1].split(',')[2]).not()) then
    return FALSE
  endif
  name $!suppoName/BEND
  !this.atta.append(!!ce)
  !bendrad = !!ce.pod[1] / 2
  radius $!bendrad
  conn and pl is d

  !clear = !this.mdsTrunnionObject.userdps[4].min
  !clearDistance = !clear + !od / 2
  clear $!clearDistance behind p0 of $!this.tranci
  sel lstu

  -- Create baseplate
  new PLAT
  !this.basePlate = !!ce

  -- SET the variable for TRUNNION Bore Size
  !tBore = pa bore
  handle any
    !tBore = pa bore of prev
  endhandle
  !this.trunni.tbor = !tBore

  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[3]).not()) then
    return FALSE
  endif
  sel lstu
  ori pl is d
  distance 500
  name $!suppoName/PLAT.1
  !this.atta.append(!!ce)
  !!mdsSetDesPars(!!ce)

  --connect the trunnion tail to the last member
  !!ce = !this.trunni
  conn pt to pl of last mem

  return TRUE
endmethod

----------------------------------------------------------------------
--
-- Method:      createTrunniTT26
--
-- Description: create the trunni hierarchy appropriate to !this.onType
--
----------------------------------------------------------------------
define method .createTrunniTT26(!direction is DIRECTION) is BOOLEAN

  --revisit Note: this is a duplicate of createTrunniTT22(..) presently, copied in as a starting point
  !this.onlyAllowInTube = true

  --create the support element hierarchy
  if (!this.fromPrelim.not()) then
    !!mdsCreateSuppElement(!this.softType, !this.elementToSupport)
    !suppName = ''
  else
    !suppName = !this.fromPreliminarySupport()
    if (!suppName.eq('')) then
      return FALSE
    endif
  endif

  --set member data for the new hierarchy
  !this.tranci = !!ce
  !this.trunni = !!ce.owner
  !this.suppo = !!ce.owner.owner

  --set the support names
  !!mdsSetSupportNames(!this.suppo, !suppName)
  !suppoName = !this.suppo.name

  !this.trunni.pspe = !this.elementOwningBran.pspe
  --select the TRUNNI tube
  if (!this.onType.eq('BRAN')) then
    if (!this.elementToSupport.type.eq('BRAN')) then
      !this.trunni.hstu = !this.elementToSupport.hstu
      !ldir = !this.elementToSupport.hdir
    else
      !this.trunni.hstu = !this.elementToSupport.lstu
      !ldir = !this.elementToSupport.ldir
    endif
  else
    !this.trunni.hstu = !this.elementToSupport.lstu
    !ldir = !this.elementToSupport.ldir
  endif

  -- we have a selector for the TRANCI
  !!ce = !this.tranci
  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[1]).not()) then
    return FALSE
  endif
  !!mdsSetDesPars(!!ce)
  -- this is the only bit which is different to TT22 Start
  !p3dir = p3 dir
  !down = d
  !angle = !p3dir.angle(!down)
  despar n15 $!angle
  -- this is the only bit which is different to TT22 End
  sel lstu
  ori pl is d and p3 is $!ldir

  --  --revisit, this can be put in when we have a selector for TRANCI in the MDS-Ancillaries spec
  --!!ce = !this.tranci
  --select spref with styp 'PIPE-REST' spec /MDS-Ancillaries
  --select lstu

  !this.atta.append(!this.tranci)

  --set up the trunni head and tail
  !this.inTube = true
  !this.trunni.hpos = !this.getTrunniConnectionPoint()
  !this.trunni.hdir = !direction
  !this.trunni.tdir = !direction.opposite()
  !this.trunni.tcon = 'ATT'

  !!ce = !this.tranci
  conn
  --revisit, this should not be the case
  handle (7, 3)
    fconn
  endhandle

  ori pl is d and p3 is $!ldir

  -- set up the SSREFE
  !this.ssrefe = object MDSSSREFE(!this.tranci, !this.softType)
  !!ce = !this.ssrefe.ssrefe
  stext '$!this.suppo.name'
  :MDSSuppType |$!this.softType|

  !!ce = !this.tranci

  -- Cut hole comment
  :MDSTrunNote1 ''

  -- create makeup piece
  !this.createMakeup()


  -- Set SSREFE to bore of leave of TRREDU
  if (  !this.ssrefe.trunBoreSSREFE(!!ce).not()) then
    -- cannot find a spcom for the ssrefe
    return FALSE
  endif

  if (!!mdsTrunNotes.boolean()) then
    :MDSTubeNote1 ''
  endif

  -- Baseplate atta
  new PLAT
  !this.basePlate = !!ce

  -- SET the variable for TRUNNION Bore Size
  !tBore = pa bore
  handle any
    !tBore = pa bore of prev
  endhandle
  !this.trunni.tbor = !tBore

  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[2]).not()) then
    return FALSE
  endif
  sel lstu
  ori pl is d
  distance 1000
  :MDSTrunNote1 ''
  name $!suppoName/PLAT.1
  !this.atta.append(!!ce)
  !!mdsSetDesPars(!!ce)

  if (defined(!this.mdsTrunnionObject.sTypes[1].split(',')[3])) then
    -- we need to create a base plate and move the tail to its end
    new PLAT
    if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[3]).not()) then
      return FALSE
    endif
    sel lstu
    ori pl is d
    pos pa at pl of prev
    !this.atta.append(!!ce)
    !!mdsSetDesPars(!!ce)
    name $!suppoName/PLAT.2
    !this.bearingPlate = !!ce
    $!suppoName/PLAT.1
    pos pl at pa of next
  endif

  --connect the trunnion tail to the last member
  !!ce = !this.trunni
  conn pt to pl of last mem

  return TRUE
endmethod

----------------------------------------------------------------------
--
-- Method:      .createTrunniTT27
--
-- Description:
--
----------------------------------------------------------------------
define method .createTrunniTT27(!direction is DIRECTION) is BOOLEAN
  --create the support element hierarchy
  if (!this.fromPrelim.not()) then
    !!mdsCreateSuppElement(!this.softType, !this.elementToSupport)
    !suppName = ''
  else
    !suppName = !this.fromPreliminarySupport()
    if (!suppName.eq('')) then
      return FALSE
    endif
  endif

  --set member data for the new hierarchy
  !this.tranci = !!ce
  !this.trunni = !!ce.owner
  !this.suppo = !!ce.owner.owner

  --set the support names
  !!mdsSetSupportNames(!this.suppo, !suppName)
  !suppoName = !this.suppo.name

  !this.trunni.pspe = !this.elementOwningBran.pspe
  --select the TRUNNI tube
  if (!this.onType.eq('BRAN')) then
    if (!this.elementToSupport.type.eq('BRAN')) then
      !this.trunni.hstu = !this.elementToSupport.hstu
    else
      !this.trunni.hstu = !this.elementToSupport.lstu
    endif
  else
    !this.trunni.hstu = !this.elementToSupport.lstu
  endif

  !!ce = !this.tranci
  select spref with styp 'PIPE-REST' spec /MDS-Ancillaries
  select lstu

  !this.atta.append(!!ce)

  --set up the trunni head and tail
  !this.trunni.hpos = !this.getTrunniConnectionPoint()
  !this.trunni.hdir = !direction
  !this.trunni.tdir = !direction.opposite()
  !this.trunni.tcon = 'ATT'

  !!mdsSupportEditor.trunnionDirection.val = !direction.string().before(' WRT')

  !!ce = !this.tranci
  conn
  handle (7, 3)
    fconn
  endhandle

  -- set up the SSREFE
  !this.ssrefe = object MDSSSREFE(!this.tranci, !this.softType)
  !!ce = !this.ssrefe.ssrefe
  :MDSSuppType |$!this.softType|
  stext '$!this.suppo.name'

  !!ce = !this.tranci

  -- Makeup Piece
  new TRREDU select spref with styp 'MAKEUPOFF' spec /MDS-Ancillaries
  name $!suppoName/MAKEUP
  despar $!this.availableBores[1] 0mm 0mm
  sel lstu
  connect
  handle (7,3)
    fconnect
  endhandle
  mtoc off
  !this.atta.append(!!ce)
  :MDSTrunNote1 ''
  if (!!mdsTrunNotes.boolean()) then
    :MDSTubeNote1 ''
  endif

  if (!this.elementToSupport.type.eq('BRAN')) then
    !od = !this.elementToSupport.phod
  else
    !od = pl od of $!<this.elementToSupport>
  endif
  !tOd = !!ce.lodiam
  -- Work out the adjustment needed for the exact length to be added to the iso
  !hyp = (!od / 2)
  !side1 = (!tOd / 2)
  !offset = (sqrt ((!hyp * !hyp) - (!side1 * !side1)))

  desp num 2 $!offset

  -- Set SSREFE to bore of leave of TRREDU
  if (  !this.ssrefe.trunBoreSSREFE(!!ce).not()) then
    -- cannot find a spcom for the ssrefe
    return FALSE
  endif

  !index = 0
  do !i indices !this.availableBores
    if (!this.availableBores[!i].eq(!this.workingBore.string())) then
      !index = !i
    endif
  enddo
  if (!index.eq(0)) then
    !index = 1
  endif

  !sType = !this.mdsTrunnionObject.sTypes[!index]

  !splitStype = !sType.split(',')

  -- Create end plate
  new PLAT
  if (!this.selectSpref('', !splitStype[1]).not()) then
    return FALSE
  endif
  sel lstu
  ori
  name $!suppoName/PLAT.1
  !this.atta.append(!!ce)
  !!mdsSetDesPars(!!ce)
  conn
  handle (7,3)
    fconn
  endhandle
  !this.basePlate = !!ce
  if (!!mdsTrunNotes.boolean()) then
    :MDSTrunNote1 ''
  endif
  distance 184mm

  !ref = !!ce
  !!ce = !this.trunni
  conn pt to pl of last mem
  !!ce = !ref

  -- Create second bedplate
  new PLAT
  if (!this.selectSpref('', !splitStype[2]).not()) then
    return FALSE
  endif
  sel lstu
  name $!suppoName/PLAT.2
  !this.atta.append(!!ce)
  !!mdsSetDesPars(!!ce)
  connect to pt of owner
  handle (7,3)
    fconnect to pt of owner
  endhandle
  previous
  conn to next
  handle (7,3)
    fconn to next
  endhandle

  next
  new PLAT
  if (!this.selectSpref('', !splitStype[3]).not()) then
    return FALSE
  endif
  sel lstu
  name $!suppoName/PLAT.3
  !this.atta.append(!!ce)
  !!mdsSetDesPars(!!ce)
  conn to prev

  --connect the trunnion tail to the last member
  !!ce = !this.trunni
  conn pt to pl of last mem

  return TRUE
endmethod

----------------------------------------------------------------------
--
-- Method:      createTrunniTT33
--
-- Description: create the trunni hierarchy appropriate to !this.onType
--
----------------------------------------------------------------------
define method .createTrunniTT33(!direction is DIRECTION) is BOOLEAN

  --create the support element hierarchy
  if (!this.fromPrelim.not()) then
    !!mdsCreateSuppElement(!this.softType, !this.elementToSupport)
    !suppName = ''
  else
    !suppName = !this.fromPreliminarySupport()
    if (!suppName.eq('')) then
      return FALSE
    endif
  endif

  --set member data for the new hierarchy
  !this.tranci = !!ce
  !this.trunni = !!ce.owner
  !this.suppo = !!ce.owner.owner

  --set the support names
  !!mdsSetSupportNames(!this.suppo, !suppName)
  !suppoName = !this.suppo.name

  !this.trunni.pspe = !this.elementOwningBran.pspe
  --select the TRUNNI tube

  if (!this.onType.eq('BRAN')) then
    if (!this.elementToSupport.type.eq('BRAN')) then
      !this.trunni.hstu = !this.elementToSupport.hstu
    else
      !this.trunni.hstu = !this.elementToSupport.lstu
    endif
  else
    !this.trunni.hstu = !this.elementToSupport.lstu
  endif

  --revisit, this can be put in when we have a selector for TRANCI in the MDS-Ancillaries spec
  !!ce = !this.tranci
  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[1]).not()) then
    return FALSE
  endif
  select lstu
  !this.atta.append(!this.tranci)
  !!mdsSetDesPars(!!ce)

  --set up the trunni head and tail
  !this.trunni.hpos = !this.getTrunniConnectionPoint()
  !this.trunni.hdir = !direction
  !this.trunni.tdir = !direction.opposite()
  !this.trunni.tcon = 'ATT'

  !!mdsSupportEditor.trunnionDirection.val = !direction.string().before(' WRT')

  -- Cut hole comment
  :MDSTrunNote1 ''
  arri 2 lea 1 $* Turn clamp around as bolts should be away from trunnion
  if (!!ce.compref.type.eq('BRAN')) then
    conn
    handle (7, 3)
      fconn
    endhandle
    ori and p3 is hdir of compref
  else
    conn and p3 is pl of compref
    --revisit, this should not be the case
    handle (7, 3)
      fconn and p3 is pl of compref
    endhandle
  endif

  -- set up the SSREFE
  !this.ssrefe = object MDSSSREFE(!this.tranci, !this.softType)
  !!ce = !this.ssrefe.ssrefe
  stext '$!this.suppo.name'
  :MDSSuppType |$!this.softType|

  -- Create TRREDU makeup piece
  !!ce = !this.tranci
  !this.createMakeup()
  !od = pa od

  -- Set SSREFE to bore of leave of TRREDU
  if (  !this.ssrefe.trunBoreSSREFE(!!ce).not()) then
    -- cannot find a spcom for the ssrefe
    return FALSE
  endif

  new bend
  if (!this.selectSpref('TRANCI', !this.mdsTrunnionObject.sTypes[1].split(',')[2]).not()) then
    return FALSE
  endif
  name $!suppoName/BEND
  !this.atta.append(!!ce)
  !bendrad = !!ce.pod[1] / 2
  radius $!bendrad
  conn and pl is d

  !clear = !this.mdsTrunnionObject.userdps[4].min
  !clearDistance = !clear + !od / 2
  clear $!clearDistance behind p0 of $!this.tranci
  sel lstu

  -- Create baseplate
  new PLAT
  !this.basePlate = !!ce

  -- SET the variable for TRUNNION Bore Size
  !tBore = pa bore
  handle any
    !tBore = pa bore of prev
  endhandle
  !this.trunni.tbor = !tBore

  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[3]).not()) then
    return FALSE
  endif
  sel lstu
  ori pl is d
  distance 500
  name $!suppoName/PLAT.1
  !this.atta.append(!!ce)
  !!mdsSetDesPars(!!ce)

  --connect the trunnion tail to the last member
  !!ce = !this.trunni
  conn pt to pl of last mem

  return TRUE
endmethod

----------------------------------------------------------------------
--
-- Method:      .createTrunniTT35
--
-- Description:
--
----------------------------------------------------------------------
define method .createTrunniTT35(!direction is DIRECTION) is BOOLEAN

  --revisit Note: this is a duplicate of createTrunniTT04(..) presently, copied in as a starting point

  --create the support element hierarchy
  if (!this.fromPrelim.not()) then
    !!mdsCreateSuppElement(!this.softType, !this.elementToSupport)
    !suppName = ''
  else
    !suppName = !this.fromPreliminarySupport()
    if (!suppName.eq('')) then
      return FALSE
    endif
  endif

  --set member data for the new hierarchy
  !this.tranci = !!ce
  !this.trunni = !!ce.owner
  !this.suppo = !!ce.owner.owner

  --set the support names
  !!mdsSetSupportNames(!this.suppo, !suppName)
  !suppoName = !this.suppo.name

  !this.trunni.pspe = !this.elementOwningBran.pspe
  --select the TRUNNI tube
  if (!this.onType.eq('BRAN')) then
    if (!this.elementToSupport.type.eq('BRAN')) then
      !this.trunni.hstu = !this.elementToSupport.hstu
    else
      !this.trunni.hstu = !this.elementToSupport.lstu
    endif
  else
    !this.trunni.hstu = !this.elementToSupport.lstu
  endif

  -- we have a selector for the TRANCI
  !!ce = !this.tranci
  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[1]).not()) then
    return FALSE
  endif
  !!mdsSetDesPars(!!ce)
  sel lstu
  ori pl is d

  !this.atta.append(!this.tranci)

  --set up the trunni head and tail
  !this.inTube = true
  !this.trunni.hpos = !this.getTrunniConnectionPoint()
  !this.trunni.hdir = !direction
  !this.trunni.tdir = !direction.opposite()
  !this.trunni.tcon = 'ATT'

  !!ce = !this.tranci
  if (!!ce.compref.type.eq('BRAN')) then
    conn
    handle (7, 3)
      fconn
    endhandle
    ori and p3 is hdir of compref
  else
    conn and p3 is pl of compref
    --revisit, this should not be the case
    handle (7, 3)
      fconn and p3 is pl of compref
    endhandle
  endif

  -- set up the SSREFE
  !this.ssrefe = object MDSSSREFE(!this.tranci, !this.softType)
  !!ce = !this.ssrefe.ssrefe
  stext '$!this.suppo.name'
  :MDSSuppType |$!this.softType|

  !!ce = !this.tranci

  -- Cut hole comment
  :MDSTrunNote1 ''

  -- create makeup piece
  !this.createMakeup()

  -- Set SSREFE to bore of leave of TRREDU
  if (  !this.ssrefe.trunBoreSSREFE(!!ce).not()) then
    -- cannot find a spcom for the ssrefe
    return FALSE
  endif

  if (!!mdsTrunNotes.boolean()) then
    :MDSTubeNote1 ''
  endif

  -- Baseplate atta
  new PLAT
  !this.basePlate = !!ce

  -- SET the variable for TRUNNION Bore Size
  !tBore = pa bore
  handle any
    !tBore = pa bore of prev
  endhandle
  !this.trunni.tbor = !tBore

  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[2]).not()) then
    return FALSE
  endif
  sel lstu
  ori pl is d
  distance 1000
  :MDSTrunNote1 ''
  name $!suppoName/PLAT.1
  !this.atta.append(!!ce)
  !!mdsSetDesPars(!!ce)

  if (defined(!this.mdsTrunnionObject.sTypes[1].split(',')[3])) then
    -- we need to create a base plate and move the tail to its end
    new PLAT
    if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[3]).not()) then
      return FALSE
    endif
    sel lstu
    ori pl is d
    pos pa at pl of prev
    !this.atta.append(!!ce)
    !!mdsSetDesPars(!!ce)
    name $!suppoName/PLAT.2
    !this.bearingPlate = !!ce
    $!suppoName/PLAT.1
    pos pl at pa of next
  endif


  --connect the trunnion tail to the last member
  !!ce = !this.trunni
  conn pt to pl of last mem

  return TRUE
endmethod

----------------------------------------------------------------------
--
-- Method:      .createTrunniTT40
--
-- Description: create the trunni hierarchy for TT40 and TT41
--
----------------------------------------------------------------------
define method .createTrunniTT40(!direction is DIRECTION) is BOOLEAN

  --create the support element hierarchy
  if (!this.fromPrelim.not()) then
    !!mdsCreateSuppElement(!this.softType, !this.elementToSupport)
    !suppName = ''
  else
    !suppName = !this.fromPreliminarySupport()
    if (!suppName.eq('')) then
      return FALSE
    endif
  endif

  --set member data for the new hierarchy
  !this.tranci = !!ce
  !this.trunni = !!ce.owner
  !this.suppo = !!ce.owner.owner

  --set the support names
  !!mdsSetSupportNames(!this.suppo, !suppName)
  !suppoName = !this.suppo.name

  !this.trunni.pspe = !this.elementOwningBran.pspe
  --select the TRUNNI tube
  if (!this.onType.eq('BRAN')) then
    if (!this.elementToSupport.type.eq('BRAN')) then
      !this.trunni.hstu = !this.elementToSupport.hstu
    else
      !this.trunni.hstu = !this.elementToSupport.lstu
    endif
  else
    !this.trunni.hstu = !this.elementToSupport.lstu
  endif

  -- we have a selector for the TRANCI
  !!ce = !this.tranci
  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[1]).not()) then
    return FALSE
  endif
  !!mdsSetDesPars(!!ce)
  sel lstu
  ori pl is d

  --  --revisit, this can be put in when we have a selector for TRANCI in the MDS-Ancillaries spec
  --!!ce = !this.tranci
  --select spref with styp 'PIPE-REST' spec /MDS-Ancillaries
  --select lstu

  !this.atta.append(!this.tranci)

  --set up the trunni head and tail
  !this.inTube = true
  !this.trunni.hpos = !this.getTrunniConnectionPoint()
  !this.trunni.hdir = !direction
  !this.trunni.tdir = !direction.opposite()
  !this.trunni.hcon = 'ATT'
  !this.trunni.tcon = 'ATT'

  !!ce = !this.tranci
  conn and p3 is pl of compref
  handle any
    conn and p3 is hdir of compref
    handle any
      pos pa at pl of prev
      ori
    endhandle
  endhandle

  -- set up the SSREFE
  !this.ssrefe = object MDSSSREFE(!this.tranci, !this.softType)
  !!ce = !this.ssrefe.ssrefe
  stext '$!this.suppo.name'
  :MDSSuppType |$!this.softType|

  !!ce = !this.tranci
  -- Cut hole comment
  :MDSTrunNote1 ''

  -- create makeup piece
  !this.createMakeup()

  -- Design para 10 of the prev comp needs to be to the pl od of the make up piece
  -- to spread the clamps apar

  -- Set SSREFE to bore of leave of TRREDU
  if (  !this.ssrefe.trunBoreSSREFE(!!ce).not()) then
    -- cannot find a spcom for the ssrefe
    return FALSE
  endif

  if (!!mdsTrunNotes.boolean()) then
    :MDSTubeNote1 ''
  endif

  -- Baseplate atta
  new PLAT
  !this.basePlate = !!ce

  -- SET the variable for TRUNNION Bore Size
  !tBore = pa bore
  handle any
    !tBore = pa bore of prev
  endhandle
  !this.trunni.tbor = !tBore

  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[2]).not()) then
    return FALSE
  endif
  sel lstu
  ori pl is d
  distance 1000
  :MDSTrunNote1 ''
  name $!suppoName/PLAT.1
  !this.atta.append(!!ce)
  !!mdsSetDesPars(!!ce)

  if (defined(!this.mdsTrunnionObject.sTypes[1].split(',')[3])) then
    -- we need to create a base plate and move the tail to its end
    new PLAT
    if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[3]).not()) then
      return FALSE
    endif
    sel lstu
    ori pl is d
    pos pa at pl of prev
    !this.atta.append(!!ce)
    !!mdsSetDesPars(!!ce)
    name $!suppoName/PLAT.2
    !this.bearingPlate = !!ce
    $!suppoName/PLAT.1
    pos pl at pa of next
  endif

  --connect the trunnion tail to the last member
  !!ce = !this.trunni
  conn pt to pl of last mem

  return TRUE
endmethod

---------------------------------------------------------------------
--
-- Method:      createTrunniTT44
--
-- Description: create the trunni hierarchy appropriate to !this.onType
--
----------------------------------------------------------------------
define method .createTrunniTT44(!direction is DIRECTION) is BOOLEAN

  --create the support element hierarchy
  if (!this.fromPrelim.not()) then
    !!mdsCreateSuppElement(!this.softType, !this.elementToSupport)
    !suppName = ''
  else
    !suppName = !this.fromPreliminarySupport()
    if (!suppName.eq('')) then
      return FALSE
    endif
  endif

  --set member data for the new hierarchy
  !this.tranci = !!ce
  !this.trunni = !!ce.owner
  !this.suppo = !!ce.owner.owner

  --set the support names
  !!mdsSetSupportNames(!this.suppo, !suppName)
  !suppoName = !this.suppo.name

  !this.trunni.pspe = !this.elementOwningBran.pspe
  --select the TRUNNI tube
  if (!this.onType.eq('BRAN')) then
    if (!this.elementToSupport.type.eq('BRAN')) then
      !this.trunni.hstu = !this.elementToSupport.hstu
    else
      !this.trunni.hstu = !this.elementToSupport.lstu
    endif
  else
    !this.trunni.hstu = !this.elementToSupport.lstu
  endif

  --revisit, this can be put in when we have a selector for TRANCI in the MDS-Ancillaries spec
  !!ce = !this.tranci
  select spref with styp 'PIPE-REST' spec /MDS-Ancillaries
  select lstu
  !this.atta.append(!this.tranci)

  --set up the trunni head and tail
  !this.trunni.hpos = !this.getTrunniConnectionPoint()
  !this.trunni.hdir = !direction
  !this.trunni.tdir = !direction.opposite()
  !this.trunni.hcon = 'ATT'
  !this.trunni.tcon = 'ATT'

  !!ce = !this.tranci
  conn
  --revisit, this should not be the case
  handle (7, 3)
    fconn
  endhandle

  -- set up the SSREFE
  !this.ssrefe = object MDSSSREFE(!this.tranci, !this.softType)
  !!ce = !this.ssrefe.ssrefe
  stext '$!this.suppo.name'
  :MDSSuppType |$!this.softType|

  !!ce = !this.tranci

  -- Cut hole comment
  :MDSTrunNote1 ''

  -- create makeup piece
  !this.createMakeup()

  -- Set SSREFE to bore of leave of TRREDU
  if (!this.ssrefe.trunBoreSSREFE(!!ce).not()) then
    -- cannot find a spcom for the ssrefe
    return FALSE
  endif

  if (!!mdsTrunNotes.boolean()) then
    :MDSTubeNote1 ''
  endif

  -- Baseplate atta
  new PLAT
  !this.basePlate = !!ce

  -- SET the variable for TRUNNION Bore Size
  !tBore = pa bore
  handle any
    !tBore = pa bore of prev
  endhandle
  !this.trunni.tbor = !tBore

  if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[1]).not()) then
    return FALSE
  endif
  sel lstu
  ori pl is d
  distance 1000
  :MDSTrunNote1 ''
  name $!suppoName/PLAT.1
  !this.atta.append(!!ce)
  !!mdsSetDesPars(!!ce)

  if (defined(!this.mdsTrunnionObject.sTypes[1].split(',')[2])) then
    -- we need to create a base plate and move the tail to its end
    new PLAT
    if (!this.selectSpref('', !this.mdsTrunnionObject.sTypes[1].split(',')[2]).not()) then
      return FALSE
    endif
    sel lstu
    ori pl is d
    pos pa at pl of prev
    !this.atta.append(!!ce)
    !!mdsSetDesPars(!!ce)
    name $!suppoName/PLAT.2
    !this.bearingPlate = !!ce
    $!suppoName/PLAT.1
    pos pl at pa of next
  endif

  -- reset the reinforcing plate to be hard coded AC68
  !!ce = !this.tranci
  despar 0mm 0mm 0mm 0mm 0mm 0mm 0mm 0mm 0mm 0mm
  if (!this.selectSpref('','AC68').not()) then
    return FALSE
  endif
  !!mdsSetDesPars(!!ce)
  select lstu
  --revisit, this should not be the case
  conn and p3 is pl of compref
  handle any
    conn and p3 is hdir of compref
    handle any
      pos pa at pl of prev
    endhandle
  endhandle
  --connect the trunnion tail to the last member
  !!ce = !this.trunni
  conn pt to pl of last mem

  return TRUE
endmethod

----------------------------------------------------------------------
-- Method:      createPlat
--
-- Description: common plate creation method
--
----------------------------------------------------------------------
define method .createPlat(!name is STRING, !sType is STRING) is BOOLEAN

  -- Baseplate atta
  new PLAT $!name
  if (!this.selectSpref('', !sType).not()) then
    return FALSE
  endif
  sel lstu
  ori pl is d
  conn
  handle any
    pos pa at pl of prev
  endhandle
  !this.atta.append(!!ce)
  !!mdsSetDesPars(!!ce)

  return TRUE
endmethod

----------------------------------------------------------------------
--
-- Method:      presetDespars
--
-- Description: Selects spref from passed stype and halts creation if any errors are encountered
--
----------------------------------------------------------------------
define method .presetDespars(!type is STRING, !sType is STRING) is BOOLEAN

  if (!type.neq('')) then
    !typeString = 'type ' + !type + ' '
  else
    !typeString = ''
  endif
  !choice = select spec /MDS-Ancillaries with $!typeString styp '$!sType'
  handle ANY
    return FALSE
  elsehandle none
    if (undefined(!choice[1])) then
      return FALSE
    endif
  endhandle

  !nomBore = !choice[1].cpara[1]
  handle ANY
    return FALSE
  endhandle
  !mds$!<sType>defs = object MDSREADPARSDATA()
  !mdscheckdefs = !mds$!<sType>defs.mdsdefsdata(!sType, !nomBore)
  if (!mdscheckdefs) then
    do !a index !mds$!<sType>defs.despars
      !val = !mds$!<sType>defs.despars[!a]
      if (!sType.eq('AC68')) then
        if (!val.match(',') neq 0) then
          !lbore = lbor of nex
          !nbore = !lbore.real()
          !desplit = !val.after('(').before(')').split()
          do !desnum values !desplit
            if (!desnum.before(',').real() eq !nbore) then
              !val = !desnum.after(',')
              break
            endif
          enddo
        endif
      endif

      desp n$!a $!val

    enddo
    return TRUE
  endif

  return FALSE

endmethod

----------------------------------------------------------------------
--
-- Method:      selectSpref
--
-- Description: Selects spref from passed stype and halts creation if any errors are encountered
--
----------------------------------------------------------------------
define method .selectSpref(!type is STRING, !sType is STRING) is BOOLEAN

  if (!type.neq('')) then
    !typeString = 'type ' + !type + ' '
  else
    !typeString = ''
  endif

  if (!this.presetDespars(!typeString, !sType).not()) then
    despar 0mm 0mm 0mm 0mm 0mm 0mm 0mm 0mm 0mm 0mm
  endif

  select spref with $!typeString styp '$!sType' spec /MDS-Ancillaries
  handle (42,501)(42,64)
    !pipeBore = ph bore of trunni
    !softType= !this.softType
    $!this.suppo
    delete suppo
    !!alert.error('A $!softType trunnion cannot be built on a $!pipeBore bore pipe.  This is because the required $!sType is not available for this bore.')
    return FALSE
  endhandle

  return TRUE
endmethod

----------------------------------------------------------------------
--
-- Method:      getTrunniConnectionPoint
--
-- Description: get the connection point for the TRUNNI element. this was previously defined as a P3 point on the element but the new point must be constructed from available data
--
----------------------------------------------------------------------
define method .getTrunniConnectionPoint() is POSITION

  if (!this.onType.eq('ELBO')) then
    !connectionPoint = !this.getElbowConnectionPoint()
  elseif (!this.onType.eq('TEE')) then
    !connectionPoint = !this.elementToSupport.pPosition[0].wrt( /* )
  elseif (!this.onType.eq('REDU')) then
    --we no longer need to swap out our reducer for one with an appropriate p-point, the connection point is now calculated as follows
    --1) get intersect from small bore arrive/leave to point on large bore leave/arrive plane (!inter)
    --2) get distance from small bore arrive/leave point to intersect point in 1) (!reduLength)
    --3) offset from small bore arrive/leave point in arrive/leave direction by !reduLength / 2 (this is our connection point)
    if (!this.elementABore.lt(!this.elementLBore)) then
      !sbPos = !this.elementAPos
      !sbDir = !this.elementAdir.opposite()
      !lbPos = !this.elementLPos
      !lbDir = !this.elementLdir.opposite()
    else
      !sbPos = !this.elementLPos
      !sbDir = !this.elementLdir.opposite()
      !lbPos = !this.elementAPos
      !lbDir = !this.elementAdir.opposite()
    endif

    !sbPv = object POINTVECTOR(!sbPos, !sbDir)
    !lbPv = object POINTVECTOR(!lbPos, !lbDir)
    !inter = !sbPv.intersection(!lbPv.plane())
    !reduLength = !sbPv.position.distance(!inter)
    !connectionPointPv = !sbPv.offset(!reduLength / 2)
    !connectionPoint =  !connectionPointPv.position

  else
    --this must be a branch tube
    !pv = object POINTVECTOR(!this.elementLPos, !this.elementLdir)
    !connectionPoint = !pv.offset(!this.inTubeDist).position
  endif

  return !connectionPoint

endmethod

----------------------------------------------------------------------
--
-- Method:      .getElbowConnectionPoint()
--
-- Description: get the connection point for the TRUNNI element
-- if the base type is an ELBO
--
----------------------------------------------------------------------
define method .getElbowConnectionPoint() is POSITION

  if (!this.hori) then
    -- its a horizontal trunnion from an elbo which leaves downwards
    if (!this.elementAPos.up.gt(!this.elementLPos.up)) then
      -- this assumes that the elbo arrives from vertical
      if (!this.elementP0Pos.up.gt(!this.elementLPos.up)) then
        -- p0 is higher than the pl so it is sloping
        !connectionPoint = !this.elementAPos
      else
        !connectionPoint = !this.elementLPos
      endif
    else
      if (!this.elementP0Pos.up.gt(!this.elementAPos.up)) then
        !connectionPoint = !this.elementLPos
      else
        if (!this.softType.eq('TT16')) then
          if (!this.trunni.hdir.isParallel(!this.elementToSupport.ldir)) then
            !connectionPoint = !this.elementLPos
          else
            !connectionPoint = !this.elementAPos
          endif
        else
          !connectionPoint = !this.elementAPos
        endif
      endif
    endif
  else
    -- its not horizontal
    if (!this.elementAPos.up.gt(!this.elementLPos.up)) then
      -- the elbo arrives from vertical

      if (!this.elementP0Pos.up.gt(!this.elementLPos.up) and !this.elementToSupport.lgrad.gt(-5)) then
        !connectionPoint = !this.elementLPos
      else
        !connectionPoint = !this.elementAPos
      endif
    else
      if (!this.elementP0Pos.up.gt(!this.elementAPos.up)) then
        !connectionPoint = !this.elementAPos
      else
        !connectionPoint = !this.elementLPos
      endif
    endif
  endif
  -- !connectionPoint = !this.elementAPos
  return !connectionPoint

endmethod


----------------------------------------------------------------------
--
-- Method:      modOffset
--
-- Description: change offset for dummy leg trunnion
--
----------------------------------------------------------------------
define method .modOffset()

  --note contents originally from mdsmodoffset.pmlfnc

  --!!mdsSupportEditor.dimCont1.setDimensionValueById('offset', 500)
  !gadgetVal = !!mdsSupportEditor.dimCont1.getDimensionValueById('offset')

  !delete = false
  if (undefined(!!noOffsetMessage)) then
    !!noOffsetMessage = true
    !delete = true
  endif

  -- Initialise Variables

  !support = !!mdsSupportEditor.support

  -- Goto the datum and climb to branch

  !!ce = !support.datum
  trunni

  -- Find the makeup piece

  !mem = mem count
  do !findMakeUp from 1 to $!mem
    trunni
    $!findMakeUp
    if (!!ce.styp.eq('MAKEUP') or !!ce.styp.eq('MAKEUPOFF')) then
      break
    endif
  enddo

  !makeup = !!ce

  -- We are at the makeup piece so check to see if the value is more than the maximum allowed
  -- Main OD - Branch od / 2
  !tOd = !!ce.lodiam
  trunni
  !saveBran = !!ce
  if (!this.elementToSupport.type.eq('BRAN')) then
    !hOd = !this.elementToSupport.phod
  else
    !hOd = pl od of $!<this.elementToSupport>
  endif

  !maxOffset = ((!hOd - !tOd) / 2)
  !negMaxOffset = !maxOffset * -1

  !atMax = false
  !atMax = false

  if (!gadgetVal.gt(!maxOffset)) then
    !sOffset = !gadgetVal.string(!!distanceFMT)
    !mOffset = !maxOffset.string(!!distanceFMT)
    if (!!noOffsetMessage) then
      !!alert.error(|Offset stated of $!sOffset exceeds the maximum offset of $!mOffset$n. Setting offset to maximum|)
    endif
    !!mdsSupportEditor.dimCont1.setDimensionValueById('offset', !maxOffset)
    !atMax = true
  endif

  if (!gadgetVal.lt(!negMaxOffset)) then
    !sOffset = !gadgetVal.string(!!distanceFMT)
    !mOffset = !negMaxOffset.string(!!distanceFMT)
    if (!!noOffsetMessage) then
      !!alert.error(|Offset stated of $!sOffset exceeds the minimum offset of $!mOffset$n. Setting offset to minimum|)
    endif
    !!mdsSupportEditor.dimCont1.setDimensionValueById('offset', !negMaxOffset)
    !atMax = true
  endif

  !gadgetVal = !!mdsSupportEditor.dimCont1.getDimensionValueById('offset')

  same
  despar n3 $!gadgetVal

  -- We need to change the makeup length to take into account of the offset
  !unusedcurrentMakeup = despar 2

  -- Work out the adjustment needed for the exact length to be added to the iso
  !!ce = !this.elementToSupport
  var !radius const dist p0 to p1
  var !od pa od
  !hyp = !radius.real() + (!od.real() / 2)

  -- For TRUNNION type "TT14" the offset needs to be CORRECTLY set for an UPPER or a LOWER ELBOW
  -- Therefore CHECK if it is an UPPER or a LOWER type ELBO and if it's an HORIZONTAL or a VERTICAL type
  !elbo = !!ce
  if (!support.type.eq('TT14')) then
    !sign = '+'
    if  (!elbo.pdir[2].wrt(/* ).string().eq('U WRT /*') or !elbo.pdir[2].wrt(/* ).string().eq('Z WRT /*')) then
      -- Horizontal Trunnion -- on a LOWER ELBO
      !sign = '+'
    elseif  (!elbo.pdir[2].wrt(/* ).string().eq('D WRT /*') or !elbo.pdir[2].wrt(/* ).string().eq('-Z WRT /*')) then
      -- Horizontal Trunnion -- on an UPPER ELBOW
      !sign = '+'
    elseif  (!elbo.pdir[1].wrt(/* ).string().eq('U WRT /*') or !elbo.pdir[1].wrt(/* ).string().eq('Z WRT /*')) then
      -- Vertical Trunnion on a LOWER ELBOW
      !sign = '-'
    elseif  (!elbo.pdir[1].wrt(/* ).string().eq('D WRT /*') or !elbo.pdir[1].wrt(/* ).string().eq('-Z WRT /*')) then
      -- Vertical Trunnion -- on an UPPER ELBOW
      !sign = '-'
    endif
    -- Calculate the !side value
    !side1 = !radius.real() + (!tOd / 2) $!sign !gadgetVal

  else
    !side1 = !radius.real() + (!tOd / 2) + !gadgetVal
  endif

  --For TRUNNION type "TT14" the offset needs to be set to ZERO <0> when BOP of a LOWER ELBOW,
  --i.e. "<!elbo.pdir[2].wrt(/* ).string().eq('U WRT /*')", is Required for the TRUNNION
  if ((!support.type.eq('TT14')) and (!elbo.pdir[2].wrt(/* ).string().eq('U WRT /*') or !elbo.pdir[2].wrt(/* ).string().eq('Z WRT /*')) and (!atMax)) then
    -- The current MDS "TT14" TRUNNION type is HORIZONTAL and is attached to a LOWER ELBOW
    -- Set the OFFSET value at DESPAR NUMBER 2 of the REDUCER "!makeup"
    !offset = 0
    !!ce    = !makeup
    desp n2 0mm
  else
    -- Otherwise Calculate the value of "!offset" for all other TRUNNION types and ELBO orientations with the ORIGINAL CODE below
    !offset = (sqrt ((!hyp * !hyp) - (!side1 * !side1)))
    handle any
      !!ce = !makeup
      despar n2 0mm
    elsehandle none
      !!ce = !makeup
      despar n2 $!offset
      -- If at max offset then set despar 2 to 0 only for TT15
      if (!support.type.eq('TT15') and !atMax) then
        despar n2 0mm
      elseif (!support.type.eq('TT14') and !atMax) then
        despar n2 0mm
      endif
      if (!support.type.eq('TT15') and !!noOffsetMessage) then
        despar n2 $!offset
      endif
    endhandle
  endif

  !!ce = !saveBran
  !mem = mem count

  do !reset from 1 to $!mem
    $!reset
    thro ce
    trunni
  enddo

  trunni
  conn pt to last mem

  -- Despar 4 stores the branch offset so reset it
  $!this.ssrefe.ssrefe
  despar n4 $!gadgetVal

  if (!delete) then
    !!noOffsetMessage.delete()
  endif

endmethod

----------------------------------------------------------------------
--
-- Method:      setTrunLength
--
-- Description: set the length of the trunnion
-- this method is only called with an argument of true if its
-- setting the trunnion height by cursor. Normally its false
----------------------------------------------------------------------
define method .setTrunLength(!id is BOOLEAN)

  !trunniHpos = !this.trunni.hpos.wrt( /* )
  !trunniHdir = !this.trunni.hdir.wrt( /* )
  !trunniTpos = !this.trunni.tpos.wrt( /* )

  --Special case for TT20 as the height is fixed between two pipes
  if (!this.softType.eq('TT20')) then
    --Get the trunnion height
    pin 9 at $!trunniHpos
    pin 9 dir $!trunniHdir
    pin 9 plane pin 9 thro p0 of $!<this.elementToSupport>
    var !height const dist pin 9 to $!trunniTpos
    !!mdsSupportEditor.dimCont1.setDimensionValueById('hei', !height.real())
    !!mdsResetPins(1,9)
    !this.setSSREFEDespars(!this.ssrefe.ssrefe)
    return
  elseif (!this.softType inset('TT25','TT33','TT34')) then
    !this.posStanchion(!id)
    return
  endif

  !currentUnits = !!kGetSetUnits()

  -- find out if the linkRef is set and save the Guide orientation
  if (!this.basePlate.set() and !this.basePlate.:MDSLinkRef.set()) then
    if (!this.basePlate.:MDSLinkRef.first().badRef().not()) then
      !iPad = !this.basePlate.:MDSLinkRef.first()
      !guideOri = !iPad.orientation.wrt(/* )
    endif
  endif

  --revisit, check nwelds functionality?
  $(
  -- Determine the Pipe and Trunnion Weld Values at the ATTA/PLAT elements from the Specific MDSSELECTION object
  !trunnionBranch = !plat.owner
  if (!trunnionBranch.:mdsTrun) then
    !members = !trunnionBranch.members
    !!ce = !plat.owner
    do !element values !members
      if (!element.type.eq('ATTA') and !element.atty.eq('')) then
        skip if (!element.name.matchwild('*/SREF'))
        !!mdsNWelds(!element)
      elseif (!element.type.eq('PLAT')) then
        !!mdsNWelds(!element)
        -- Determine the Pipe Weld Value for a Trunnion attached to the MAIN PIPE at the MDS Trunnion REDU element from the Specific MDSSELECTION object
        -- This 'VIRTUAL' MDS Trunnion REDUCER element which is 'WELDED' to the MAIN PIPE for ISOMETRIC/SPOOLER WELD NUMBERING purposes
      elseif ((!element.type.eq('REDU')) and (!element.spref.name.matchwild('*/MDS/.*MAKEUP*')) ) then
        !!mdsNWelds(!element)
      else
        skip
      endif
    enddo
  endif
  $)

  --get the form length value in case we need to use it
  !formLengthVal = !!mdsSupportEditor.dimCont1.getDimensionValueById('hei')

  --create pin 9 at the TRUNNI head
  pin 9 at $!trunniHpos
  pin 9 dir $!trunniHdir
  if (!this.elementToSupport.type.eq('ELBO')) then
    -- get the elbo centreline height
    pin 9 plane pin 9 thro p0 of $!<this.elementToSupport>
  endif
  pin 9 off

  --if !id is true then we are using pick to identify the element to set to
  if (!id) then

    !!mdsIdPick = object MDSIDPICK()
    !picked = !!mdsIdPick.pick(4, 'Identify element to set trunnion to')
    if (unset(!picked)) then
      !!kRestoreUnits(!currentUnits)
      return
    endif

    !idEle = !picked

    -- Navigating to the OWNING Trunnion BRANCH Element
    pin 7 at $!trunniHpos
    pin 8 at $!trunniHpos
    pin 8 dir $!trunniHdir
    pin 8 plane $!trunniHdir clear 0 inf $!idEle

    pin 7 dir to pin 8

    var !pin7Dir dir pin 7
    !trunHdirStr = !trunniHdir.wrt(world).string().before(' WRT')

    pin 7 off
    if (!trunHdirStr.neq(!pin7Dir)) then
      pin 8 off
      !!alert.error('Identified element is below support. Change direction in order to set trunnion to identified element.')
      !!kRestoreUnits(!currentUnits)
      return
    endif

  else

    pin 8 copy pin 9
    pin 8 dist $!formLengthVal

  endif

  pin 9 off
  pin 8 off

  -- If pin 8 is above pin 9 error
  var !tp9u pin 9 pos wrt /*
  var !tp8u pin 8 pos wrt /*
  !p9u = !tp9u.position()
  !p8u = !tp8u.position()

  --check for verticality
  if (!this.softType inset('TT27', 'TT36', 'TT37', 'TT42')) then
    --revisit I don't see any reason why these supports should exclude this check
    -- I think these are available in all directions according to the plotfile TM
  else
    if (!this.trunni.hdir.wrt( /* ).string().eq('U WRT /*') or !this.trunni.hdir.wrt( /* ).string().eq('Z WRT /*')) then
      if (!p8u.up.lt(!p9u.up)) then
        !!alert.error('Identified element is below support')
        !!kRestoreUnits(!currentUnits)
        return
      endif
    else
      if (!p8u.up.gt(!p9u.up)) then
        !!alert.error('Identified element is above support')
        !!kRestoreUnits(!currentUnits)
        return
      endif
    endif
  endif

  -----------------------------------------------------------------------------------------------------------------
  -- MDS Trunnions with a Bearing Plate and a Trunnion Base Plate
  -----------------------------------------------------------------------------------------------------------------
  !bearPl = '0'
  !at59pl = '0'
  !ceComp = !!ce

  if (!this.bearingPlate.set()) then
    !!ce = !this.bearingPlate
    if (!!ce.styp inset ('AC19A','AC19B','AC18A','AC18B')) then
      var !bearPl const dist pa to pl
      previous
      if (!!ce.styp inset ('AT59','AT68')) then
        var !at59pl const dist pa to pl
      endif
      next
      ori and p3 is p3 of $!<this.basePlate>
      pos pa at pl  of prev
      trunni
      -- was branch
    else
      !!alert.error('There is an error in the configuration of this MDS trunnion. Aborting Trunnion Length modification')
      !!kRestoreUnits(!currentUnits)
      return
    endif
  elseif (!this.basePlate.set()) then
    -- Trunnion Base Plate
    !!ce = !this.basePlate
    next
    handle NONE
      if (!!ce.styp inset ('AT59','AT68')) then
        var !at59pl const dist pa to pl
      endif
    elsehandle ANY
    endhandle
  endif

  -- We need to move pin 8 down by the thickness of the bearing plate if gadget driven
  if (!id.not()) then
    pin 8 dist $!bearPl
    pin 8 dist $!at59pl
  endif

  !!ce = !ceComp


  -- ***********************
  -- *** IMPORTANT NOTE: ***
  -- ***********************
  -- The variable !trunLen <trunnion length> is checked here, which is before any ADJUSTMENT has been made
  -- to allow for Trunnion Base ELL's with BEARING PLATES plus BASE PLATES at the Trunnion Branch Tail.
  --
  -- With the addition of ''AT59'' - (Cold Isolation Block for Trunnions) - Further checks will also required
  -- to cater for the Component Length of an ''AT59'' ancillary when checking the TRUE Trunnion Length
  -- is within the MINIMUM and MAXIMUM allowed Trunnion Lengths .

  -- Set the trunnion length Variable
  var !trunLen const dist pin 9 to pin 8
  var !trunLen ( $!trunLen - $!bearPl - $!at59pl )
  if (!this.softType.eq('TT36')) then
    !tee = !this.elementToSupport
    !teePl = !tee.ldir.wrt(worl)
    if (!tee.type eq 'TEE') then
      if (!teePl.string().eq('U WRT /*') or !teePl.string().eq('D WRT /*') or !teePl.string().eq('Z WRT /*') or !teePl.string().eq('-Z WRT /*')) then
        !trunData = object MDSGETTRUNOVERDIM(!tee,!idEle)
        handle NONE
        elsehandle ANY
        endhandle
        !unusedVal = !trunData.vertTrunDim
        handle NONE
          !trunLen = !trunData.vertTrunDim.string()
        elsehandle ANY
        endhandle
      endif
    endif
  endif
  --------------------------------------------------------------

  -- Determine the MINIMUM and MAXIMUM TRUNNION LENGTH values
  -- The MAIN PIPE Bore Size = !plBore, i.e. the PHEAD of the TRUNNION BRANCH
  !mdsTrunData = !this.mdsTrunnionObject.mdsTrunData(!this.softType, !this.trunni.hbor.real().value())

  -- CHECK if Trunnion DEFAULTS DATA OK in the MDS DEFAULTS PARAGON DATABASE CATA
  if (!mdsTrunData.not()) then
    -- ERROR with the MDS DEFAULTS in the PARAGON CATALOGUE "SECTION" or "CATEGORY" element
    if (!this.mdsTrunnionObject.mdsStd.sactive.not()) then
      -- ERROR with the MDS DEFAULTS in the PARAGON CATALOGUE "SECTION" element
      !errorMessage = !this.mdsTrunnionObject.mdsStd.smess
    elseif (!this.mdsTrunnionObject.mdsStd.cactive.not()) then
      -- ERROR with the MDS DEFAULTS in the PARAGON CATALOGUE "CATEGORY" element
      !errorMessage = !this.mdsTrunnionObject.mdsStd.cmess
    endif
    return
  else
    -- trunnion data is valid now check the length
    -- Trunnion DATA is OK, so GET and SET the MINIMUM and MAXIMUM Trunnion Lengths
    !min = !this.mdsTrunnionObject.minLength
    !max = !this.mdsTrunnionObject.maxLength
  endif

  -- Now check if the LENGTH required is outside the MINIMUM and MAXIMUM TRUNNION LENGTH values
  !minMax = FALSE
  if (!trunLen.real().nint().lt(!min)) then
    !uMin = !min.string(!!distanceFMT)
    !uTrunLen = !trunLen.string(!!distanceFMT)
    pin 8 off
    !!alert.error(|Length stated ($!uTrunLen) is less than the minimum allowed. Setting length to minimum of $!uMin|)
    !trunLen = !min.string()
    pin 8 copy pin 9
    !minMaxVal = !min
    pin 8 dist ( $!minMaxVal + $!bearPl + $!at59pl)
    !minMax = TRUE
  elseif (!trunLen.real().nint().gt(!max)) then
    !uMax = !max.string(!!distanceFMT)
    !uTrunLen = !trunLen.string(!!distanceFMT)
    pin 8 off
    !!alert.error(|Length stated ($!uTrunLen) is more than the maximum allowed. Setting length to maximum of $!uMax|)
    !trunLen = !max.string()
    pin 8 copy pin 9
    !minMaxVal = !max
    !minMax = TRUE
  endif

  -------------------------------------------------------------------------
  -- Adjustment for the trunnion being attached to an elbow If connected to
  -- an elbow then we need to add the leg length to the overall height
  -------------------------------------------------------------------------
  if (!this.onType.eq('ELBO') and !this.inTube.not()) then
    if (!this.softType inset ('TT36','TT37')) then
      !ll = !this.elementToSupport.pPos[0].distance(!this.elementToSupport.pPos[1])
    else

      if (!this.hori) then
        if (!this.elementAPos.up.gt(!this.elementLPos.up)) then

          if (!this.elementP0Pos.up.gt(!this.elementLPos.up)) then
            !pToUse = 'pa'
          else
            !pToUse = 'pl'
          endif

        else

          if (!this.elementP0Pos.up.gt(!this.elementAPos.up)) then
            !pToUse = 'pl'
          else
            !pToUse = 'pa'
          endif

        endif

      else
        if (!this.elementAPos.up.gt(!this.elementLPos.up)) then
          if (!this.elementP0Pos.up.gt(!this.elementLPos.up)) then
            !pToUse = 'pl'
          else
            !pToUse = 'pa'
          endif
        else
          if (!this.elementP0Pos.up.gt(!this.elementAPos.up)) then
            !pToUse = 'pa'
          else
            !pToUse = 'pl'
          endif
        endif
      endif

      pin 9 at $!pToUse of $!<this.elementToSupport>
      pin 8 at $!pToUse of $!<this.elementToSupport>
      pin 8 dir $!trunniHdir
      pin 8 plane pin 8 thro p0 of $!<this.elementToSupport>
      var !llTemp const dist pin 9 to pin 8
      !ll = !llTemp.real()
    endif
  else
    !ll = 0
  endif

  !overall = !trunLen.real() + !ll
  -------------------------------------------------------------------
  -- Adjustment for a bearing plate existing in the trunnion standard
  -------------------------------------------------------------------

  --  For ID mode -If the element has a bearing plate then the length must be SUBTRACTED from the overall length
  --  because the length does NOT include the bearing plate

  --  For non-ID mode -If the element has a bearing plate then the length must be ADDED to the overall length
  --  because the length does NOT include the bearing plate
  !extra = '0'

  -- Search members for AC19 which is the bearing plate standard in the trunnion
  --revisit !bearingPlate will be a member on the trunnion
  if (!this.bearingPlate.set()) then
    !!ce = !this.bearingPlate
    var !extra const dist pa to pl
  endif

  -- When the element is a REDU add the difference between extra and the redu offset
  if (!this.onType.eq('REDU') and !this.inTube.not()) then
    pin 9 at $!trunniHpos
    pin 8 copy pin9
    if (!this.elementToSupport.abor.gt(!this.elementToSupport.lbor)) then
      pin 8 plane pin 8 thro pl of $!<this.elementToSupport>
    else
      pin 8 plane pin 8 thro pa of $!<this.elementToSupport>
    endif
    var !bit const dist pin 9 to pin 8
    !diff = !extra.real() - !bit.real()
    if (!this.elementToSupport.abor.gt(!this.elementToSupport.lbor)) then
      !overall = !overall - !extra.real()  + !diff
    else
      if (!id.not()) then
        !overall = !overall - !extra.real()  - !diff.abs()
      else
        !overall = !overall - !diff
      endif
    endif
  endif

  pin 8 at $!trunniHpos
  pin 8 dir $!trunniHdir
  pin 9 dir $!trunniHdir

  if (!id) then
    if (!minMax) then
      pin 8 dist ( $!minMaxVal + $!bearPl + $!at59pl )
      if (!this.onType.eq('ELBO') and !this.inTube.not()) then
        pin 8 at p1 of $!<this.elementToSupport>
        pin 8 plane pin 8 thro p0 of $!<this.elementToSupport>
        pin 8 dist ( $!minMaxVal + $!bearPl + $!at59pl )
      endif
    else
      pin 8 plane pin 8 clear 0 inf $!idEle
    endif
  elseif (!id.not()) then
    pin 8 dist $!overall
  endif

  --revisit, what type are we connecting and orientating here? - start
  -- save p3 to put back in original orientation
  !!ce = !this.trunni
  last mem
  !p3dir = p3 dir wrt /*
  handle ANY
  endhandle

  conn
  handle (7,3)(7,42)
    fconn
  endhandle
  ori and p3 is $!p3dir
  handle ANY
  endhandle
  --revisit, what type are we connecting and orientating here? - end

  if (!id) then
    var !pos pin 8 pos
  elseif (!id.not()) then
    -- Position Pin 8 at the Pipe Tail
    pin 8 dist $!bearPl
    pin 8 dist $!at59pl
    var !pos pin 8 pos
  endif

  -- this sets the tpos but its overridden by connect later
  !this.trunni.tpos = !pos.position()

  --revisit, what is the current element type supposed to be at this point?
  pos p2 at pin 8

  !!ce = !this.trunni
  conn pt to last mem
  tcon clos

  !!mdsResetPins(8,9)

  -- The following loop reads the support configuration contained in the elementOffsetConfig Object
  -- this loops through the member array and positions the element according to the config setting.
  do !count from 1 to !this.atta.size()
    break if (!count gt !this.elementOffsets.size())
    !!ce = !this.atta[$!count]
    !saveOri = ori
    --revisit we should look to see where this code is needed rather than keep adding exceptions
    skip if (!!ce.sType.eq('AC27'))
    skip if (!!ce.sType.eq('AC28'))
    skip if (!!ce.sType.eq('AC29'))
    skip if (!!ce.sType.eq('AC31'))
    skip if (!!ce.sType.eq('AC45'))
    skip if (!!ce.sType.eq('AC53'))
    skip if (!!ce.sType.eq('AC54'))
    skip if (!!ce.sType.eq('AC58'))
    skip if (!!ce.sType.eq('AC68'))
    skip if (!!ce.sType.eq('AT29A'))
    skip if (!!ce.sType.eq('AT29B'))
    skip if (!!ce.sType.eq('AT31A'))
    skip if (!!ce.sType.eq('AT31B'))
    skip if (!!ce.sType.eq('AC44'))
    skip if (!!ce.sType.eq('AT68'))
    skip if (!!ce.sType.eq('AT59'))
    skip if (!!ce.sType.eq('AT105A'))
    skip if (!!ce.sType.eq('AT105B'))
    !an640s = ''
    do !num from 640 to 700
      !an640s = !an640s + 'AN$!num'
    enddo
    skip if (!an640s.match(!!ce.styp).neq(0))

    if (!this.elementOffsets[$!count].eq('0')) then
      if (!!ce.name.match('MAKEUP').gt(0)) then
        !saveP3Dir = p3 dir wrt /*
      endif
      conn
      handle (7,3)(7,42)
        fconn
      endhandle
      if (!!ce.name.match('MAKEUP').gt(0) ) then
        ori and p3 is $!saveP3Dir
      endif
    elseif (!this.elementOffsets[$!count].match('-').eq(1) and !!ce.sType.neq('AC29')) then
      if (!!ce.styp.eq('AC20')) then
        pos pa dist ( $!this.elementOffsets[$!count] - $!bearPl ) from pt
      else
        pos pa dist $!this.elementOffsets[$!count] from pt
      endif
    else
      if ((!this.softType.eq('TT23')) and (!count.eq(3))) then
        !ceOrig = !!ce
        !crefTyp = !this.tranci.compref.type
        !mainOd = !this.tranci.compref.lod
        !crefDbr = !this.tranci.compref
        -- Calculate the Distance to Move the Atta(AC46) note: Height to isolation block now controlled by property Pdist for AC46
        if (!crefTyp.eq('ELBO')) then
          -- Navigate to the ELBO
          !!ce    = !crefDbr
          !adjust = !!kStrReal('cons dist p0 to plane p1')
          same
          if (!this.inTube) then
            !distPa = (!!ce.prop.pdist.real() + (!mainOd / 2))
          else
            !distPa = (!!ce.prop.pdist.real() + (!mainOd / 2)) + !adjust
          endif
        elseif (!crefTyp.eq('REDU')) then
          -- Navigate to REDU
          !!ce    = !crefDbr
          --!mainOd MUST always be the SMALLER BORE O/Dia of the REDU
          !paBore = !!ce.abore
          !plBore = !!ce.lbore
          if (!plBore.gt(!paBore)) then
            !mainOd = !!ce.aod
          else
            !mainOd = !!ce.lod
          endif
          same
          !distPa = (!!ce.prop.pdist.real() + (!mainOd / 2))
        else
          !distPa = (!!ce.prop.pdist.real() + (!mainOd / 2))
        endif
        -- Navigate back to the Atta(AC46) note:End of height controlled by property Pdist for AC46
        !!ce = !ceOrig
        dist $!distPa
      else
        dist $!this.elementOffsets[$!count]
      endif
    endif

  enddo

  -- Connect the last member to the pt
  !!ce = !this.trunni
  last mem
  !p3dir = p3 dir wrt world
  handle ANY
  endhandle
  conn to pt
  ori and p3 is $!p3dir
  handle ANY
  endhandle

  -- The members at the end of the trunnion may not be connected if they have
  -- a thickness i.e baseplate so connect them up if they are -0
  -- Find the last connect member

  !counter = 1
  do !search values !this.elementOffsets
    if (!search.eq('-0')) then
      !last = !this.atta[$!counter]
    else
      !counter = !counter + 1
    endif
  enddo

  !branmem = !this.trunni.mcou
  do !count from !branmem to 1 by -1
    !!ce = !this.trunni
    $!count
    !saveOri = ori
    back conn forw
    handle (7,3)(7,42)
      back fconn forw
    endhandle
    if (!!ce.styp.matchwild('AC19*')) then
      ori and p3 is p3 of $!<this.basePlate>

    else
      ori $!saveOri
    endif
    if (!!ce.ref.eq(!last.ref)) then
      break
    endif
  enddo

  --check for minimum height of TT23 pipe stool
  if (!this.softType.eq('TT23')) then
    plat 3
    var !dist construct distance pl of ce to pa of nex
    if (!dist.real() lt !this.elementOffsets[3].real()) then
      !minHeight = !this.elementOffsets[3].real()
      !!alert.error('Distance between isolation block and base plate = ' & !dist.real().nint() & 'mm
      Apply lower value from BOP to Plate top to allow for ' & !minHeight & 'mm minimum distance')
      --return
    endif
  endif

  !suppoName = !this.suppo.name

  !this.drawAids()

  -- line up all ATTAS if the pipe is sloping
  !makeup = !suppoName + '/MAKEUP'
  $!makeup
  do
    nex
    handle ANY
      break
    endhandle
    thro ce
  enddo
  tail
  conn pt to last mem
  -- items should now be lined up

  -- If a field fitt weld exists connect it to the collar
  !weld = !suppoName + '/FFW'
  !!ce = !weld.dbref()
  handle ANY
  elsehandle NONE
    connect to next
  endhandle

  -- Check for Valid Trunnion Base Plate SType
  if (!this.basePlate.badRef().not()) then
    if (!this.basePlate.sType inset ('AC18A','AC18B','AC30','AC47','AC49','AC60','AC61','AC62','AC63','AC64','AC65','AC66','AC67')) then
      if (!this.basePlate.:MDSLinkRef.set()) then
        do !mdsLinkRef values !this.basePlate.:MDSLinkRef
          -- Check if UDA :MDSLinkRef is Valid
          if (!mdsLinkRef.badRef().not()) then
            if (not !mdsLinkRef.styp.substring(1,2) inset ('AN','GT','ST')) then
              !bPlt = !this.basePlate
              !iPad = !bPlt.:MDSLinkRef.first()
              !tNam = !bPlt.owner.owner.name & '/SREF.1'
              !trun = !tNam.dbref()
              -- Adjust Trunnion Length within the MIN/MAX Trunnion Length Limits to Accommodate the Isolation Pad/Cold Isolation Block etc...
              !trunLenWarn = STRING()
              if (!!mdsAdjTrunLen(!bPlt,!iPad,!trun,!!mdsSupportEditor,'ADJUST',!trunLenWarn).not()) then
                !!alert.warning(!trunLenWarn)
              endif
              break
            endif
          endif
        enddo
      endif
    endif
  endif

  -- re-orientate the guide to what it was before the reset trunnion length
  if (defined(!guideOri)) then
    !!ce = !iPad
    !!ce.ori = !guideOri
  endif

  --for TT27, orientate the plates
  if (!this.softType.eq('TT27')) then
    !!ce = !this.basePlate
    if (!!ce.:MDSRotated) then
      ori and p9 is $!!ce.:MDSRotDir
      handle ANY
      elsehandle NONE
        next
        ori and p9 is $!!ce.:MDSRotDir
        nex
        ori and p9 is $!!ce.:MDSRotDir
      endhandle
    endif
  endif

  --adjust so that Vertical pipes work correctly when identifying an element
  if (!this.softType.eq('TT36')) then
    !tee = !this.elementToSupport
    !bran = !this.trunni
    if (!tee.type eq 'TEE') then
      !teePl = !tee.ldir.wrt(worl)
      if (!teePl.string().eq('U WRT /*') or !teePl.string().eq('D WRT /*') or !teePl.string().eq('Z WRT /*') or !teePl.string().eq('-Z WRT /*')) then
        !trunData = object MDSGETTRUNOVERDIM(!tee,!idEle)
        handle NONE
        elsehandle ANY
        endhandle
        !!mdsSupportEditor.tLength.val = !trunData.vertTrunDim
        handle NONE
        elsehandle ANY
        endhandle
        !!mdsSupportEditor.direction.val = !trunData.vertTrunDir.string()
        handle NONE
        elsehandle ANY
        endhandle
        $!tee
        lea 3
        dir d
        handle ANY  $* Pipe must be vertical so set direction N
          dir $!trunData.vertTrunDir
          handle NONE
            dir $!trunData.vertTrunDir
          elsehandle ANY
          endhandle
        endhandle
        lea 2

        $!bran

        var !headDir hdir
        !hDirTemp = object DIRECTION(!headDir)
        !hDirTempOpp = !hDirTemp.opposite()

        if (!!ce.type.eq('BRAN')) then
          conn ph to same
          !tdir = !trunData.vertTrunDir
          handle NONE
            !tdirOpp =  !tdir.opposite()
            !!mdsTT36.tDirection = !tdirOpp.string()
            tdir $!tdirOpp
            --revisit, this won't currently work
            call !this.setTrunLength('GADGET',!!mdsTT36.tLength.val.real(),!!mdsTT36.support.datum,!!mdsTT36.direction.val,'TT36',!!mdsTT36)
          elsehandle ANY
          endhandle
          !tdir = !!mdsSupportEditor.tDirection.direction()

          !tdir = $!hDirTempOpp
          handle (2,779)
            !tdir = axis $!hDirTempOpp
          endhandle
          tdir $!tdir

          handle NONE
            --revisit, need to figure out what this method is
            !!mdsTT36.boremethod()
          elsehandle ANY
          endhandle
        endif
      endif
    endif
  endif

  --apply the height as measured from current geometry to the dimension control box
  !!mdsSupportEditor.dimCont1.setDimensionValueById('hei', !trunLen.real())

  -- Set the despars on the ssrefe for the trunnion
  !this.setSSREFEDespars(!this.ssrefe.ssrefe)

  --if this is a TT21 trunnion type then we need to check if there is a connected hanger associated with the plate that needs to be moved
  if (!this.softType.eq('TT21')) then
    var !plat last plat of $!this.trunni
    handle (2,113)
      --there is no plate (shouldn't happen)
    elsehandle NONE
      !currElem = !!ce
      !hangerRef = !plat.dbref().suprfa
      if (!hangerRef.set()) then
        --a hanger support is connected to the trunnion plate. we need to move it with the trunnion
        !tpos = !this.trunni.tpos.wrt( /* )
        !offsetDist = !trunniTpos.distance(!tpos)
        if (!offsetDist.gt(0)) then
          !offsetDir = !trunniTpos.direction(!tpos).string().before('WRT')
        else
          !offsetDir = 'd'
        endif
        --offset all of the hanger elements by the amount that the trunnion has been shortend/lengthend
        !suppo = !hangerRef[1].owner.owner
        do !member values !suppo.members
          !!ce = !member
          by $!offsetDir $!offsetDist
        enddo
      endif
      !!ce = !currElem
    endhandle
  endif

  -- Reset Pins
  !!mdsResetPins (8,9)

  !!kRestoreUnits(!currentUnits)

endmethod

----------------------------------------------------------------------
--
-- Method:      setupElementOffsets
--
-- Description: set up the elementOffsets array that contains reconnect offsets for the softtype
--
----------------------------------------------------------------------
define method .setupElementOffsets()

  !this.elementOffsets = !this.elementOffsetConfig.getElementOffsets(!this.softType)

endmethod

----------------------------------------------------------------------
--
-- Method:      drawAids
--
-- Description: draws graphical aids
--
----------------------------------------------------------------------
define method .drawAids()

  !ref = !!ce

  do !count from 212 to 259
    var !num ($!count - 211)
    !attaname = !this.suppo.name & '.' & !num
    !!ce = !attaname.dbref()
    handle ANY
      skip
    endhandle
    -- Store Atta reference in form
    var !pos pos wrt world
    handle ANY
      skip
    endhandle
    $M/%PMLUI%/DES/CLIB/GAIDTEXT $!count $!num $<$!pos$>
  enddo

  !!ce = !ref

endmethod

----------------------------------------------------------------------
--
-- Method:      inTube
--
-- Description: toggle from supporting component (i.e. ELBO) to TUBE
--
----------------------------------------------------------------------
define method .inTube()

  !dist = !this.validateInTubeDist()
  if (!dist.eq(-1)) then
    return
  elseif (!dist.neq(0)) then
    !this.inTubeDist = !dist
  endif

  --first move the trunni
  if (!this.elementToSupport.type.eq('ELBO')) then
    !this.elboInTube()
  elseif (!this.elementToSupport.type.eq('TEE')) then
    !this.teeInTube()
  elseif (!this.elementToSupport.type.eq('REDU')) then
    !this.reduInTube()
  elseif (!this.elementToSupport.type.eq('BRAN') OR !this.onType.eq('TUBE')) then
    !this.inTube = TRUE
  endif

  if (!this.inTube eq (FALSE)) then
    !!mdsSupportEditor.distanceFromElement = object DBREF()
    !!mdsSupportEditor.previousSupport = object DBREF()
    !!mdsSupportEditor.supportDistance.val = FALSE
  endif

  -- If the Trunnion contains an AT29 soft type then we need to switch it from A to B or vice versa
  if (!!mdsSoftTypeList(!this.tranci.styp).eq('AT29')) then
    !this.switchAT29(!this.tranci.styp)
  endif

  --get the offset value to apply to the reducer makeup piece
  !reducerName = !this.suppo.name + '/MAKEUP'

  -- calculate the vertical length of the reducer (TRREDU)
  !offset = !this.calculateMakeupOffset(!reducerName.dbref())
  !reducerName.dbref().despar[2] = !offset

  !height = !!mdsSupportEditor.dimCont1.getDimensionValueById('hei')
  handle ANY
    --Support Editor has not been initialised yet so leave the height for now, it will be set later on anyway
  elsehandle NONE
    --reset the height if the height has already been set, we don't want to run the set length method if we are creating a trunnion that can only be created on tube
    if (!height.neq(0) and !this.onlyAllowInTube.not()) then
      !this.setTrunLength(false)
    endif
  endhandle

  --set the dimensioncontrol distance gadget to either active or inactive based upon whether or not the trunnion is in tube
  do !tab from 1 to !!mdsSupportEditor.dimControls
    !!mdsSupportEditor.dimCont$!tab$n.setDimensionActive('distance', !this.distanceActive)
  enddo

  !!mdsSupportEditor.distance.active = !this.distanceActive
  !!mdsSupportEditor.supportDistance.active = !this.distanceActive
  !!mdsSupportEditor.through.active = !this.distanceActive

  -- Check that the datum has been set on the support editor, if not set it.
  -- This is required for MDSDistance
  if (!!mdsSupportEditor.support.datum.badRef()) then
    !!mdsSupportEditor.support.datum = !this.tranci
  endif

  if (!this.inTube) then
    !!mdsdistance(!!mdsSupportEditor)
  else
    !!mdsHideAidDist()
  endif

  !!mdsSupportEditor.distanceValue = !this.inTubeDist * !this.inTube.real()

  do !tab from 1 to !!mdsSupportEditor.dimControls
    --If !this.inTube flag is false then distance will be set to 0, else it will be set to the !this.inTubeDist value
    !!mdsSupportEditor.dimCont$!tab$n.setDimensionValueById('distance', !!mdsSupportEditor.distanceValue)
  enddo

  if (!this.distanceActive) then
    !!mdsSupportEditor.trunnionInTubeToggle.addPixmap('AvevaSharedIcons>ID_TRUNNION_ON_COMPONENT>32')
    !!mdsSupportEditor.trunnionInTubeToggle.setTooltip('Pipe - Click to Support Component')
  else
    !!mdsSupportEditor.trunnionInTubeToggle.addPixmap('AvevaSharedIcons>ID_TRUNNION_ON_PIPE>32')
    !!mdsSupportEditor.trunnionInTubeToggle.setTooltip('Component - Click to Support Pipe')
  endif

  --redraw graphical aids
  !this.drawAids()

endmethod

----------------------------------------------------------------------
--
-- Method:      elboInTube
--
-- Description: if the trunnion is supporting an elbo change the hierarchy so that it is supporting tube and visa-versa
--
----------------------------------------------------------------------
define method .elboInTube()

  if (!this.inTube.not()) then

    --we are supporting the component elbo. Modify to support the tube
    --first move the trunni so that it is in-line with p0 of the elbo
    var !ldir pl dir of $!<this.elementToSupport> wrt /*
    var !lpos pl pos of $!<this.elementToSupport> wrt /*
    var !p0pos p0 pos of $!<this.elementToSupport> wrt /*
    !direction = !this.elementP0Pos.direction(!this.tranci.position)

    --drag move through p0 of the elbo (note cannot do this at trunni level so we have to reconnect the head after the move
    !!ce = !this.tranci
    drag move $!direction thro p0 of $!<this.elementToSupport>

    if (!!mdsSupportEditor.distance.val) then
      !pvLeave = object POINTVECTOR(!lpos.position(), !ldir.direction())
    else
      !pvLeave = object POINTVECTOR(!p0pos.position(), !ldir.direction())
    endif

    !pvLeave = !pvLeave.offset(!this.inTubeDist)

    !pvLeavePos = !pvLeave.position
    drag move $!ldir thro $!pvLeavePos

    --reconnect the trunni head to the arrive of the tranci
    !!ce = !this.trunni
    conn ph to pa of 1

    --set the in-tube flag
    !this.inTube = true
    !this.distanceActive = true

    if (!this.softType inset('TT36','TT42')) then
      !this.activateOffset(false)
    endif

  else

    --currently we are supporting the tube. Modify to support the elbo
    --first move the trunni so that it is connected to the elbo
    var !ldir pl dir of $!<this.elementToSupport> wrt /*
    !direction = !this.trunni.hdir.opposite().wrt( /* )

    --drag move through p0 of the elbo (note cannot do this at trunni level so we have to reconnect the head after the move
    !!ce = !this.tranci
    drag move $!ldir thro p0 of $!<this.elementToSupport>
    --now drag move thro p arrive
    drag move $!direction thro pa of $!<this.elementToSupport>

    --reconnect the trunni head to the arrive of the tranci
    !!ce = !this.trunni
    conn ph to pa of 1

    --set the in-tube flag
    !this.inTube = false
    !this.distanceActive = false

    if (!this.softType inset('TT36','TT42')) then
      !this.activateOffset(true)
      -- make the offset zero
      !!mdsSupportEditor.dimCont1.setDimensionValueById('offset',0)
      !this.modOffset()
    endif
    -- !this.setTrunLength(false)

  endif

endmethod

----------------------------------------------------------------------
--
-- Method:      .activateOffset
--
-- Description: if the trunnion has an offset this swaps it when we move
-- between tube and elbow
--
----------------------------------------------------------------------
define method .activateOffset(!action is BOOLEAN)

  if (!action) then
    !!mdsSupportEditor.dimCont1.setDimensionValueById('offset', !this.ssrefe.ssrefe.desp[4])
    !!mdsSupportEditor.dimCont1.setDimensionActive('offset', true)
  else
    !!mdsSupportEditor.dimCont1.setDimensionValueById('offset', 0)
    !!mdsSupportEditor.dimCont1.setDimensionActive('offset', false)
  endif

endmethod


----------------------------------------------------------------------
--
-- Method:      teeInTube
--
-- Description: if the trunnion is supporting a tee change the hierarchy so that it is supporting tube and visa-versa
--
----------------------------------------------------------------------
define method .teeInTube()


  --revisit I think that for all tees the offset gadget should not be enabled but this is more specific
  if (!this.softType inset('TT36','TT37','TT42')) then
    !this.activateOffset(false)
  endif
  if (!this.inTube.not()) then

    --we are supporting the component tee. Modify to support the tube
    var !ldir pl dir of $!<this.elementToSupport> wrt /*
    var !lpos pl pos of $!<this.elementToSupport> wrt /*

    --create a pointvector distanced along the line by the inTubeDist

    var !p0pos p0 pos of $!<this.elementToSupport> wrt /*

    if (!!mdsSupportEditor.distance.val) then
      !pvLeave = object POINTVECTOR(!lpos.position(), !ldir.direction())
    else
      !pvLeave = object POINTVECTOR(!p0pos.position(), !ldir.direction())
    endif

    --!pvLeave = object POINTVECTOR(!lpos.position(), !ldir.direction())
    !pvLeave = !pvLeave.offset(!this.inTubeDist)

    --drag move through !pvLeavePos (note cannot do this at trunni level so we have to reconnect the head after the move
    !!ce = !this.tranci
    !pvLeavePos = !pvLeave.position
    drag move $!ldir thro $!pvLeavePos

    --reconnect the trunni head to the arrive of the tranci
    !!ce = !this.trunni
    conn ph to pa of 1

    --set the in-tube flag
    !this.inTube = true
    !this.distanceActive = true


  else

    --we are supporting the tube. Modify to support the tee
    --first move the trunni so that it is connected to the tee
    var !ldir pl dir of $!<this.elementToSupport> wrt /*

    --drag move through p0 of the tee (note cannot do this at trunni level so we have to reconnect the head after the move
    !!ce = !this.tranci
    drag move $!ldir thro p0 of $!<this.elementToSupport>

    --reconnect the trunni head to the arrive of the tranci
    !!ce = !this.trunni
    conn ph to pa of 1

    --set the in-tube flag
    !this.inTube = false
    !this.distanceActive = false

  endif

endmethod

----------------------------------------------------------------------
--
-- Method:      reduInTube
--
-- Description: if the trunnion is supporting a reducer change the hierarchy so that it is supporting tube and visa-versa
--
----------------------------------------------------------------------
define method .reduInTube()

  !!ce = !this.trunni

  --revisit I think that for all tees the offset gadget should not be enabled but this is more specific
  if (!this.softType inset('TT36','TT37','TT42')) then
    !this.activateOffset(false)
  endif

  --find the 'up' direction of an eccentric reducer
  if (!this.elementABore.lt(!this.elementLBore)) then
    !sbPos = !this.elementAPos
    !sbDir = !this.elementAdir.opposite()
    !lbPos = !this.elementLPos
    !lbDir = !this.elementLdir.opposite()
  else
    !sbPos = !this.elementLPos
    !sbDir = !this.elementLdir.opposite()
    !lbPos = !this.elementAPos
    !lbDir = !this.elementAdir.opposite()
  endif

  !sbPv = object POINTVECTOR(!sbPos, !sbDir)
  !lbPv = object POINTVECTOR(!lbPos, !lbDir)
  !lbPvInter = !sbPv.intersection(!lbPv.plane())
  if (!lbPvInter.eq(!lbPv.position)) then
    !concentric = true
  else
    !concentric = false
    !upDir = !lbPvInter.direction(!lbPv.position)
  endif
  if (!this.inTube.not()) then

    --we are supporting the component elbo. Modify to support the tube
    --first move the trunni so that it is in-line with p0 of the elbo
    var !ldir pl dir of $!<this.elementToSupport> wrt /*
    var !lpos pl pos of $!<this.elementToSupport> wrt /*

    --drag move through pl of the reducer if the reducer is eccentric (note cannot do this at trunni level so we have to reconnect the head after the move)
    !!ce = !this.tranci
    if (!concentric.not()) then
      drag move $!upDir thro pl of $!<this.elementToSupport>
    endif

    var !p0pos p0 pos of $!<this.elementToSupport> wrt /*

    if (!!mdsSupportEditor.distance.val) then
      !pvLeave = object POINTVECTOR(!lpos.position(), !ldir.direction())
    else
      !pvLeave = object POINTVECTOR(!p0pos.position(), !ldir.direction())
    endif

    !pvLeave = !pvLeave.offset(!this.inTubeDist)

    !pvLeavePos = !pvLeave.position
    drag move $!ldir thro $!pvLeavePos

    --reconnect the trunni head to the arrive of the tranci
    !!ce = !this.trunni
    conn ph to pa of 1

    --set the in-tube flag
    !this.inTube = true
    !this.distanceActive = true
  else

    --we are supporting the tube. Modify to support the elbo
    --first move the trunni so that it is connected to the elbo
    var !ldir pl dir of $!<this.elementToSupport> wrt /*

    --drag move through p0 of the elbo (note cannot do this at trunni level so we have to reconnect the head after the move)
    !!ce = !this.tranci
    !connectionPoint = !this.getTrunniConnectionPoint()
    drag move $!ldir thro $!connectionPoint
    --now drag move thro p arrive
    drag move $!upDir thro $!connectionPoint

    --reconnect the trunni head to the arrive of the tranci
    !!ce = !this.trunni
    conn ph to pa of 1

    --set the in-tube flag
    !this.inTube = false
    !this.distanceActive = false

  endif

endmethod

----------------------------------------------------------------------
--
-- Method:      calculateMakeupOffset
--
-- Description: calculate the makeup piece offset value
--
----------------------------------------------------------------------
define method .calculateMakeupOffset(!reducer is DBREF) is REAL

  if (!this.inTube) then
    !onType = 'TUBE'
  else
    !onType = !this.onType
  endif

  -- Work out the adjustment needed for the exact length to be added to the iso
  if (!this.elementToSupport.type.eq('BRAN')) then
    !od = !this.elementToSupport.phod
  else
    !od = pl od of $!<this.elementToSupport>
  endif
  !tOd = !reducer.lodiam
  if (!onType.eq('ELBO')) then
    !radius = !this.elementAPos.distance(!this.elementToSupport.pPosition[0].wrt( /* ))
    !hyp = !radius + (!od / 2)
    !side1 = !radius + (!tOd / 2)
    !offset = (sqrt ((!hyp * !hyp) - (!side1 * !side1)))
  else
    -- Find smallest bore if redu
    if (!onType.eq('REDU')) then
      !rb1 = pod 1 of $!<this.elementToSupport>
      !rb2 = pod 2 of $!<this.elementToSupport>
      if (!rb1.gt(!rb2)) then
        !od = !rb2
      else
        !od = !rb1
      endif
    endif
    !hyp = (!od / 2)
    !side1 = (!tOd / 2)
    !offset = (sqrt ((!hyp * !hyp) - (!side1 * !side1)))
  endif

  return !offset

endmethod

----------------------------------------------------------------------
--
-- Method:      modWeldInsertionLength
--
-- Description: To deal with special case of the Insertion Length property of a collar
--
----------------------------------------------------------------------
define method .modWeldInsertionLength(!passedCollar is DBREF)
  --AC20 has a property ILEN which is the Insertion Length (this is despar 4)
  --The value of this despar changes the position of the p1 of the collar PLATE which causes the tubi to
  --extend past the collar, thus the Insertion Length.
  --This, however, is no longer required as we now use the Allowance attribute of the previous WELD to hold this value.
  --Because of this we need to set the desp 4 on the PLATE to 0mm and instead assign the value to the Allowance on the weld.
  !ce = !!ce

  !!ce = !passedCollar
  !allow = desp
  --Set despar on PLATE to 0mm
  desp n4 0mm
  prev
  --Assign value Allowance attribute of previous WELD
  allow $!allow[4]

  !!ce = !ce
endmethod

----------------------------------------------------------------------
--
-- Method:      setSSREFEDespars
--
-- Description: Set the despars for the trunnion on the SSREFE
--
----------------------------------------------------------------------
define method .setSSREFEDespars(!ssrefe is DBREF)

  !ref = !!ce
  -- In order to create the correct code, store appropriate values in despar attribute
  -- 1 is the head bore of bran
  -- 2 is the branch bore of bran
  -- 3 dummy
  -- 4 dummy
  -- 5 is the height
  -- 6 is the install type
  !suppoName = !this.suppo.name
  !!ce = !ssrefe

  !dp1 = !this.trunni.hbore.string().before('mm')
  !dp2 = !this.trunni.tBore.string().before('mm')

  -- dp3 will be all the alpha chars
  !dp3 = ''

  !replacementName = !!mdsGetReplacementName(!this.softType)
  do !loop from 1 to !replacementName.length()
    !char = !replacementName.substring(!loop,1)
    !typeTest = 0
    !real.delete()
    handle ANY
    endhandle
    !unusedTest = !typeTest + !char.real()
    handle NONE
      !real = FALSE
    elsehandle ANY
      !real = TRUE
    endhandle
    if (!real) then
      !dp3 = !dp3 + !char
    else
      break
    endif
  enddo

  if (!!ce.styp.eq('TT14')) then
    !dp3 = 'A'
  elseif (!!ce.styp.eq('TT15')) then
    !dp3 = 'B'
  elseif (!!ce.styp.eq('TT16')) then
    !dp3 = 'C'
  endif

  !dp5 = !!mdsSupportEditor.dimCont1.getDimensionValueById('hei')
  !dp6 = !replacementName.substring(6)

  -- if is a TT07 the second bore is found from the makeup piece leave
  if (!!ce.styp inset('TT06','TT07','TT10','TT11','TT20','TT22','TT28')) then
    !makeup = !suppoName  + '/MAKEUP'
    $!makeup
    !dp2 = !!ce.ppbore[2].string().before('mm')
    same
  endif

  if (!dp6.eq('').or(!dp6.eq('-'))) then
    !dp6 = 'A'
  endif

  if (!!ce.styp inset('TT14','TT15','TT16')) then
    despar $!dp1 $!dp2 $!dp3 0mm $!dp5
  elseif (!!ce.styp inset('TT17','TT24','TT31','TT32')) then
    despar $!dp1 $!dp2 $!dp5
  else
    despar $!dp1 $!dp2 0mm 0mm $!dp5 $!dp6
  endif


  --set dummy leg offset despar if required
  if (!this.dummyLegOffset.set()) then
    desp num 4 $!this.dummyLegOffset
  endif

  --set clearance value
  if (!!ce.styp inset('TT25','TT33','TT34')) then
    !clearance = !!mdsSupportEditor.dimCont1.getDimensionValueById('clea')
    despar $!dp1 $!dp2 $!dp5 $!clearance
  endif


  !!ce = !ref
endmethod

----------------------------------------------------------------------
--
-- Method:      getOrthogDir
--
-- Description: Get the orthogonal direction for HORI and VERT trunnions
--
----------------------------------------------------------------------
define method .getOrthogDir() is DIRECTION
  !dir2 = object DIRECTION('U wrt /*')
  !dir3 = object DIRECTION('D wrt /*')

  if (!this.elementLdir.neq(!dir2) AND !this.elementLdir.neq(!dir3)) then
    !dir1 = !this.elementLdir.wrt(/* )
    !trunDir = !dir1.orthogonal(!dir2)
  else
    !trunDir = object DIRECTION('n wrt /*')
  endif

  return !trunDir
endmethod

----------------------------------------------------------------------
--
-- Method:      posStanchion
--
-- Description: Position stanchions such as TT25, TT33 and TT34 (alternative to setTrunLength)
--
----------------------------------------------------------------------
define method .posStanchion(!id is BOOLEAN)
  -- Find current units and set the distance unit to metric
  !currentUnits = !!kGetSetUnits()

  --if !id is true then we are using pick to identify the element to set to
  if (!id) then

    !!mdsIdPick = object MDSIDPICK()
    !picked = !!mdsIdPick.pick(4, 'Identify element to set trunnion to')

    if (unset(!picked)) then
      !!kRestoreUnits(!currentUnits)
      return
    endif

    !horiz = TRUE
    !idEle = !picked
    if (!picked.type eq 'GENSEC') then
      !start = poss wrt world
      !end   = pose wrt world
      -- Check Identified Section is Horizontal
      if (!start.up.nint() neq !end.up.nint()) then
        !horiz = FALSE
      else
        !horiz = TRUE
      endif
    endif
    if (!horiz.not()) then
      !!alert.error('Identified element is not horizontal')
      !!kRestoreUnits(!currentUnits)
      return
    endif

    -- Check Identified GENSEC is 'Above' or 'Below' Trunnion and is Suitable for Trunnion
    !!ce = !!mdsSupportEditor.support.datum
    !datum = !!mdsSupportEditor.support.datum
    !ddir = d
    !test = !!mdsreltodatum(!datum, !idEle, !ddir, 0)
    if (!test[1].upcase() eq 'BELOW') then
      -- If 'BELOW' is returned - it is OK to Proceed
    elseif (!test[1].upcase() eq 'ABOVE') then
      !!ce = !datum
      pin 6 at $!datum
      pin 5 at pl of bend 1
      pin 6 dir d
      if (defined(!test[2])) then
        pin 6 $!test[2]
      else
        pin 6 clear 0 inf $!idEle
      endif
      var !u1 pin6 u
      var !u2 pin5 u
      if (!u1.real() gt !u2.real()) then
        !!alert.error('Identified element is too high to support from')
        !!kRestoreUnits(!currentUnits)
        return
      endif
    else
      !horiz = FALSE
      !!alert.error('Identified element is unsuitable to support from')
      !!kRestoreUnits(!currentUnits)
      return
    endif

    --------------------------------------------------------------------------------
    -- Position Trunnion ONTOP or UNDER the Identified Steel Section
    !!ce  = !this.basePlate
    if (defined(!test[2])) then
      pos pl $!test[2]
    else
      pos pl clear 0 inf $!idEle
    endif
    -- calculate the length and reset it
    !bend = prev bend
    !trunLen  = !!ce.lpos.distance(!bend.position)
  else
    !!ce  = !this.basePlate
    !trunLen = !!mdsSupportEditor.dimCont1.getDimensionValueById('hei')
    pos pl dist $!trunLen from prev
  endif

  !min = !this.mdsTrunnionObject.minLength
  !max = !this.mdsTrunnionObject.maxLength

  -- Now check if the LENGTH required is outside the MINIMUM and MAXIMUM TRUNNION LENGTH values
  if (!trunLen lt !min) then
    !uMin = !min.string(!!distanceFMT)
    !uTrunLen = !trunLen.string(!!distanceFMT)
    !!alert.error(|Length stated ($!uTrunLen) is less than the minimum allowed. Setting length to minimum of $!uMin|)
    pos pl dist $!min from prev
    !trunLen = !min
  elseif (!trunLen gt !max) then
    !uMax = !max.string(!!distanceFMT)
    !uTrunLen = !trunLen.string(!!distanceFMT)
    !!alert.error(|Length stated ($!uTrunLen) is more than the maximum allowed. Setting length to maximum of $!uMax|)
    pos pl dist $!max from prev
    !trunLen = !max
  endif

  prompt dismiss

  !!ce = !this.trunni
  conn pt to last mem

  --apply the height as measured from current geometry to the dimension control box
  !!mdsSupportEditor.dimCont1.setDimensionValueById('hei', !trunLen.real())

  --Ensure the stanchion is facing the correct way
  !this.modStanchion()

  -- Set the despars on the ssrefe for the trunnion
  !this.setSSREFEDespars(!this.ssrefe.ssrefe)

  -- Reset Pins
  !!mdsResetPins (8,9)
  !this.drawAids()

  !!kRestoreUnits(!currentUnits)

endmethod

----------------------------------------------------------------------
--
-- Method:      getOrthogDir
--
-- Description: Get the orthogonal direction for HORI and VERT trunnions
--
----------------------------------------------------------------------
define method .getOrthogDir() is DIRECTION
  !dir2 = object DIRECTION('U wrt /*')
  !dir3 = object DIRECTION('D wrt /*')

  if (!this.elementLdir.neq(!dir2) AND !this.elementLdir.neq(!dir3)) then
    !dir1 = !this.elementLdir.wrt(/* )
    !trunDir = !dir1.orthogonal(!dir2)
  else
    !trunDir = object DIRECTION('n wrt /*')
  endif

  return !trunDir
endmethod

----------------------------------------------------------------------
--
-- Method:      modStanchion
--
-- Description: For stanchions TT25, TT33 and TT34 modifies geometry based on the clearance direction values on the form
--
----------------------------------------------------------------------
define method .modStanchion()

  !direction = !!mdsSupportEditor.trunnionDirection.val.direction()
  !trunLen = !!mdsSupportEditor.dimCont1.getDimensionValueById('hei')
  !clear = !!mdsSupportEditor.dimCont1.getDimensionValueById('clea')

  !minClear = !this.mdsTrunnionObject.userdps[4].min
  !maxClear = !this.mdsTrunnionObject.userdps[4].max

  -- Check if the clearance required is outside the minimum and maximum values
  if (!clear lt !minClear) then
    !uMin = !minClear.string(!!distanceFMT)
    !uClear = !clear.string(!!distanceFMT)
    !!alert.error(|Clearance stated ($!uClear) is less than the minimum allowed. Setting to minimum value of $!uMin|)
    !clear = !minClear
    !!mdsSupportEditor.dimCont1.setDimensionValueById('clea', !clear)
  elseif (!clear gt !maxClear) then
    !uMax = !maxClear.string(!!distanceFMT)
    !uClear = !clear.string(!!distanceFMT)
    !!alert.error(|Clearance stated ($!uClear) is more than the maximum allowed. Setting to maximum value of $!uMax|)
    !clear = !maxClear
    !!mdsSupportEditor.dimCont1.setDimensionValueById('clea', !clear)
  endif

  --Get outside diameter from tranci
  !this.trunni.hdir = !direction
  !!ce = !this.tranci
  !od = pa od

  --Orientate to a different p-point for AT52B
  !pPoint = 'p3'
  if (!this.tranci.sType.eq('AT52B')) then
    !pPoint = 'p5'
  endif

  if (!!ce.compref.type.eq('BRAN')) then
    conn and $!pPoint is hdir of compref
    handle (7, 3)
      fconn and $!pPoint is hdir of compref
    endhandle
  else
    conn and $!pPoint is pl of compref
    --revisit, this should not be the case
    handle (7, 3)
      fconn and $!pPoint is pl of compref
    endhandle
  endif

  TRREDU 1 of $!this.trunni
  connect and p3 is d
  handle (7,3)
    fconnect and p3 is d
  endhandle

  --Position bend with the given clearance from the pipe
  BEND 1 of $!this.trunni
  conn and pl is d
  !clearDistance = !clear + !od / 2
  clear $!clearDistance behind p0 of $!this.tranci

  --Position base plate inline with the bend
  !!ce = !this.basePlate
  ori pl is d
  distance 500
  pos pl dist $!trunLen from prev

  --Connect the trunnion tail to the last member
  !!ce = !this.trunni
  conn pt to pl of last mem

  -- Set the despars on the ssrefe for the trunnion
  !this.setSSREFEDespars(!this.ssrefe.ssrefe)

endmethod

----------------------------------------------------------------------
--
-- Method:      .initialisePlates
--
-- Description: for bores and stypes where there are multiple bores and multiple plates
-- a new gadget for plates has been added. The bore list is still a list of bores
-- but the current bore is now linked to the plate selection
--
----------------------------------------------------------------------
define method .initialisePlates(!bores is ARRAY, !plates is ARRAY)

  !!mdsSupportEditor.boreList  = !bores
  !bores.unique()
  !!mdsSupportEditor.plateList = !plates
  !plates.unique()
  !!mdsSupportEditor.availableTrunnionBores.dtext = !bores
  !!mdsSupportEditor.availableTrunnionPlates.dtext = !plates
  !!mdsSupportEditor.availableTrunnionPlates.visible = true
  !!mdsSupportEditor.availableTrunnionPlates.active = true
  -- make the gadget active for a test later
  !!mdsSupportEditor.availableTrunnionPlates.active = true

endmethod

----------------------------------------------------------------------
--
-- Method:      .setCurrentBorePlate
--
-- Description: matches the current plate and bore combination
--
----------------------------------------------------------------------
define method .setCurrentBorePlate(!bore is STRING,!plate is STRING)is REAL

  !buildindex = 0
  do !a index !!mdsSupportEditor.boreList
    if (!bore.eq(!!mdsSupportEditor.boreList[!a]).and(!plate.eq(!!mdsSupportEditor.plateList[!a]))) then
      !buildindex = !a
      break
    endif
  enddo

  if (!buildindex.eq(0)) then
    -- we haven't found the right combination so return the default
    !buildindex = 1
  endif

  return !buildindex

endmethod

----------------------------------------------------------------------
--
-- Method:      switchAT29
--
-- Description: When switching a trunnion between in elbow and in tube, switch AT29 from A to B and vice versa
--
----------------------------------------------------------------------
define method .switchAT29(!styp is STRING)

  !ce = !!ce
  !hardType = !styp.substring(1, !styp.length() - 1)
  if (!this.inTube.not() AND !this.elementToSupport.type.neq('ELBO')) then
    !!alert.message('Unable to switch head component ($!styp). This component can only support an ELBO or TUBE. Reverting to PIPE-REST')
    !newSoftType = 'PIPE-REST'
  elseif (!this.inTube.not()) then
    !newSoftType = !hardType + 'B'
    !compTypeRef = /MDS/ATTA/DEFAULTS/$!newSoftType/HCOMPTYPE
    !compTypesAllowed = !compTypeRef.pprop.after(|( '|).before(|' )|).split(',')
    if (!compTypesAllowed[2].boolean().not()) then
      !!alert.message('Unable to switch head component to type ($!newSoftType). This component has been restricted for creation on an ELBO. Reverting to PIPE-REST')
      !newSoftType = 'PIPE-REST'
    endif
  else
    !newSoftType = !hardType + 'A'
    !compTypeRef = /MDS/ATTA/DEFAULTS/$!newSoftType/HCOMPTYPE
    !compTypesAllowed = !compTypeRef.pprop.after(|( '|).before(|' )|).split(',')
    if (!compTypesAllowed[1].boolean().not()) then
      !!alert.message('Unable to switch head component to type ($!newSoftType). This component has been restricted for creation on TUBE. Reverting to PIPE-REST')
      !newSoftType = 'PIPE-REST'
    endif
  endif

  !!ce = !this.tranci
  select spref with styp '$!newSoftType' spec /MDS-Ancillaries
  handle any
    !!alert.message('Unable to switch head component ($!styp) reverting to PIPE-REST')
    !newSoftType = 'PIPE-REST'
    select spref with styp '$!newSoftType' spec /MDS-Ancillaries
  endhandle
  !!mdsSetDesPars(!!ce)

  !ancillaryActionsObject = object MDSANCILLARYACTIONS()
  !ancillaryActionsObject.mdsAncillaryActions(!newSoftType, !this.tranci, !this.tranci.compref)

  -- Use dummy string to pass stype, as it will be done in addSuppToTab
  !nullString = ''
  using namespace 'Aveva.Supports.Presentation.DimensionControl'
  !!mdsSupportEditor.addSuppToTab(!this.tranci,!nullString,!!mdsSupportEditor.dimCont2,!!mdsSupportEditor.fDimCont2)
  !!ce = !ce

endmethod

----------------------------------------------------------------------
--
-- Method:      dimensionChangeAN640
--
-- Description: Modifies the dimensions of AN640-AN670 ancillary if the plate it is attached to is modified
--
----------------------------------------------------------------------
define method .dimensionChangeAN640(!modifiedElement is DBREF)

  !an640s = ''
  do !num from 640 to 700
    !an640s = !an640s + 'AN$!num'
  enddo

  !foundAN640 = object DBREF()
  if (!modifiedElement.:MDSLinkRef.set()) then
    do !mdsLinkRef values !modifiedElement.:MDSLinkRef
      if (!an640s.match(!mdsLinkRef.styp).neq(0)) then
        !foundAN640 = !mdsLinkRef
        break
      endif
    enddo
  endif

  if (!foundAN640.unset()) then
    return
  endif

  !ce = !!ce
  !!ce = !foundAN640

  -- Use the PWID, PLEN and PTHK values from the baseplate
  !pWid = prop PWID of $!modifiedElement
  handle NONE
    var !whichData coll all data with dkey eq 'PLEN' for dtre of ce
    !despNum = !whichData[1].dbref().num
    handle NONE
      desp N$!despNum  $!pWid
    elsehandle ANY
    endhandle
  elsehandle ANY
  endhandle
  !pLen = prop PLEN of $!modifiedElement
  handle NONE
    var !whichData coll all data with dkey eq 'PWID' for dtre of ce
    !despNum = !whichData[1].dbref().num
    handle NONE
      desp N$!despNum  $!pLen
    elsehandle ANY
    endhandle
  elsehandle ANY
  endhandle
  !pThk = prop PTHK of $!modifiedElement
  handle NONE
    var !whichData coll all data with dkey eq 'PTHK' for dtre of ce
    !despNum = !whichData[1].dbref().num
    handle NONE
      desp N$!despNum  $!pThk
    elsehandle ANY
    endhandle
  elsehandle ANY
  endhandle

  !!ce = !ce
endmethod

----------------------------------------------------------------------
--
-- Method:      fromPreliminarySupport
--
-- Description: Alternative method to calling mdscreatesuppelement if a trunnion is from a preliminary
--
----------------------------------------------------------------------
define method .fromPreliminarySupport() is STRING
  !ce = !!ce

  --DAC/Locked check on the element to support is required
  !dacCheck = object MDSDAC(!this.elementToSupport, 'DACISS', '')
  if (!dacCheck.result.not()) then
    --If element has failed locked/DAC check then return
    !!alert.error(!dacCheck.errorText)
    !!ce = !ce
    return ''
  endif

  !suppName = !!mdsGetSuppName(!this.elementToSupport)

  -- Get information from selected element
  !!ce = !this.elementToSupport
  !bore = !!ce.lbore
  !direction = !!ce.ldir.wrt(/* )
  !position = !!ce.pPos[0].wrt(/* )

  !elementToSupport = !this.elementToSupport.compref

  !!ce = !this.elementToSupport
  SUPC
  delete SUPC
  handle ANY
    !!ce = !ce
    return ''
  endhandle

  new TRUNNI
  !!ce.hbor = !bore
  !!ce.tbor = !bore
  !!ce.hdir = !direction
  !!ce.tdir = !direction.opposite()
  !!ce.hpos = !position
  !!ce.tpos = !position
  !!ce.hcon = 'ATT'
  !!ce.tcon = 'ATT'

  new TRANCI
  compref $!elementToSupport

  !this.elementToSupport = !elementToSupport

  return !suppName
endmethod
